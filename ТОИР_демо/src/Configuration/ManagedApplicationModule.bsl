
// СтандартныеПодсистемы

#Область ОписаниеПеременных

// Хранилище глобальных переменных.
//
// ПараметрыПриложения - Соответствие - хранилище переменных, где:
//   * Ключ - Строка - имя переменной в формате "ИмяБиблиотеки.ИмяПеременной";
//   * Значение - Произвольный - значение переменной.
//
// Инициализация (на примере СообщенияДляЖурналаРегистрации):
//   ИмяПараметра = "СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации";
//   Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
//     ПараметрыПриложения.Вставить(ИмяПараметра, Новый СписокЗначений);
//   КонецЕсли;
//  
// Использование (на примере СообщенияДляЖурналаРегистрации):
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"].Добавить(...);
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"] = ...;
Перем ПараметрыПриложения Экспорт; // Хранилище глобальных переменных.

#КонецОбласти

// Конец СтандартныеПодсистемы

#Область ОбработчикиСобытий
Процедура ПередНачаломРаботыСистемы(Отказ)
	
	// Вызывает процедуру проверки лицензии
	торо_ПередНачаломРаботыСистемы(Отказ);
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы();
	// Конец СтандартныеПодсистемы
	
	// ИнтернетПоддержкаПользователей
	ИнтернетПоддержкаПользователейКлиент.ПередНачаломРаботыСистемы();
	// Конец ИнтернетПоддержкаПользователей

	ДоступностьРоли = торо_ОбщегоНазначенияВызовСервера.ДоступностьРолиТОиРБазовыеПрава();	
	Если Не ДоступностьРоли Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Вам не назначена роль ""Базовые права ТОиР"", использование подсистемы ""ТОиР"" невозможно. Запуск конфигурации невозможен.'"));
		Возврат;	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы();
	// Конец СтандартныеПодсистемы
	
	ЗапретитьОткрытиеНесколькихСеансов = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
	"НастройкиТОиР",
	"ЗапретитьОткрытиеНесколькихСеансов",
	Истина);
	
	Если торо_ОбщегоНазначенияВызовСервера.ЕстьПравоУКонфигурации("АктивныеПользователи") Тогда
		Если ЗапретитьОткрытиеНесколькихСеансов Тогда
			ОткрываетсяНеПервыйСеанс = торо_ОбщегоНазначенияВызовСервера.ПроверитьОткрытиеНесколькихСеансов();
			
			Если ОткрываетсяНеПервыйСеанс Тогда
				ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Пользователем с таким именем уже выполнен вход в систему'"));
				ЗавершитьРаботуСистемы(Ложь);
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить("НастройкиТОиР", "ПолучатьУведомленияВВидеВсплывающихПодсказок", Ложь) Тогда
		
		ПодключитьОбработчикОжидания("ПоказатьВсплывающиеСообщения", 15);
		
	КонецЕсли;
	
	//ПоказыватьПутеводитель = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить("НастройкиТОиР", "ПоказыватьПутеводительПоДемоБазе", Истина); 

	//Если ПоказыватьПутеводитель Тогда
	//	Попытка
	//		ОткрытьФорму("Обработка.торо_ПутеводительПоДемонстрационнойБазе.Форма.Форма");
	//	Исключение
	//		СообщениеОбОшибке = Новый СообщениеПользователю;
	//		СообщениеОбОшибке.Текст = ОписаниеОшибки();
	//		СообщениеОбОшибке.Сообщить();
	//	КонецПопытки;
	//КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения);
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура торо_ПередНачаломРаботыСистемы(Отказ)
	
	// Пример проверки версии компоненты
	НеобходимаяВерсия = "2.1.8";
	Если НЕ слкМенеджерЗащиты.ПроверитьВерсиюКомпонентыСЛК(НеобходимаяВерсия) Тогда
		торо_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Устаревшая версия СЛК, для работы конфигурации необходима СЛК %1 или выше.'"),НеобходимаяВерсия));
	КонецЕсли;
	
	ОписаниеОшибки = "";
	Если Не слкМенеджерЗащиты.ПроверитьЛицензиюСеанса("28DD", ОписаниеОшибки) Тогда
		торо_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеОписаниеОшибки(ОписаниеОшибки));
	КонецЕсли;		

КонецПроцедуры

Функция КраткоеОписаниеОшибки(ОписаниеОшибки)
	
	ТекстОшибки = ОписаниеОшибки;
	Позиция = СтрНайти(ТекстОшибки, "}:");
	Если Позиция > 0 Тогда
		ТекстОшибки = СокрЛП(Сред(ТекстОшибки, Позиция + 2, СтрДлина(ТекстОшибки)));
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

#КонецОбласти