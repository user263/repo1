
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Элементы.СообщениеИНН.Видимость = Ложь;
	Элементы.СообщениеКПП.Видимость = Ложь;
	
	// Заполним значения реквизитов
	РеквизитыФормы = ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл 
		Если Параметры.Свойство(РеквизитФормы.Имя) Тогда
			ЭтаФорма[РеквизитФормы.Имя] = Параметры[РеквизитФормы.Имя];
		КонецЕсли;
	КонецЦикла;
	
	УстановитьЦветЗаголовкаПоляОшибки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КПППриИзменении(Элемент)
	ПриИзмененииКПП();
	ПроверитьКорректностьКПП();
КонецПроцедуры

&НаКлиенте 
Процедура ИННПриИзменении()
	// Проверим корректность заполнения
	ПроверитьКорректностьИНН();
	
	// Заполним КПП
	Если ПустаяСтрока(ИНН) И НЕ ПустаяСтрока(КПП) Тогда
		КПП = "";
	КонецЕсли;	
	
	Если СтрДлина(ИНН) >= 4 
	 И ПустаяСтрока(КПП) Тогда
		
		КПП = Лев(ИНН, 4) + "01001";
		
		ПриИзмененииКПП();
		
		ПроверитьКорректностьКПП();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	ПараметрыЗакрытия = Новый Структура("
		|ИНН, 
		|КПП, 
		|Префикс");
	ЗаполнитьЗначенияСвойств(ПараметрыЗакрытия, ЭтаФорма);
	
	Закрыть(ПараметрыЗакрытия);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаКлиенте
Процедура ПриИзмененииКПП()
	Если СтрДлина(КПП) >= 4 Тогда
		
		КодНалоговогоОргана = Лев(КПП, 4);
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте 
Процедура ПроверитьКорректностьИНН()
	СообщениеОшибки = "";
	
	ИННСоответствуетТребованиям = ПустаяСтрока(ИНН) ИЛИ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, ЮридическоеФизическоеЛицо = 0, СообщениеОшибки);
	
	ПозицияДолжен = Найти(СообщениеОшибки, НСтр("ru='должен'"));
	СообщениеОшибки = СтрЗаменить(СообщениеОшибки, НСтр("ru='должен'"), НСтр("ru='Должен'"));
	СообщениеОшибки = Прав(СообщениеОшибки, СтрДлина(СообщениеОшибки) - ПозицияДолжен + 1);
	ПозицияПервойТочки = Найти(СообщениеОшибки, ".");
	СообщениеОшибки = Лев(СообщениеОшибки, ПозицияПервойТочки);
	
	СообщениеИНН = СообщениеОшибки;
	
	Если Не ИННСоответствуетТребованиям Тогда
		Элементы.СообщениеИНН.Видимость = Истина;
		Элементы.ИНН.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
		Элементы.СообщениеИНН.ЦветТекста = ЦветЗаголовкаПоляОшибки;
	Иначе
		Элементы.СообщениеИНН.Видимость = Ложь;
		Элементы.ИНН.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
		Элементы.СообщениеИНН.ЦветТекста = ЦветЗаголовкаАвто;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте 
Процедура ПроверитьКорректностьКПП()
	СообщениеОшибки = "";
	
	КППСоответствуетТребованиям = ПустаяСтрока(КПП) ИЛИ РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КПП, СообщениеОшибки);
	
	ПозицияДолжен = Найти(СообщениеОшибки, НСтр("ru='должен'"));
	СообщениеОшибки = СтрЗаменить(СообщениеОшибки, НСтр("ru='должен'"), НСтр("ru='Должен'"));
	СообщениеОшибки = Прав(СообщениеОшибки, СтрДлина(СообщениеОшибки) - ПозицияДолжен + 1);
	ПозицияПервойТочки = Найти(СообщениеОшибки, ".");
	СообщениеОшибки = Лев(СообщениеОшибки, ПозицияПервойТочки);
	
	СообщениеКПП = СообщениеОшибки;
	
	Если Не КППСоответствуетТребованиям Тогда
		Элементы.СообщениеКПП.Видимость = Истина;
		Элементы.КПП.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
		Элементы.СообщениеКПП.ЦветТекста = ЦветЗаголовкаПоляОшибки;
	Иначе
		Элементы.СообщениеКПП.Видимость = Ложь;
		Элементы.КПП.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
		Элементы.СообщениеКПП.ЦветТекста = ЦветЗаголовкаАвто;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьЦветЗаголовкаПоляОшибки()
	ЦветЗаголовкаПоляОшибки = ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветЗаголовкаАвто = ЦветаСтиля.ЦветТекстаФормы;
КонецПроцедуры

#КонецОбласти

#КонецОбласти
