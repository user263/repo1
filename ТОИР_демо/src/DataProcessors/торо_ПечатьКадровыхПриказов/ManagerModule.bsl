#Область СлужебныеПроцедурыИФункции

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Т1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
						КоллекцияПечатныхФорм,
						"ПФ_MXL_Т1", НСтр("ru='Приказ о приеме'"),
						ПолучитьТабличныйДокументПриказаТ1(УправлениеПечатью.МакетПечатнойФормы("Обработка.торо_ПечатьКадровыхПриказов.ПФ_MXL_Т1"), МассивОбъектов, ОбъектыПечати), ,
						"Обработка.торо_ПечатьКадровыхПриказов.ПФ_MXL_Т1");
	КонецЕсли;
					
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Т8") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
						КоллекцияПечатныхФорм,
						"ПФ_MXL_Т8", НСтр("ru='Приказ об увольнении'"),
						ПолучитьТабличныйДокументПриказаТ8(УправлениеПечатью.МакетПечатнойФормы("Обработка.торо_ПечатьКадровыхПриказов.ПФ_MXL_Т8"), МассивОбъектов, ОбъектыПечати), ,
						"Обработка.торо_ПечатьКадровыхПриказов.ПФ_MXL_Т8");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Т5") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
						КоллекцияПечатныхФорм,
						"ПФ_MXL_Т5", НСтр("ru='Приказ о переводе'"),
						ПолучитьТабличныйДокументПриказаТ5(УправлениеПечатью.МакетПечатнойФормы("Обработка.торо_ПечатьКадровыхПриказов.ПФ_MXL_Т5"), МассивОбъектов, ОбъектыПечати), ,
						"Обработка.торо_ПечатьКадровыхПриказов.ПФ_MXL_Т5");
	КонецЕсли;	
	
КонецПроцедуры								

#Область ПроцедурыФункцииПечатиКадровыхДанных

// Процедура печати документа.
// Возвращает табличный документ - сформированную печатную форму приказа о приеме или увольнении.
//
// Параметры:
//	МассивОбъектов - массив сотрудников.
//  ОбъектыПечати  - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в
//                   которой был выведен объект).
//	ВидПриказа     - "ПриказОПриеме" или "ПриказОбУвольнении".
//
// Возвращаемое значение:
//	Табличный документ
//
Функция ПолучитьТабличныйДокументПриказаТ1(Макет, МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
	
	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_ПриказОПриеме";
	МассивДанныхЗаполнения = ПолучитьДанныеДляПечатиКадровогоПриказаТ1(МассивОбъектов);	
	
	ВывестиДанныеКадровогоПриказаВТабличныйДокумент(Макет, ДокументРезультат, МассивДанныхЗаполнения, ОбъектыПечати);
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	Возврат ДокументРезультат;
КонецФункции 
	
// Процедура печати документа.
// Возвращает табличный документ - сформированную печатную форму приказа о приеме или увольнении.
//
// Параметры:
//	МассивОбъектов - массив сотрудников.
//  ОбъектыПечати  - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в
//                   которой был выведен объект).
//	ВидПриказа     - "ПриказОПриеме" или "ПриказОбУвольнении".
//
// Возвращаемое значение:
//	Табличный документ
//
Функция ПолучитьТабличныйДокументПриказаТ5(Макет, МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
	
	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_ПриказОПереводе";
	МассивДанныхЗаполнения = ПолучитьДанныеДляПечатиКадровогоПриказаТ5(МассивОбъектов);	
	
	ВывестиДанныеКадровогоПриказаВТабличныйДокумент(Макет, ДокументРезультат, МассивДанныхЗаполнения, ОбъектыПечати);
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	Возврат ДокументРезультат;
КонецФункции 
	
// Процедура печати документа.
// Возвращает табличный документ - сформированную печатную форму приказа о приеме или увольнении.
//
// Параметры:
//	МассивОбъектов - массив сотрудников.
//  ОбъектыПечати  - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в
//                   которой был выведен объект).
//	ВидПриказа     - "ПриказОПриеме" или "ПриказОбУвольнении".
//
// Возвращаемое значение:
//	Табличный документ
//
Функция ПолучитьТабличныйДокументПриказаТ8(Макет, МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;

	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_ПриказОбУвольнении";
	МассивДанныхЗаполнения = ПолучитьДанныеДляПечатиКадровогоПриказаТ8(МассивОбъектов);
	
	ВывестиДанныеКадровогоПриказаВТабличныйДокумент(Макет, ДокументРезультат, МассивДанныхЗаполнения, ОбъектыПечати);
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	Возврат ДокументРезультат;
КонецФункции 

Функция ПолучитьДанныеДляПечатиКадровогоПриказаТ1(МассивОбъектов)
	
	Выборка = СформироватьЗапросДляТ1(МассивОбъектов).Выбрать();
	
	МассивПараметров = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Параметры = ПолучитьСтруктуруПараметровПриказаТ1();
		
		ЗаполнитьЗначенияСвойств(Параметры, Выборка);
		
		ЦелаяЧасть = Цел(Выборка.Оклад);
		Параметры.ОкладТарифнаяСтавкаЦелаяЧасть = ЦелаяЧасть;
		ДробнаяЧасть = Выборка.Оклад - ЦелаяЧасть;
		Параметры.ОкладТарифнаяСтавкаДробнаяЧасть = ?(ДробнаяЧасть = 0, "00", ДробнаяЧасть*100);
		
		МассивПараметров.Добавить(Параметры);
		
	КонецЦикла;
	
	Возврат МассивПараметров;

КонецФункции	

Функция ПолучитьДанныеДляПечатиКадровогоПриказаТ5(МассивОбъектов)
	
	Выборка = СформироватьЗапросДляТ5(МассивОбъектов).Выбрать();
	
	МассивПараметров = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Параметры = ПолучитьСтруктуруПараметровПриказаТ5();		
		
		ЗаполнитьЗначенияСвойств(Параметры, Выборка);
		
		ЦелаяЧасть = Цел(Выборка.Оклад);
		Параметры.ОкладТарифнаяСтавкаЦелаяЧасть = ЦелаяЧасть;
		ДробнаяЧасть = Выборка.Оклад - ЦелаяЧасть;
		Параметры.ОкладТарифнаяСтавкаДробнаяЧасть = ?(ДробнаяЧасть = 0, "00", ДробнаяЧасть*100);
		
		Если ЗначениеЗаполнено(Параметры.ДатаОкончания) Тогда
			Параметры.ВидПеревода = "Временно";
		Иначе
			Параметры.ВидПеревода = "Постоянно";
		КонецЕсли;
		
		МассивПараметров.Добавить(Параметры);
		
	КонецЦикла;
	
	Возврат МассивПараметров;

КонецФункции	

Функция ПолучитьДанныеДляПечатиКадровогоПриказаТ8(МассивОбъектов)
	
	Выборка = СформироватьЗапросДляТ8(МассивОбъектов).Выбрать();
	
	МассивПараметров = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Параметры = ПолучитьСтруктуруПараметровПриказаТ8();		
		
		ЗаполнитьЗначенияСвойств(Параметры, Выборка);

		ПредставлениеДатыУвольнения  = Формат(Выборка.ДатаУвольнения, "ДЛФ=DD");
		
		Параметры.ДатаУвольненияЧисло = ?(ЗначениеЗаполнено(Выборка.ДатаУвольнения),
			СокрЛП(Лев(ПредставлениеДатыУвольнения,2)),
			"      ");
		Параметры.ДатаУвольненияМесяцГод = ?(ЗначениеЗаполнено(Выборка.ДатаУвольнения),
			СокрЛП(Прав(ПредставлениеДатыУвольнения, СтрДлина(ПредставлениеДатыУвольнения)-2)),
			"                     г.");
		
		МассивПараметров.Добавить(Параметры);
		
	КонецЦикла;
	
	Возврат МассивПараметров;

КонецФункции	

Процедура ВывестиДанныеКадровогоПриказаВТабличныйДокумент(Макет, ТабличныйДокумент, МассивДанныхЗаполнения, ОбъектыПечати)
	
	Если Макет <> Неопределено Тогда
		
		ПервыйПриказ = Истина;
		Для каждого ПараметрыМакета Из МассивДанныхЗаполнения Цикл
			Если Не ПервыйПриказ Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			Иначе
				ПервыйПриказ = Ложь;
			КонецЕсли;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Макет.Параметры.Заполнить(ПараметрыМакета);
			ТабличныйДокумент.Вывести(Макет);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ПараметрыМакета.Ссылка);	
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСтруктуруПараметровПриказаТ1()
	
	Параметры = ПолучитьСтруктуруПараметровКадровогоПриказа();
	
	Параметры.Вставить("ДатаПриема");
	Параметры.Вставить("ДатаЗавершенияТрудовогоДоговора");
	Параметры.Вставить("ОкладТарифнаяСтавкаЦелаяЧасть");
	Параметры.Вставить("ОкладТарифнаяСтавкаДробнаяЧасть");
	Параметры.Вставить("ВалютаТарифнойСтавки", "руб.");
	Параметры.Вставить("КопейкиТарифнойСтавки", "коп.");
	Параметры.Вставить("Надбавка", 0);
	Параметры.Вставить("ДлительностьИспытательногоСрока", "");

	Возврат Параметры;
	
КонецФункции	

Функция ПолучитьСтруктуруПараметровПриказаТ5()
	
	Параметры = ПолучитьСтруктуруПараметровКадровогоПриказа();
	
	Параметры.Вставить("ДатаНачала");
	Параметры.Вставить("ДатаОкончания");
	Параметры.Вставить("ВидПеревода");
	Параметры.Вставить("Подразделение");
	Параметры.Вставить("Должность");
	Параметры.Вставить("НовоеПодразделение");
	Параметры.Вставить("НоваяДолжность");
	Параметры.Вставить("ОкладТарифнаяСтавкаЦелаяЧасть");
	Параметры.Вставить("ОкладТарифнаяСтавкаДробнаяЧасть");
	Параметры.Вставить("ВалютаТарифнойСтавки", "руб.");
	Параметры.Вставить("КопейкиТарифнойСтавки", "коп.");
	Параметры.Вставить("Надбавка", 0);
	Параметры.Вставить("ОснованиеПеревода");
	Параметры.Вставить("ПричинаПеревода");

	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСтруктуруПараметровПриказаТ8()
	
	Параметры = ПолучитьСтруктуруПараметровКадровогоПриказа();
	
	Параметры.Вставить("ДатаУвольненияЧисло");
	Параметры.Вставить("ДатаУвольненияМесяцГод");
	Параметры.Вставить("ОснованиеУвольнения", "");
	
	Возврат Параметры;
	
КонецФункции

Функция СформироватьЗапросДляТ1(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриемНаРаботу.Ссылка,
		|	ПриемНаРаботу.Номер КАК НомерДок,
		|	ПриемНаРаботу.Дата КАК ДатаДок,
		|	ПриемНаРаботу.Организация КАК НазваниеОрганизации,
		|	ПриемНаРаботу.Подразделение,
		|	ПриемНаРаботу.Сотрудник КАК Работник,
		|	ПриемНаРаботу.Должность,
		|	ПриемНаРаботу.ДолжностьРуководителя,
		|	ПриемНаРаботу.Руководитель КАК РуководительРасшифровкаПодписи,
		|	ПриемНаРаботу.ДатаПриема,
		|	ПриемНаРаботу.ДатаЗавершенияТрудовогоДоговора,
		|	ПриемНаРаботу.ДлительностьИспытательногоСрока,
		|	ЕСТЬNULL(Сотрудники.Код, """") КАК ТабельныйНомер,
		|	ЕСТЬNULL(торо_ТекущиеТарифныеСтавкиСотрудников.Оклад, 0) КАК Оклад
		|ИЗ
		|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ПриемНаРаботу.Сотрудник = Сотрудники.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ТекущиеТарифныеСтавкиСотрудников КАК торо_ТекущиеТарифныеСтавкиСотрудников
		|		ПО ПриемНаРаботу.Сотрудник = торо_ТекущиеТарифныеСтавкиСотрудников.Сотрудник
		|			И ПриемНаРаботу.Организация = торо_ТекущиеТарифныеСтавкиСотрудников.Организация
		|ГДЕ
		|	ПриемНаРаботу.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция СформироватьЗапросДляТ5(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадровыйПеревод.Ссылка КАК Ссылка,
		|	КадровыйПеревод.Сотрудник КАК Сотрудник,
		|	КадровыйПеревод.Организация КАК Организация,
		|	МАКСИМУМ(КадроваяИсторияСотрудников.Период) КАК ДатаПредыдущейДолжности
		|ПОМЕСТИТЬ ВТ_ТекущиеКадровыеДанныеПериод
		|ИЗ
		|	Документ.КадровыйПеревод КАК КадровыйПеревод
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|		ПО (КадроваяИсторияСотрудников.Сотрудник = КадровыйПеревод.Сотрудник)
		|			И (КадроваяИсторияСотрудников.Организация = КадровыйПеревод.Организация)
		|			И (КадроваяИсторияСотрудников.Регистратор <> КадровыйПеревод.Ссылка)
		|			И (КадроваяИсторияСотрудников.Период < КадровыйПеревод.Дата)
		|ГДЕ
		|	КадровыйПеревод.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	КадровыйПеревод.Ссылка,
		|	КадровыйПеревод.Организация,
		|	КадровыйПеревод.Сотрудник
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	Сотрудник,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТекущиеКадровыеДанныеПериод.Ссылка КАК Ссылка,
		|	ВТ_ТекущиеКадровыеДанныеПериод.Сотрудник КАК Сотрудник,
		|	ВТ_ТекущиеКадровыеДанныеПериод.Организация КАК Организация,
		|	КадроваяИсторияСотрудников.Подразделение,
		|	КадроваяИсторияСотрудников.Должность
		|ПОМЕСТИТЬ ВТ_ТекущиеКадровыеДанные
		|ИЗ
		|	ВТ_ТекущиеКадровыеДанныеПериод КАК ВТ_ТекущиеКадровыеДанныеПериод
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|		ПО ВТ_ТекущиеКадровыеДанныеПериод.Сотрудник = КадроваяИсторияСотрудников.Сотрудник
		|			И ВТ_ТекущиеКадровыеДанныеПериод.Организация = КадроваяИсторияСотрудников.Организация
		|			И ВТ_ТекущиеКадровыеДанныеПериод.ДатаПредыдущейДолжности = КадроваяИсторияСотрудников.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	Сотрудник,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КадровыйПеревод.Ссылка,
		|	КадровыйПеревод.Номер КАК НомерДок,
		|	КадровыйПеревод.Дата КАК ДатаДок,
		|	КадровыйПеревод.Организация КАК НазваниеОрганизации,
		|	КадровыйПеревод.Подразделение КАК НовоеПодразделение,
		|	КадровыйПеревод.Сотрудник КАК Работник,
		|	КадровыйПеревод.Должность КАК НоваяДолжность,
		|	КадровыйПеревод.ДолжностьРуководителя,
		|	КадровыйПеревод.Руководитель КАК РуководительРасшифровкаПодписи,
		|	ЕСТЬNULL(Сотрудники.Код, """") КАК ТабельныйНомер,
		|	ЕСТЬNULL(торо_ТекущиеТарифныеСтавкиСотрудников.Оклад, 0) КАК Оклад,
		|	КадровыйПеревод.ДатаНачала,
		|	КадровыйПеревод.ДатаОкончания,
		|	КадровыйПеревод.ПричинаПеревода,
		|	КадровыйПеревод.ОснованиеПеревода,
		|	ЕСТЬNULL(ВТ_ТекущиеКадровыеДанные.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
		|	ЕСТЬNULL(ВТ_ТекущиеКадровыеДанные.Должность, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК Должность
		|ИЗ
		|	Документ.КадровыйПеревод КАК КадровыйПеревод
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО КадровыйПеревод.Сотрудник = Сотрудники.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ТекущиеТарифныеСтавкиСотрудников КАК торо_ТекущиеТарифныеСтавкиСотрудников
		|		ПО КадровыйПеревод.Сотрудник = торо_ТекущиеТарифныеСтавкиСотрудников.Сотрудник
		|			И КадровыйПеревод.Организация = торо_ТекущиеТарифныеСтавкиСотрудников.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТекущиеКадровыеДанные КАК ВТ_ТекущиеКадровыеДанные
		|		ПО КадровыйПеревод.Ссылка = ВТ_ТекущиеКадровыеДанные.Ссылка
		|			И КадровыйПеревод.Сотрудник = ВТ_ТекущиеКадровыеДанные.Сотрудник
		|			И КадровыйПеревод.Организация = ВТ_ТекущиеКадровыеДанные.Организация
		|ГДЕ
		|	КадровыйПеревод.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция СформироватьЗапросДляТ8(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Увольнение.Ссылка,
		|	Увольнение.Номер КАК НомерДок,
		|	Увольнение.Дата КАК ДатаДок,
		|	Увольнение.Организация КАК НазваниеОрганизации,
		|	Увольнение.Подразделение,
		|	Увольнение.Сотрудник КАК Работник,
		|	Увольнение.ДолжностьРуководителя,
		|	Увольнение.Руководитель КАК РуководительРасшифровкаПодписи,
		|	ЕСТЬNULL(Сотрудники.Код, """") КАК ТабельныйНомер,
		|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК Должность,
		|	Увольнение.ДатаУвольнения,
		|	Увольнение.ОснованиеУвольнения
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО Увольнение.Сотрудник = Сотрудники.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ПО Увольнение.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник
		|			И Увольнение.Организация = ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация
		|ГДЕ
		|	Увольнение.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция ПолучитьСтруктуруПараметровКадровогоПриказа() Экспорт
	
	СтруктураПараметровКадровогоПриказа = Новый Структура;
	
	СтруктураПараметровКадровогоПриказа.Вставить("Ссылка");
	СтруктураПараметровКадровогоПриказа.Вставить("НазваниеОрганизации", "");
	СтруктураПараметровКадровогоПриказа.Вставить("НомерДок", "");
	СтруктураПараметровКадровогоПриказа.Вставить("ДатаДок", '00010101');
	СтруктураПараметровКадровогоПриказа.Вставить("Работник", "");
	СтруктураПараметровКадровогоПриказа.Вставить("ТабельныйНомер", "");
	СтруктураПараметровКадровогоПриказа.Вставить("Подразделение");
	СтруктураПараметровКадровогоПриказа.Вставить("Должность");
	СтруктураПараметровКадровогоПриказа.Вставить("ДолжностьРуководителя", "");
	СтруктураПараметровКадровогоПриказа.Вставить("РуководительРасшифровкаПодписи", "");
	
	Возврат СтруктураПараметровКадровогоПриказа;
		
КонецФункции


#КонецОбласти

#КонецОбласти
