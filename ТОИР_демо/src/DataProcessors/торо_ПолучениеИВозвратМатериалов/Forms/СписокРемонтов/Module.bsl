#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Строка Из Параметры.МассивСтрок Цикл
		НС = Объект.Ремонты.Добавить();
		ЗаполнитьЗначенияСвойств(НС, Строка);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
		ЭтаФорма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Список ремонтов требующих ""%1"" с характеристикой ""%2""'"),
			Параметры.Номенклатура,Параметры.Характеристика);
	Иначе
		ЭтаФорма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Список ремонтов требующих ""%1""'"),
			Параметры.Номенклатура);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРемонты

&НаКлиенте
Процедура РемонтыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	ОткрытьДокументСПозиционированиемНаРемонте(ДанныеСтроки.Документ, ДанныеСтроки.ID_Ремонта);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьДокументСПозиционированиемНаРемонте(Документ, ID)

	Если ТипЗнч(Документ) = Тип("ДокументСсылка.торо_ПланГрафикРемонта") Тогда
		ОткрытьФорму("Документ.торо_ПланГрафикРемонта.Форма.ФормаДокумента", Новый Структура("Ключ, ID", Документ, ID), ЭтаФорма);
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.торо_ЗаявкаНаРемонт") Тогда
		ОткрытьФорму("Документ.торо_ЗаявкаНаРемонт.Форма.ФормаДокумента", Новый Структура("Ключ, ID", Документ, ID), ЭтаФорма);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти