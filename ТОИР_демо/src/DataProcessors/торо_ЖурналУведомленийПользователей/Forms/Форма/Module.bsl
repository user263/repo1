#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "Пользователь", Пользователи.ТекущийПользователь());
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ДатаНачала",ОтборДатаНачала);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ДатаКонца", ?(ЗначениеЗаполнено(ОтборДатаОкончания),ОтборДатаОкончания,Дата("39991231")));
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ОтборПоСпособу", ОтборСпособУведомления);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ОтборСпособУведомленияПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ОтборПоСпособу", ОтборСпособУведомления);
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаНачалаПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ДатаНачала",ОтборДатаНачала);
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаОкончанияПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ДатаКонца", ?(ЗначениеЗаполнено(ОтборДатаОкончания),ОтборДатаОкончания,Дата("39991231")));
КонецПроцедуры

&НаКлиенте
Процедура ТолькоНепрочитанныеПриИзменении(Элемент)
	ТолькоНепрочитанныеПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
&НаКлиенте
Процедура СписокУведомленийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекДанные = Элементы.СписокУведомлений.ТекущиеДанные;
	ПоказатьЗначение(Неопределено, ТекДанные.Источник);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СнятьПометку(Команда)
	Если НЕ ТолькоНепрочитанные Тогда
		МассивСтрок = новый Массив;
		Для Каждого Эл Из Элементы.СписокУведомлений.ВыделенныеСтроки Цикл
			МассивСтрок.Добавить(Эл);
		КонецЦикла;
		СнятьПометкуНаСервере(МассивСтрок);
		Элементы.СписокУведомлений.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПометитьКакПрочитанное(Команда)
	МассивСтрок = новый Массив;
	Для Каждого Эл Из Элементы.СписокУведомлений.ВыделенныеСтроки Цикл
		МассивСтрок.Добавить(Эл);
	КонецЦикла;
	ПометитьКакПрочитанноеНаСервере(МассивСтрок);
	Элементы.СписокУведомлений.Обновить();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Процедура УстановитьТекстЗапроса(ДинСписок,ТолькоНепрочитанные)
	// При изменении запросов необходимо изменить процедуры установки флажкой "прочитано".
	Если ТолькоНепрочитанные Тогда
		ДинСписок.ОсновнаяТаблица = "РегистрСведений.торо_УведомленияПользователей";
		ДинСписок.ТекстЗапроса = "ВЫБРАТЬ
		                         |	ВЫБОР
		                         |		КОГДА ВложенныйЗапрос.КоличествоСпособовУведомлений > 1
		                         |			ТОГДА 2
		                         |		КОГДА торо_УведомленияПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.торо_СпособыУведомленияПользователей.УведомлениеПоЭлектроннойПочте)
		                         |			ТОГДА 1
		                         |		КОГДА торо_УведомленияПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.торо_СпособыУведомленияПользователей.УведомлениеВВидеВсплывающейПодсказки)
		                         |			ТОГДА 0
		                         |	КОНЕЦ КАК СпособУведомленияКартинка,
								 |	торо_УведомленияПользователей.СпособУведомления,
		                         |	торо_УведомленияПользователей.Источник,
		                         |	торо_УведомленияПользователей.ТемаПисьма,
		                         |	торо_УведомленияПользователей.ТелоПисьма,
		                         |	торо_УведомленияПользователей.ДатаУведомления,
		                         |	0 КАК Прочитано
		                         |ИЗ
		                         |	РегистрСведений.торо_УведомленияПользователей КАК торо_УведомленияПользователей
		                         |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		                         |			торо_УведомленияПользователей.ID_уведомления КАК ID_уведомления,
		                         |			КОЛИЧЕСТВО(торо_УведомленияПользователей.СпособУведомления) КАК КоличествоСпособовУведомлений,
		                         |			МАКСИМУМ(торо_УведомленияПользователей.СпособУведомления) КАК МаксСпособУведомления
		                         |		ИЗ
		                         |			РегистрСведений.торо_УведомленияПользователей КАК торо_УведомленияПользователей
		                         |		ГДЕ
		                         |			торо_УведомленияПользователей.Пользователь = &Пользователь
		                         |			И торо_УведомленияПользователей.ДатаУведомления МЕЖДУ &ДатаНачала И &ДатаКонца
		                         |		
		                         |		СГРУППИРОВАТЬ ПО
		                         |			торо_УведомленияПользователей.ID_уведомления) КАК ВложенныйЗапрос
		                         |		ПО торо_УведомленияПользователей.ID_уведомления = ВложенныйЗапрос.ID_уведомления
								 |			И торо_УведомленияПользователей.СпособУведомления = ВложенныйЗапрос.МаксСпособУведомления
		                         |ГДЕ
		                         |	(&ОтборПоСпособу = 2
		                         |			ИЛИ ВЫБОР
		                         |				КОГДА ВложенныйЗапрос.КоличествоСпособовУведомлений > 1
		                         |					ТОГДА 2
		                         |				КОГДА торо_УведомленияПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.торо_СпособыУведомленияПользователей.УведомлениеПоЭлектроннойПочте)
		                         |					ТОГДА 1
		                         |				КОГДА торо_УведомленияПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.торо_СпособыУведомленияПользователей.УведомлениеВВидеВсплывающейПодсказки)
		                         |					ТОГДА 0
		                         |			КОНЕЦ = &ОтборПоСпособу)";
	Иначе
		ДинСписок.ОсновнаяТаблица = "РегистрСведений.торо_ИсторияУведомленийПользователей";
		ДинСписок.ТекстЗапроса = "ВЫБРАТЬ
		                         |	ВЫБОР
		                         |		КОГДА ВложенныйЗапрос.КоличествоСпособовУведомлений > 1
		                         |			ТОГДА 2
		                         |		КОГДА торо_ИсторияУведомленийПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.торо_СпособыУведомленияПользователей.УведомлениеПоЭлектроннойПочте)
		                         |			ТОГДА 1
		                         |		КОГДА торо_ИсторияУведомленийПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.торо_СпособыУведомленияПользователей.УведомлениеВВидеВсплывающейПодсказки)
		                         |			ТОГДА 0
		                         |	КОНЕЦ КАК СпособУведомленияКартинка,
								 |	торо_ИсторияУведомленийПользователей.СпособУведомления,
		                         |	торо_ИсторияУведомленийПользователей.Источник,
		                         |	торо_ИсторияУведомленийПользователей.ТемаПисьма,
		                         |	торо_ИсторияУведомленийПользователей.ТелоПисьма,
		                         |	торо_ИсторияУведомленийПользователей.ДатаУведомления,
		                         |	ВЫБОР
		                         |		КОГДА торо_УведомленияПользователей.ID_уведомления ЕСТЬ NULL 
		                         |			ТОГДА 1
		                         |		ИНАЧЕ 0
		                         |	КОНЕЦ КАК Прочитано
		                         |ИЗ
		                         |	РегистрСведений.торо_ИсторияУведомленийПользователей КАК торо_ИсторияУведомленийПользователей
		                         |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		                         |			торо_ИсторияУведомленийПользователей.ID_уведомления КАК ID_уведомления,
		                         |			КОЛИЧЕСТВО(торо_ИсторияУведомленийПользователей.СпособУведомления) КАК КоличествоСпособовУведомлений,
		                         |			МАКСИМУМ(торо_ИсторияУведомленийПользователей.СпособУведомления) КАК МаксСпособУведомления
		                         |		ИЗ
		                         |			РегистрСведений.торо_ИсторияУведомленийПользователей КАК торо_ИсторияУведомленийПользователей
		                         |		ГДЕ
		                         |			торо_ИсторияУведомленийПользователей.Пользователь = &Пользователь
		                         |			И торо_ИсторияУведомленийПользователей.ДатаУведомления МЕЖДУ &ДатаНачала И &ДатаКонца
		                         |		
		                         |		СГРУППИРОВАТЬ ПО
		                         |			торо_ИсторияУведомленийПользователей.ID_уведомления) КАК ВложенныйЗапрос
		                         |		ПО торо_ИсторияУведомленийПользователей.ID_уведомления = ВложенныйЗапрос.ID_уведомления
		                         |			И (торо_ИсторияУведомленийПользователей.СпособУведомления = ВложенныйЗапрос.МаксСпособУведомления)
		                         |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_УведомленияПользователей КАК торо_УведомленияПользователей
		                         |		ПО торо_ИсторияУведомленийПользователей.ID_уведомления = торо_УведомленияПользователей.ID_уведомления
		                         |			И торо_ИсторияУведомленийПользователей.СпособУведомления = торо_УведомленияПользователей.СпособУведомления
		                         |ГДЕ
		                         |	(&ОтборПоСпособу = 2
		                         |			ИЛИ ВЫБОР
		                         |				КОГДА ВложенныйЗапрос.КоличествоСпособовУведомлений > 1
		                         |					ТОГДА 2
		                         |				КОГДА торо_ИсторияУведомленийПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.торо_СпособыУведомленияПользователей.УведомлениеПоЭлектроннойПочте)
		                         |					ТОГДА 1
		                         |				КОГДА торо_ИсторияУведомленийПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.торо_СпособыУведомленияПользователей.УведомлениеВВидеВсплывающейПодсказки)
		                         |					ТОГДА 0
		                         |			КОНЕЦ = &ОтборПоСпособу)";
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СнятьПометкуНаСервере(МассивКлючейРегистра)
	Для Каждого КлючРегистра Из МассивКлючейРегистра Цикл
		МенеджерЗаписи = РегистрыСведений.торо_УведомленияПользователей.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи,КлючРегистра);
		МенеджерЗаписи.Прочитать();
		Если НЕ МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписиИстории = РегистрыСведений.торо_ИсторияУведомленийПользователей.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписиИстории,КлючРегистра);
			МенеджерЗаписиИстории.Прочитать();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи,МенеджерЗаписиИстории);
			МенеджерЗаписи.Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПометитьКакПрочитанноеНаСервере(МассивКлючейРегистра)
	Для Каждого КлючЗаписи Из МассивКлючейРегистра Цикл
		МенеджерЗаписи = РегистрыСведений.торо_УведомленияПользователей.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи,КлючЗаписи);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ТолькоНепрочитанныеПриИзмененииНаСервере()
	УстановитьТекстЗапроса(СписокУведомлений,ТолькоНепрочитанные);
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьТекстЗапроса(СписокУведомлений,ТолькоНепрочитанные);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ДатаНачала",ОтборДатаНачала);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ДатаКонца", ?(ЗначениеЗаполнено(ОтборДатаОкончания),ОтборДатаОкончания,Дата("39991231")));
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ЭтаФорма.СписокУведомлений, "ОтборПоСпособу", ОтборСпособУведомления);
	
КонецПроцедуры

#КонецОбласти
