#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ПланГрафикРемонта") Тогда
		Объект.ПланГрафикРемонта = Параметры.ПланГрафикРемонта;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПланГрафикРемонтаПриИзменении(Элемент)
	Если ПредДок <> Объект.ПланГрафикРемонта И Объект.ПланРемонтов.Количество() > 0 Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПланГрафикРемонтаПриИзмененииЗавершение", ЭтотОбъект), НСтр("ru = 'Данные в таблице ремонтов будут заменены. Продолжить?'"),РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПланГрафикРемонтаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПредДок = Объект.ПланГрафикРемонта;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьОценкиРМНаСервере();
	
	ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Оценки риск-менеджмента записаны.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОтбору(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОтборуЗавершение", ЭтотОбъект), НСтр("ru = 'Данные в таблице ремонтов будут заменены. Продолжить?'"),РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокиПоОтбору(Команда)
	ДанныеДляЗаполнения = ПолучитьДанныеДляЗаполнения(Объект.ПланГрафикРемонта, Объект.Отбор_ВладелецОР, Объект.Отбор_Подразделение, Объект.Отбор_ВидРемонта, Объект.Отбор_СпособВыполнения, Объект.Отбор_ОбъектРемонта, Объект.Отбор_Направление, Объект.Отбор_СуммаОт, Объект.Отбор_СуммаДо);
	
	Для каждого ТекСтрока из ДанныеДляЗаполнения Цикл
		Если Объект.ПланРемонтов.НайтиСтроки(Новый Структура("ID", ТекСтрока.ID)).Количество() = 0 Тогда
			НС = Объект.ПланРемонтов.Добавить();
			ЗаполнитьЗначенияСвойств(НС,ТекСтрока);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ЗаполнитьПоОтборуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    Объект.ПланРемонтов.Очистить();
    
    ДанныеДляЗаполнения = ПолучитьДанныеДляЗаполнения(Объект.ПланГрафикРемонта, Объект.Отбор_ВладелецОР, Объект.Отбор_Подразделение, Объект.Отбор_ВидРемонта, Объект.Отбор_СпособВыполнения, Объект.Отбор_ОбъектРемонта, Объект.Отбор_Направление, Объект.Отбор_СуммаОт, Объект.Отбор_СуммаДо);
    
    Для каждого ТекСтрока из ДанныеДляЗаполнения Цикл
        НС = Объект.ПланРемонтов.Добавить();
        ЗаполнитьЗначенияСвойств(НС,ТекСтрока);
    КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеДляЗаполнения(ПланГрафикРемонта, Отбор_ВладелецОР, Отбор_Подразделение, Отбор_ВидРемонта, Отбор_СпособВыполнения, Отбор_ОбъектРемонта, Отбор_Направление, Отбор_СуммаОт, Отбор_СуммаДо)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_ПланГрафикРемонтаПланРемонтов.ID,
	|	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот,
	|	торо_ПланГрафикРемонтаПланРемонтов.ДатаКон,
	|	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач,
	|	торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот,
	|	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель,
	|	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения
	|ПОМЕСТИТЬ торо_ПланГрафикРемонтаПланРемонтов
	|ИЗ
	|	Документ.торо_ПланГрафикРемонта.ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	|ГДЕ
	|	торо_ПланГрафикРемонтаПланРемонтов.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА &Организация = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВЫБОР
	|					КОГДА НЕ торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот.ВнешнийОбъект
	|						ТОГДА торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот.Организация = &Организация
	|					ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот.Контрагент = &Организация
	|				КОНЕЦ
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот.Подразделение = &Подразделение
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Направление = ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот.Направление = &Направление
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот = &ОбъектРемонта
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ВидРемонта = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот = &ВидРемонта
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &СуммаОт = 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.СуммаРемонта >= &СуммаОт
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &СуммаДо = 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.СуммаРемонта <= &СуммаДо
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Способ = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ &Способ = торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ПланГрафикРемонтаПланРемонтов.ID,
	|	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот,
	|	торо_ПланГрафикРемонтаПланРемонтов.ДатаКон,
	|	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач,
	|	торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот,
	|	ВЫБОР
	|		КОГДА торо_РМРемонтовСрезПоследних.ВероятностьВыходаИзСтроя ЕСТЬ NULL 
	|			ТОГДА ЕСТЬNULL(торо_РМРемонтовСрезПоследних_Группа.ВероятностьВыходаИзСтроя, 0)
	|		ИНАЧЕ торо_РМРемонтовСрезПоследних.ВероятностьВыходаИзСтроя
	|	КОНЕЦ КАК ВероятностьВыходаИзСтроя,
	|	ВЫБОР
	|		КОГДА торо_РМРемонтовСрезПоследних.Ущерб ЕСТЬ NULL 
	|			ТОГДА ЕСТЬNULL(торо_РМРемонтовСрезПоследних_Группа.Ущерб, 0)
	|		ИНАЧЕ торо_РМРемонтовСрезПоследних.Ущерб
	|	КОНЕЦ КАК Ущерб,
	|	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель,
	|	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения
	|ПОМЕСТИТЬ ТабНормОценок
	|ИЗ
	|	торо_ПланГрафикРемонтаПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_РМРемонтов.СрезПоследних(&ТекДата, ) КАК торо_РМРемонтовСрезПоследних
	|		ПО торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот = торо_РМРемонтовСрезПоследних.ГруппаОбъектовРемонтов
	|			И торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот = торо_РМРемонтовСрезПоследних.ВидРемонта
	|			И (ВЫБОР
	|				КОГДА торо_РМРемонтовСрезПоследних.Сезон = ЗНАЧЕНИЕ(Справочник.торо_Сезоны.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫБОР
	|						КОГДА торо_РМРемонтовСрезПоследних.Сезон.Переходящий
	|								И (торо_ПланГрафикРемонтаПланРемонтов.ДатаНач МЕЖДУ ДОБАВИТЬКДАТЕ(торо_РМРемонтовСрезПоследних.Сезон.ДатаНачалаСезона, ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2) И ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(торо_РМРемонтовСрезПоследних.Сезон.ДатаНачалаСезона, ГОД), ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2)
	|									ИЛИ торо_ПланГрафикРемонтаПланРемонтов.ДатаНач МЕЖДУ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(торо_РМРемонтовСрезПоследних.Сезон.ДатаОкончанияСезона, ГОД), ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2) И ДОБАВИТЬКДАТЕ(торо_РМРемонтовСрезПоследних.Сезон.ДатаОкончанияСезона, ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2))
	|							ТОГДА ИСТИНА
	|						КОГДА НЕ торо_РМРемонтовСрезПоследних.Сезон.Переходящий
	|								И (торо_ПланГрафикРемонтаПланРемонтов.ДатаНач МЕЖДУ ДОБАВИТЬКДАТЕ(торо_РМРемонтовСрезПоследних.Сезон.ДатаНачалаСезона, ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2) И ДОБАВИТЬКДАТЕ(торо_РМРемонтовСрезПоследних.Сезон.ДатаОкончанияСезона, ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2))
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ЛОЖЬ
	|					КОНЕЦ
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_РМРемонтов.СрезПоследних(&ТекДата, ) КАК торо_РМРемонтовСрезПоследних_Группа
	|		ПО торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот.ТиповойОР = торо_РМРемонтовСрезПоследних_Группа.ГруппаОбъектовРемонтов
	|			И торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот = торо_РМРемонтовСрезПоследних_Группа.ВидРемонта
	|			И (ВЫБОР
	|				КОГДА торо_РМРемонтовСрезПоследних_Группа.Сезон = ЗНАЧЕНИЕ(Справочник.торо_Сезоны.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫБОР
	|						КОГДА торо_РМРемонтовСрезПоследних_Группа.Сезон.Переходящий
	|								И (торо_ПланГрафикРемонтаПланРемонтов.ДатаНач МЕЖДУ ДОБАВИТЬКДАТЕ(торо_РМРемонтовСрезПоследних_Группа.Сезон.ДатаНачалаСезона, ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2) И ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(торо_РМРемонтовСрезПоследних_Группа.Сезон.ДатаНачалаСезона, ГОД), ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2)
	|									ИЛИ торо_ПланГрафикРемонтаПланРемонтов.ДатаНач МЕЖДУ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(торо_РМРемонтовСрезПоследних_Группа.Сезон.ДатаОкончанияСезона, ГОД), ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2) И ДОБАВИТЬКДАТЕ(торо_РМРемонтовСрезПоследних_Группа.Сезон.ДатаОкончанияСезона, ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2))
	|							ТОГДА ИСТИНА
	|						КОГДА НЕ торо_РМРемонтовСрезПоследних_Группа.Сезон.Переходящий
	|								И (торо_ПланГрафикРемонтаПланРемонтов.ДатаНач МЕЖДУ ДОБАВИТЬКДАТЕ(торо_РМРемонтовСрезПоследних_Группа.Сезон.ДатаНачалаСезона, ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2) И ДОБАВИТЬКДАТЕ(торо_РМРемонтовСрезПоследних_Группа.Сезон.ДатаОкончанияСезона, ГОД, ГОД(торо_ПланГрафикРемонтаПланРемонтов.ДатаНач) - 2))
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ЛОЖЬ
	|					КОНЕЦ
	|			КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабНормОценок.ID,
	|	ТабНормОценок.ВидРемонтныхРабот,
	|	ТабНормОценок.ДатаКон,
	|	ТабНормОценок.ДатаНач,
	|	ТабНормОценок.ОбъектРемонтныхРабот,
	|	ТабНормОценок.Исполнитель,
	|	ТабНормОценок.СпособВыполнения,
	|	ЕСТЬNULL(торо_ОценкаРМПлановыхРемонтов.ВероятностьВыходаИзСтроя, ТабНормОценок.ВероятностьВыходаИзСтроя) КАК ВероятностьВыходаИзСтроя,
	|	ЕСТЬNULL(торо_ОценкаРМПлановыхРемонтов.Ущерб, ТабНормОценок.Ущерб) КАК Ущерб
	|ИЗ
	|	ТабНормОценок КАК ТабНормОценок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ОценкаРМПлановыхРемонтов КАК торо_ОценкаРМПлановыхРемонтов
	|		ПО (торо_ОценкаРМПлановыхРемонтов.ID = ТабНормОценок.ID)
	|			И (торо_ОценкаРМПлановыхРемонтов.ОбъектРемонтныхРабот = ТабНормОценок.ОбъектРемонтныхРабот)
	|			И (торо_ОценкаРМПлановыхРемонтов.ВидРемонтныхРабот = ТабНормОценок.ВидРемонтныхРабот)
	|			И (торо_ОценкаРМПлановыхРемонтов.ДатаНач = ТабНормОценок.ДатаНач)
	|			И (торо_ОценкаРМПлановыхРемонтов.ДатаКон = ТабНормОценок.ДатаКон)";
	
	
	Запрос.УстановитьПараметр("Ссылка", ПланГрафикРемонта);
	
	Запрос.УстановитьПараметр("Организация", Отбор_ВладелецОР);
	Запрос.УстановитьПараметр("Подразделение", Отбор_Подразделение);
	Запрос.УстановитьПараметр("Направление", Отбор_Направление);
	Запрос.УстановитьПараметр("ВидРемонта", Отбор_ВидРемонта);
	Запрос.УстановитьПараметр("СуммаОт", Отбор_СуммаОт);
	Запрос.УстановитьПараметр("СуммаДо", Отбор_СуммаДо);
	Запрос.УстановитьПараметр("ОбъектРемонта", Отбор_ОбъектРемонта);
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	
	Если ЗначениеЗаполнено(Отбор_СпособВыполнения) Тогда		
		СпособПараметр = Отбор_СпособВыполнения;
	Иначе
		СпособПараметр = Неопределено;
	КонецЕсли;
		
	Запрос.УстановитьПараметр("Способ", СпособПараметр);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивВозврата = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		СтрТЧ = Новый Структура("ID, ВидРемонтныхРабот, ДатаКон, ДатаНач, ОбъектРемонтныхРабот, Исполнитель, СпособВыполнения, ВероятностьВыходаИзСтроя, Ущерб");
		ЗаполнитьЗначенияСвойств(СтрТЧ, Выборка);
		МассивВозврата.Добавить(СтрТЧ);
		
	КонецЦикла;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Процедура ПланГрафикРемонтаПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
        Объект.ПланГрафикРемонта = ПредДок;
    Иначе
        Объект.ПланРемонтов.Очистить();
    КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьОценкиРМНаСервере()
	
	Для каждого ТекСтрока Из Объект.ПланРемонтов Цикл
		Если ТекСтрока.ВероятностьВыходаИзСтроя * ТекСтрока.Ущерб <> 0 Тогда
			Запись = РегистрыСведений.торо_ОценкаРМПлановыхРемонтов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись,ТекСтрока);
			Запись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти