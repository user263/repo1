#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Выбрать(Истина);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Выбрать(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		СтруктураВозврата = ПолучитьСтруктуруВозвратаНаСервере(ТекущиеДанные.Ссылка);
		Оповестить("ОБРАБОТКА_ВЫБОРА_СЦЕНАРИЯ", СтруктураВозврата);
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Функция ПолучитьСтруктуруВозвратаНаСервере(Ссылка)
	
	Структура = Новый Структура();
	Структура.Вставить("Ссылка", Ссылка);

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьСогласование,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьУведомленияОСобытияхСистемы,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_УчетЗапчастей,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьТиповыеОР,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьСложныеТехКартыРемонтов,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьНаправленияОР,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьГарантии,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьДокументыПринятияИСписанияОборудования,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_УчетКонтролируемыхПоказателей,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_УчетНаработкиОборудования,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_УчетСостоянияОборудования,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_УчетПусковОборудования,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_УчетВыявленныхДефектовОборудования,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьППР,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_УчетЗамещающихРемонтов,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьКоэффициентыРемонтныхОсобенностей,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьРегламентныеМероприятия,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьПланированиеПоЖесткимРемЦиклам,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользованиеЛимитовРемонтов,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьОценкиРМ,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_РассчитыватьПлановуюСтоимостьРемонтовПриПланировании,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьЛоты,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьСметыРемонта,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьРемонтыПоСостоянию,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьОбщийПланРабот,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьВнешниеОснованияДляРабот,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьНарядыНаВыполнениеРабот,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьВозможностьОтменыРемонтов,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьУсложненнуюСхемуЗакрытияНарядов,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьАналогиНоменклатурыДляРемонтов,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьУправлениеПерсоналом,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьСтатусыДокументовТОиР,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьКонтрольОтрицательныхОстатков,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьНастраиваемыеПредставления,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_РазрешитьПользовательскуюНастройкуПредставлений,
		|	торо_ПредопределенныеСценарииРаботыСистемы.ИспользоватьПартнеровИКонтрагентов,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ВыделятьИнвестиционныеРемонты,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьСтандартныеОповещенияОПревышенииПоказателей,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьУпрощеннуюФормуДефекта,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьФоновыйРасчетППР,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьОстановочныеРемонты,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьНарядДопускНаРаботыПовышеннойОпасностиОбязательно,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьОпасноеПроизводство,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ЗапретитьПроведениеАктовСИсполнителямиБезУказанияКвал,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьАктПриемкиОборудования,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ИспользоватьНарядыНаРегламентноеМероприятие,
		|	торо_ПредопределенныеСценарииРаботыСистемы.торо_ПоказыватьПодсистемуKPI,
		|	торо_ПредопределенныеСценарииРаботыСистемы.Предопределенный
		|ИЗ
		|	Справочник.торо_ПредопределенныеСценарииРаботыСистемы КАК торо_ПредопределенныеСценарииРаботыСистемы
		|ГДЕ
		|	торо_ПредопределенныеСценарииРаботыСистемы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_ПредопределенныеСценарииРаботыСистемыВводНаОсновании.ПараДокументов,
		|	торо_ПредопределенныеСценарииРаботыСистемыВводНаОсновании.Значение
		|ИЗ
		|	Справочник.торо_ПредопределенныеСценарииРаботыСистемы.ВводНаОсновании КАК торо_ПредопределенныеСценарииРаботыСистемыВводНаОсновании
		|ГДЕ
		|	торо_ПредопределенныеСценарииРаботыСистемыВводНаОсновании.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТЗКонстант = РезультатЗапроса[0].Выгрузить();
	
	Структура.Вставить("Предопределенный", Ложь);
	
	Если ТЗКонстант.Количество() > 0 Тогда
		
		СтрокаТаблицы  = ТЗКонстант[0];
		
		Структура.Предопределенный = СтрокаТаблицы.Предопределенный;

		КолонкиТаблица = ТЗКонстант.Колонки;
		СтруктураКонстант = Новый Структура;
		
		Для каждого Колонка Из КолонкиТаблица Цикл
			
			ИмяКолонки = Колонка.Имя;
			
			Если ИмяКолонки = "Предопределенный" Тогда
				Продолжить;
			КонецЕсли; 
			
			СтруктураКонстант.Вставить(ИмяКолонки, СтрокаТаблицы[ИмяКолонки]);
			
		КонецЦикла;
		
		Структура.Вставить("МассивСтруктурЗначенийКонстант", СтруктураКонстант);
		
	КонецЕсли; 
	
	ТЗВводНаОсновании = РезультатЗапроса[1].Выгрузить();

	Если ТЗВводНаОсновании.Количество() > 0 Тогда
		
		Соответствие = Новый Соответствие;
		
		Для каждого Строка Из ТЗВводНаОсновании Цикл
			Соответствие.Вставить(Строка.ПараДокументов,Строка.Значение);
		КонецЦикла;
		Структура.Вставить("СоответствиеВводНаОсновании", Соответствие);
	КонецЕсли; 
	
	
	Возврат Структура;

КонецФункции
#КонецОбласти
 