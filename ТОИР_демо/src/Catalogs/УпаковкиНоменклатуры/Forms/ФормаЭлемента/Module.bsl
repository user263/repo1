#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	Элементы.СостоитИзДругихУпаковокПереключатель.ТолькоПросмотр = Элементы.СостоитИзДругихУпаковок.ТолькоПросмотр;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Если Объект.СостоитИзДругихУпаковок Тогда
		СостоитИзДругихУпаковок = 1;
	Иначе
		СостоитИзДругихУпаковок = 0;
	КонецЕсли;
	
	ОбновитьКоэффициентРодителя();	
	
	ЕдиницаИзмеренияВладельца = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ЕдиницаИзмерения");
	
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		Элементы.Владелец.Заголовок = НСтр("ru='Номенклатура'");
	КонецЕсли;
	
	УстановитьТекущуюСтраницуКоэффициентов(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура КонечнаяПриИзменении(Элемент)
	
	Если СостоитИзДругихУпаковок = 0 Тогда
		Объект.СостоитИзДругихУпаковок = Ложь;
	Иначе
		Объект.СостоитИзДругихУпаковок = Истина;
	КонецЕсли;
	
	УстановитьТекущуюСтраницуКоэффициентов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоУпаковокПриИзменении(Элемент)
	
	Объект.Коэффициент = Объект.КоличествоУпаковок * КоэффициентРодителя;
	
	Объект.Наименование = ОбновитьНаименование(Объект.ЕдиницаИзмерения, Объект.Коэффициент, ЕдиницаИзмеренияВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	РодительПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура КоэффициентПриИзменении(Элемент)
	
	Объект.Наименование = ОбновитьНаименование(Объект.ЕдиницаИзмерения, Объект.Коэффициент, ЕдиницаИзмеренияВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	Объект.Наименование = ОбновитьНаименование(Объект.ЕдиницаИзмерения, Объект.Коэффициент, ЕдиницаИзмеренияВладельца);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ОткрытьФорму("Справочник.УпаковкиНоменклатуры.Форма.РазблокированиеРеквизитов",,,,,, Новый ОписаниеОповещения("Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
		
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Результат);
		Элементы.СостоитИзДругихУпаковокПереключатель.ТолькоПросмотр = Элементы.СостоитИзДругихУпаковок.ТолькоПросмотр;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницуКоэффициентов(Форма)
	Если Форма.Объект.СостоитИзДругихУпаковок Тогда
		Форма.Элементы.ГруппаКоэффицентыСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаКоличествоУпаковок;
	Иначе
		Форма.Элементы.ГруппаКоэффицентыСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаКоэффициентЕдиницаИзмерения;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РодительПриИзмененииСервер()
	
	ОбновитьКоэффициентРодителя();	
	
	Объект.Коэффициент = Объект.КоличествоУпаковок * КоэффициентРодителя;
	
	Объект.Наименование = ОбновитьНаименование(Объект.ЕдиницаИзмерения, Объект.Коэффициент, ЕдиницаИзмеренияВладельца );
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоэффициентРодителя()
	
	Если Объект.СостоитИзДругихУпаковок Тогда
		КоэффициентРодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Родитель, "Коэффициент");
	Иначе
		КоэффициентРодителя = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбновитьНаименование(ЕдиницаИзмерения, Коэффициент, ЕдиницаИзмеренияВладельца)
	Возврат Справочники.УпаковкиНоменклатуры.СформироватьНаименование(ЕдиницаИзмерения, Коэффициент, ЕдиницаИзмеренияВладельца);	
КонецФункции

#КонецОбласти