
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой(ЭтаФорма);
	РассчитатьПродолжительностьГарантии();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПроверятьНаработкуПриИзменении(Элемент)
	
	Если НЕ Объект.ПроверятьНаработку Тогда
		Объект.ПоказательНаработки = Неопределено;
		Объект.ЗначениеНаработки = 0;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьПериодГарантииПриИзменении(Элемент)
	
	Если НЕ Объект.ПроверятьПериодГарантии Тогда
		Объект.ДатаНачалаГарантии = Неопределено;
		Объект.ДатаОкончанияГарантии = Неопределено;
		ПродолжительностьГарантии = 0;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаГарантииПриИзменении(Элемент)
	
	РассчитатьДатуОкончанияГарантии();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияГарантииПриИзменении(Элемент)
	
	РассчитатьПродолжительностьГарантии();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжительностьГарантииПриИзменении(Элемент)
	
	РассчитатьДатуОкончанияГарантии();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.ГруппаНаработка.Видимость = Объект.ПроверятьНаработку;
	Элементы.ПоказательНаработки.АвтоОтметкаНезаполненного = Объект.ПроверятьНаработку;
	Элементы.ЗначениеНаработки.АвтоОтметкаНезаполненного = Объект.ПроверятьНаработку;
	Если НЕ Объект.ПроверятьНаработку Тогда
		Элементы.ПоказательНаработки.ОтметкаНезаполненного = Ложь;
		Элементы.ЗначениеНаработки.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаПериодГарантии.Видимость = Объект.ПроверятьПериодГарантии;
	Элементы.ДатаНачалаГарантии.АвтоОтметкаНезаполненного = Объект.ПроверятьПериодГарантии;
	Элементы.ДатаОкончанияГарантии.АвтоОтметкаНезаполненного = Объект.ПроверятьПериодГарантии;
	Если НЕ Объект.ПроверятьПериодГарантии Тогда
		Элементы.ДатаНачалаГарантии.ОтметкаНезаполненного = Ложь;
		Элементы.ДатаОкончанияГарантии.ОтметкаНезаполненного = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПродолжительностьГарантии()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаНачалаГарантии) 
		ИЛИ НЕ ЗначениеЗаполнено(Объект.ДатаОкончанияГарантии) Тогда
		ПродолжительностьГарантии = 0;
		Возврат;
	КонецЕсли;
	
	ПродолжительностьГарантии = (Объект.ДатаОкончанияГарантии - Объект.ДатаНачалаГарантии)/86400;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДатуОкончанияГарантии()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаНачалаГарантии) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДатаОкончанияГарантии = Объект.ДатаНачалаГарантии + ПродолжительностьГарантии*86400;
	
КонецПроцедуры

#КонецОбласти