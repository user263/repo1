#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	флПараметрыИзменены = Ложь;
	
	Если Параметры.Свойство("МассивРемонтов") Тогда
		
		Для каждого Элем Из Параметры.МассивРемонтов Цикл
			
			НС = ТаблицаВидыРемонта.Добавить();
			ЗаполнитьЗначенияСвойств(НС, Элем);
			
		КонецЦикла;
		
		СписокРемонтов.ЗагрузитьЗначения(Параметры.МассивРемонтов);
		
	КонецЕсли;
	
	Если Параметры.Свойство("Нормировать") Тогда
		Нормировать = Параметры.Нормировать;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаВидыРемонта
&НаКлиенте
Процедура ТаблицаВидыРемонтаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Инд = Элемент.ТекущаяСтрока;
	
	Стр = ТаблицаВидыРемонта.Получить(Инд);
	
	ИсходныйФлаг = Стр.Выбран;
	ИсходныйЦвет = Стр.Цвет;
	ИсходныйЦветТекста = Стр.ЦветТекста;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВидыРемонтаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НЕ ОтменаРедактирования Тогда
		
		Инд = Элемент.ТекущаяСтрока;
		
		Стр = ТаблицаВидыРемонта.Получить(Инд);
		
		Пересчитать = Пересчитать Или Стр.Выбран <> ИсходныйФлаг;
		Если ИсходныйЦвет <> Стр.Цвет ИЛИ ИсходныйЦветТекста <> Стр.ЦветТекста Тогда
			
			УстановитьУсловноеОформление();
			
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОК(Команда)
	
	флПараметрыИзменены = Истина;
	
	МассивРемонтов = Новый Массив;
	
	Для каждого Стр Из ТаблицаВидыРемонта Цикл
		
		МассивРемонтов.Добавить(Новый Структура("ВидРемонтныхРабот, Выбран, Цвет, ЦветТекста, Шрифт", Стр.ВидРемонтныхРабот, Стр.Выбран, Стр.Цвет,Стр.ЦветТекста,Стр.Шрифт));
		
	КонецЦикла;
	
	Закрыть(Новый Структура("МассивРемонтов,Нормировать,Пересчитать",МассивРемонтов, Нормировать, Пересчитать));
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Для каждого Стр Из ТаблицаВидыРемонта Цикл
		
		ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаВидыРемонта");
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаВидыРемонта.ВидРемонтныхРабот");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Стр.ВидРемонтныхРабот;
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Стр.Цвет);
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Стр.ЦветТекста);
		
	КонецЦикла;
	
КонецПроцедуры

Пересчитать = Ложь;
#КонецОбласти

