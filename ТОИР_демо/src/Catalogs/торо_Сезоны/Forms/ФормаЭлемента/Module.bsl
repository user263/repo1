////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПЕРЕМЕННЫЕ
&НаКлиенте
Перем ПроцессЗаписи;
&НаКлиенте
Перем ПроисходитЗакрытие;

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаНачалаСтрока = Формат(Объект.ДатаНачалаСезона,"ДФ='dd MMMM'");
	ДатаОкончанияСтрока = Формат(Объект.ДатаОкончанияСезона,"ДФ='dd MMMM'");
	
	МесяцНачала = Месяц(Объект.ДатаНачалаСезона);
	МесяцОкончания = Месяц(Объект.ДатаОкончанияСезона);

	ДеньНачала = День(Объект.ДатаНачалаСезона);
	ДеньОкончания = День(Объект.ДатаОкончанияСезона);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьПолеДня(Элементы.ДеньНачала, ДеньНачала, МесяцНачала);
	ЗаполнитьПолеДня(Элементы.ДеньОкончания, ДеньОкончания, МесяцОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Попытка
		ПарамДатаНачалаСезона = Дата("0002",МесяцНачала,ДеньНачала);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректная дата начала сезона.'"),,,,Отказ);
	КонецПопытки;
	
	Попытка
		ПарамДатаОкончанияСезона = Дата("0002",МесяцОкончания,ДеньОкончания);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректная дата окончания сезона.'"),,,,Отказ);
	КонецПопытки;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ПарамДатаНачалаСезона > ПарамДатаОкончанияСезона Тогда
		Если Не ПроцессЗаписи Тогда
			Отказ = Истина;
			ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗаписьюЗавершение",ЭтотОбъект,
													Новый Структура("ПарамДатаНачалаСезона, ПарамДатаОкончанияСезона",
																	ПарамДатаНачалаСезона, ПарамДатаОкончанияСезона)),
							НСтр("ru = 'При окончании года сезон не будет завершен. Вы уверены, что сезон должен продолжаться в следующем году?'"),
							РежимДиалогаВопрос.ДаНет);
		КонецЕсли;	
		
	Иначе
		Объект.Переходящий = Ложь;
		Объект.ДатаНачалаСезона = ПарамДатаНачалаСезона;
		Объект.ДатаОкончанияСезона = ПарамДатаОкончанияСезона;
		
		Если ПроисходитЗакрытие Тогда
			Закрыть();
			ПроисходитЗакрытие = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура МесяцНачалаПриИзменении(Элемент)
	ЗаполнитьПолеДня(Элементы.ДеньНачала, ДеньНачала, МесяцНачала);
КонецПроцедуры

&НаКлиенте
Процедура МесяцОкончанияПриИзменении(Элемент)
	ЗаполнитьПолеДня(Элементы.ДеньОкончания, ДеньОкончания, МесяцОкончания);
КонецПроцедуры

&НаКлиенте
Процедура ДеньНачалаПриИзменении(Элемент)
	ПроверитьКорректностьНомераДня(ДеньНачала, МесяцНачала);	
КонецПроцедуры

&НаКлиенте
Процедура ДеньОкончанияПриИзменении(Элемент)
	ПроверитьКорректностьНомераДня(ДеньОкончания, МесяцОкончания);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура КомандаЗаписать(Команда)
	Записать();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Записать();
	ПроисходитЗакрытие = Истина;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ЗаполнитьПолеДня(ЭлементФормыДень, РеквизитДень, НомерМесяца)
	
	ДнейВмесяце = ВозвратДнейВмесяце(НомерМесяца);
	ЭлементФормыДень.СписокВыбора.Очистить();
	
	Для ТекДень = 1 по ДнейВмесяце Цикл
		ЭлементФормыДень.СписокВыбора.Добавить(ТекДень);
	Конеццикла;	
	
	ПроверитьКорректностьНомераДня(РеквизитДень, НомерМесяца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКорректностьНомераДня(РеквизитДень, НомерМесяца)
	
	ДнейВмесяце = ВозвратДнейВмесяце(НомерМесяца);
	
	Если РеквизитДень > ДнейВмесяце Тогда
		РеквизитДень = ДнейВмесяце;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ВозвратДнейВмесяце(ПолеВыбораМесяца);
	ДнейВмесяце = 30;
	Если ПолеВыбораМесяца = 1 Тогда
		ДнейВмесяце = 31;
	ИначеЕсли ПолеВыбораМесяца = 2 Тогда
		ДнейВмесяце = 28;		
	ИначеЕсли ПолеВыбораМесяца = 3 Тогда		
		ДнейВмесяце = 31;		
	ИначеЕсли ПолеВыбораМесяца = 4 Тогда
		ДнейВмесяце = 30;		
	ИначеЕсли ПолеВыбораМесяца = 5 Тогда
		ДнейВмесяце = 31;		
	ИначеЕсли ПолеВыбораМесяца = 6 Тогда
		ДнейВмесяце = 30;		
	ИначеЕсли ПолеВыбораМесяца = 7 Тогда
		ДнейВмесяце = 31;		
	ИначеЕсли ПолеВыбораМесяца = 8 Тогда
		ДнейВмесяце = 31;		
	ИначеЕсли ПолеВыбораМесяца = 9 Тогда
		ДнейВмесяце = 30;		
	ИначеЕсли ПолеВыбораМесяца = 10 Тогда
		ДнейВмесяце = 31;		
	ИначеЕсли ПолеВыбораМесяца = 11 Тогда		
		ДнейВмесяце = 30;				
	ИначеЕсли ПолеВыбораМесяца = 12 Тогда		
		ДнейВмесяце = 31;				
	КонецЕсли;
	Возврат ДнейВмесяце;
КонецФункции

&НаКлиенте
Процедура ПередЗаписьюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ПарамДатаНачалаСезона = ДополнительныеПараметры.ПарамДатаНачалаСезона;
	ПарамДатаОкончанияСезона = ДополнительныеПараметры.ПарамДатаОкончанияСезона;
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ПроцессЗаписи = Истина;
		
		Объект.Переходящий = Истина;
		Объект.ДатаНачалаСезона = ПарамДатаНачалаСезона;
		Объект.ДатаОкончанияСезона = ПарамДатаОкончанияСезона;
		
		Записать();
		
		Если ПроисходитЗакрытие Тогда
			Закрыть();
			ПроисходитЗакрытие = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	ПроцессЗаписи = Ложь;
КонецПроцедуры

ПроисходитЗакрытие = Ложь;
ПроцессЗаписи = Ложь;
#КонецОбласти

