#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьДанныеФизическогоЛица() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КонтактныеЛицаПартнеров.Ссылка,
	|	ФизическиеЛица.ДатаРождения,
	|	ФизическиеЛица.Пол
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО КонтактныеЛицаПартнеров.Удалить_ФизЛицо = ФизическиеЛица.Ссылка
	|ГДЕ
	|	(КонтактныеЛицаПартнеров.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.ПустаяСсылка)
	|			И ФизическиеЛица.Пол <> ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.ПустаяСсылка))
	|	ИЛИ (КонтактныеЛицаПартнеров.ДатаРождения = ДАТАВРЕМЯ(1, 1, 1)
	|			И ФизическиеЛица.ДатаРождения <> ДАТАВРЕМЯ(1, 1, 1))";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		КонтактноеЛицоОбъект = Выборка.Ссылка.ПолучитьОбъект();
		КонтактноеЛицоОбъект.ДатаРождения = Выборка.ДатаРождения;
		КонтактноеЛицоОбъект.Пол = Выборка.Пол;
		КонтактноеЛицоОбъект.ОбменДанными.Загрузка = Истина;
		КонтактноеЛицоОбъект.Записать();
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли