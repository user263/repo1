#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Функция - Фамилия для поиска
//
// Параметры:
//  Фамилия	 - Строка - фамилия которую необходимо подготовить к поиску.
// Возвращаемое значение:
//  Строка - подготовленная к поиску фамилия.
Функция ФамилияДляПоиска(Знач Фамилия) Экспорт
	
	ПодготовитьКПоискуПоФИО(Фамилия); 
	
	ПодготовитьКПоискуПоФИОСБуквамиЕЁ(Фамилия);
	
	Возврат Фамилия;
	
КонецФункции

// Функция - Имя для поиска
//
// Параметры:
//  Имя	 - Строка - имя которое необходимо подготовить к поиску.
// Возвращаемое значение:
//  Строка - подготовленное к поиску имя.
Функция ИмяДляПоиска(Знач Имя) Экспорт
	
	ПодготовитьКПоискуПоФИО(Имя); 
	
	Если ЗначениеЗаполнено(Имя) И Найти("НАТАЛЬЯ,НАТАЛИЯ", Имя) > 0 Тогда
		Имя = "НАТАЛЬИЯ";
	КонецЕсли;
	
	ПодготовитьКПоискуПоФИОСБуквамиЕЁ(Имя);
	
	Возврат Имя;
	
КонецФункции

// Функция - Отчество для поиска
//
// Параметры:
//  Отчество - Строка - отчество которое необходимо подготовить к поиску.
// Возвращаемое значение:
//  Строка - подготовленное к поиску отчество.
Функция ОтчествоДляПоиска(Знач Отчество) Экспорт
	
	ПодготовитьКПоискуПоФИО(Отчество); 
	
	ПодготовитьКПоискуПоФИОСБуквамиЕЁ(Отчество);
	
	МассивПодстрок = Новый Массив;
	МассивПодстрок.Добавить("ИЕВИЧ");
	МассивПодстрок.Добавить("ЬЕВИЧ");
	МассивПодстрок.Добавить("ИЕВНА");
	МассивПодстрок.Добавить("ЬЕВНА");
	НетЗаменыСимволов = Истина;
	Для каждого Стр Из МассивПодстрок Цикл
		ПозицияСимвола = Найти(Отчество, Стр);
		Если ПозицияСимвола > 0 Тогда
			Отчество = Лев(Отчество,ПозицияСимвола-1) + "ИЬ" + Сред(Отчество,ПозицияСимвола+1);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НетЗаменыСимволов Тогда
		
		МассивПодстрок = Новый Массив;
		МассивПодстрок.Добавить("ЬЯНОВИЧ");
		МассивПодстрок.Добавить("ИЯНОВИЧ");
		МассивПодстрок.Добавить("ИАНОВИЧ");
		МассивПодстрок.Добавить("ЬЯНОВНА");
		МассивПодстрок.Добавить("ИЯНОВНА");
		МассивПодстрок.Добавить("ИАНОВНА");
		Для каждого Стр Из МассивПодстрок Цикл
			ПозицияСимвола = Найти(Отчество, Стр);
			Если ПозицияСимвола > 0 Тогда
				Отчество = Лев(Отчество,ПозицияСимвола-1) + "ИЬЯА" + Сред(Отчество,ПозицияСимвола+2);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Отчество;
	
КонецФункции

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ОбработчикиСобытий
Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		СтруктураФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(Наименование);
		
		НаименованиеСлужебное = СокрЛП(
		ФамилияДляПоиска(СтруктураФИО.Фамилия) + " "
		+ ИмяДляПоиска(СтруктураФИО.Имя) + " "
		+ ОтчествоДляПоиска(СтруктураФИО.Отчество));
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область СлужебныеПроцедурыИФункции
Процедура ПодготовитьКПоискуПоФИО(Представление)
	
	Представление = ВРег(Представление);
	
	Представление = СтрЗаменить(Представление, "~", "~~");
	Представление = СтрЗаменить(Представление, "%", "~%");
	Представление = СтрЗаменить(Представление, "_", "~_");
	Представление = СтрЗаменить(Представление, "[", "~[");
	Представление = СтрЗаменить(Представление, "-", "~-");
	
КонецПроцедуры

Процедура ПодготовитьКПоискуПоФИОСБуквамиЕЁ(Представление)
	
	Представление = СтрЗаменить(Представление, "Е", "&");
	Представление = СтрЗаменить(Представление, "Ё", "&");
	Представление = СтрЗаменить(Представление, "&", "ЕЁ");
	
КонецПроцедуры
#КонецОбласти

#КонецЕсли