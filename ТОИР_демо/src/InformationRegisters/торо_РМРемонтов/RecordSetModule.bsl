#Область ОбработчикиСобытий
Процедура ПередЗаписью(Отказ, Замещение)
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Таб.Период,
	               |	Таб.ВидРемонта,
	               |	Таб.Сезон,
	               |	Таб.ГруппаОбъектовРемонтов
	               |ПОМЕСТИТЬ ИсходнаяТаб
	               |ИЗ
	               |	&Таб КАК Таб
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИсходнаяТаб.Период,
	               |	ИсходнаяТаб.ВидРемонта,
	               |	ИсходнаяТаб.Сезон,
	               |	ЕСТЬNULL(торо_Сезоны.ДатаНачалаСезона, ДАТАВРЕМЯ(2, 1, 1)) КАК ДатаНачала,
	               |	ЕСТЬNULL(торо_Сезоны.ДатаОкончанияСезона, ДАТАВРЕМЯ(2, 12, 31)) КАК ДатаОкончания,
	               |	ИсходнаяТаб.ГруппаОбъектовРемонтов,
	               |	торо_Сезоны.Переходящий
	               |ПОМЕСТИТЬ ТаблицаДат
	               |ИЗ
	               |	ИсходнаяТаб КАК ИсходнаяТаб
	               |		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.торо_Сезоны КАК торо_Сезоны
	               |		ПО ИсходнаяТаб.Сезон = торо_Сезоны.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаДатСравнения.ВидРемонта,
	               |	ТаблицаДатСравнения.Сезон,
	               |	ТаблицаДатСравнения.Период,
	               |	ТаблицаДат.Сезон КАК СезонПересечения,
	               |	ТаблицаДат.ДатаНачала КАК ДатаНачалаПересечения,
	               |	ТаблицаДат.ДатаОкончания КАК ДатаОкончанияПересечения,
	               |	ТаблицаДатСравнения.ДатаНачала,
	               |	ТаблицаДатСравнения.ДатаОкончания ,
	               |	ТаблицаДатСравнения.ГруппаОбъектовРемонтов
	               |ИЗ
	               |	ТаблицаДат КАК ТаблицаДат
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДат КАК ТаблицаДатСравнения
	               |		ПО ТаблицаДат.Период = ТаблицаДатСравнения.Период
	               |			И ТаблицаДат.ВидРемонта = ТаблицаДатСравнения.ВидРемонта
	               |			И ТаблицаДат.ГруппаОбъектовРемонтов = ТаблицаДатСравнения.ГруппаОбъектовРемонтов
	               |			И ТаблицаДат.Сезон <> ТаблицаДатСравнения.Сезон
	               |			И (ВЫБОР
	               |				КОГДА ТаблицаДат.Переходящий
	               |					ТОГДА ТаблицаДат.ДатаНачала < ТаблицаДатСравнения.ДатаОкончания
	               |							ИЛИ ТаблицаДат.ДатаОкончания > ТаблицаДатСравнения.ДатаНачала
	               |				КОГДА ТаблицаДатСравнения.Переходящий
	               |					ТОГДА ТаблицаДатСравнения.ДатаНачала < ТаблицаДат.ДатаОкончания
	               |							ИЛИ ТаблицаДатСравнения.ДатаОкончания > ТаблицаДат.ДатаНачала
	               |				ИНАЧЕ НЕ(ТаблицаДат.ДатаНачала > ТаблицаДатСравнения.ДатаОкончания
	               |							ИЛИ ТаблицаДат.ДатаОкончания < ТаблицаДатСравнения.ДатаНачала)
	               |			КОНЕЦ)
	               |ГДЕ
	               |	ТаблицаДатСравнения.Сезон > ТаблицаДат.Сезон";
	
	Запрос.УстановитьПараметр("Таб",ЭтотОбъект.Выгрузить());
	
	Выборка = запрос.Выполнить().Выбрать();
	
	ТаблицаПересечения = новый ТаблицаЗначений;
	ТаблицаПересечения.Колонки.Добавить("Сезон");
	ТаблицаПересечения.Колонки.Добавить("СезонПересечения");
	
	Пока Выборка.Следующий() Цикл
		Отказ = Истина;
		
		торо_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'На период %1 для вида ремонта %2 обнаружено использование пересекающихся сезонов:
			|%3 (%4-%5) и 
			|%6 (%7-%8)'"),
			Выборка.Период, //1
			Выборка.ВидРемонта, //2
			Выборка.Сезон, //3
			Формат(Выборка.ДатаНачала,"ДФ='dd MMMM'"), //4
			Формат(Выборка.ДатаОкончания,"ДФ='dd MMMM'"), //5
			Выборка.СезонПересечения, //6
			Формат(Выборка.ДатаНачалаПересечения,"ДФ='dd MMMM'"), //7
			Формат(Выборка.ДатаОкончанияПересечения,"ДФ='dd MMMM'"))); //8
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти