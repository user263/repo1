
#Область ПрограммныйИнтерфейс

// Проверяет наличие выделенных в списке строк.
//
// Параметры:
// 	Список - ДинамическийСписок - список.
//
// Возвращаемое значение:
// 	Булево - Ложь - если в списке нет выделенных строк, иначе Истина.
//
Функция ПроверитьНаличиеВыделенныхВСпискеСтрок(Список) Экспорт
	
	МассивСсылок = Новый Массив;
	
	Для Сч = 0 По Список.ВыделенныеСтроки.Количество()-1 Цикл
		Если ТипЗнч(Список.ВыделенныеСтроки[Сч]) <> Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			МассивСсылок.Добавить(Список.ВыделенныеСтроки[Сч]);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСсылок.Количество() = 0 Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru='Команда не может быть выполнена для указанного объекта!'"));
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции // ПроверитьНаличиеВыделенныхВСпискеСтрок()

// Проверяет наличие выделенных в списке ссылок и возвращает массив.
//
// Параметры:
// 		Список - ДинамическийСписок - список.
//
// Возвращаемое значение:
// 		Массив - Массив выделенных с списке ссылок.
//
Функция ПроверитьПолучитьВыделенныеВСпискеСсылки(Список) Экспорт
	
	МассивСсылок = Новый Массив;
	
	Для Сч = 0 По Список.ВыделенныеСтроки.Количество()-1 Цикл
		Если ТипЗнч(Список.ВыделенныеСтроки[Сч]) <> Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			МассивСсылок.Добавить(Список.ВыделенныеСтроки[Сч]);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСсылок.Количество() = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Команда не может быть выполнена для указанного объекта!'"));
	КонецЕсли;
	
	Возврат МассивСсылок;
	
КонецФункции // ПроверитьПолучитьВыделенныеВСпискеСсылки()

// Проверяет наличие строк в списке.
// Параметры:
//	 Список - Массив, СписокЗначений, ТаблицаЗначений - проверяемый список,
//	 ПредставлениеСписка - Строка - представление.
// Возвращаемое значение:
//		Булево -	Истина - строки в списке имеются, Ложь - строки в списке отсутствуют.
Функция ПроверитьНаличиеСтрокВСписке(Список, ПредставлениеСписка) Экспорт
	
	Если Список.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не введено ни одной строки в список ""%ПредставлениеСписка%""'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПредставлениеСписка%", ПредставлениеСписка);
		ПоказатьПредупреждение(Неопределено,ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

