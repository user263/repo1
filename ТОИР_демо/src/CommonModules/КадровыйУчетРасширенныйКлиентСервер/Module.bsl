////////////////////////////////////////////////////////////////////////////////
// КадровыйУчетРасширенныйКлиентСервер: методы кадрового учета, работающие на стороне клиента и сервера.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает комментарий к количеству ставок.
// Параметры:
//  ТекущееКоличествоСтавок - Число - текущее количество ставок.
//  НовоеКоличествоСтавок - Число - новое количество ставок.
//  ИзменитьПозицию - Булево - изменить позицию.
// Возвращаемое значение:
//  Строка - комментарий.
Функция КомментарийККоличествуСтавок(ТекущееКоличествоСтавок, НовоеКоличествоСтавок, ИзменитьПозицию) Экспорт 
	
	Если ТекущееКоличествоСтавок = 0 Тогда
		Возврат "";
	Иначе
		
		Если НЕ ИзменитьПозицию ИЛИ НовоеКоличествоСтавок = ТекущееКоличествоСтавок Тогда
			Возврат "";
		Иначе
			
			Если НовоеКоличествоСтавок > ТекущееКоличествоСтавок Тогда
				Возврат "(+" + Формат(НовоеКоличествоСтавок - ТекущееКоличествоСтавок, "ЧН=' '") + ")";
			Иначе
				Возврат "(" + Формат(НовоеКоличествоСтавок - ТекущееКоличествоСтавок, "ЧН=' '") + ")";
			КонецЕсли;		
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// Установка доступности командной панели

// Устанавливает доступность элементов комендной панели.
// Параметры:
//  Форма - УправляемаяФорма - форма.
//  ИмяКоманднойПанели - Строка - имя командной панели.
//  Доступность - Булево - доступность, которую нужно установить.
Процедура УстановитьДоступностьКоманднойПанели(Форма, ИмяКоманднойПанели, Доступность) Экспорт
	
	КоманднаяПанель = Форма.Элементы.Найти(ИмяКоманднойПанели);
	Если КоманднаяПанель = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	УстановитьДоступностьЭлементовКоманднойПанели(КоманднаяПанель, Доступность);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьДоступностьЭлементовКоманднойПанели(КоманднаяПанель, Доступность)
	
	Для каждого ЭлементПанели Из КоманднаяПанель.ПодчиненныеЭлементы Цикл
		
		Если ЭлементПанели.Вид = ВидГруппыФормы.Страницы
			ИЛИ ЭлементПанели.Вид = ВидГруппыФормы.Страница Тогда
			УстановитьДоступностьЭлементовКоманднойПанели(ЭлементПанели, Доступность);
		Иначе
			ЭлементПанели.Доступность = Доступность;
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти