////////////////////////////////////////////////////////////////////////////////
// КадровыйУчетРасширенныйКлиент: методы кадрового учета, работающие на стороне клиента.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Открывает форму выбора справочника Сотрудники с отбором только работающих
// сотрудников на указанную дату и в указанных, организации и подразделении.
//
// Параметры:
//		ВладелецФормыВыбора - УправляемаяФорма, ПолеВвода - Форма или элемент формы, для которого открывается форма выбора
//		Организация - СправочникСсылка.Организации - если не указана, в отбор попадают все сотрудники компании
//		Подразделение - СправочникСсылка.ПодразделенияОрганизации - если не указано - в отбор попадают все сотрудники
//		ДатаПримененияОтбора - Дата - на которую необходимо выбрать работающих сотрудников
//    МножественныйВыбор - Булево - если ложь - форма откроется для выбора одного сотрудника.
//
Процедура ВыбратьСотрудниковРаботающихНаДату(ВладелецФормыВыбора, Организация = Неопределено, Подразделение = Неопределено, ДатаПримененияОтбора = '00010101', МножественныйВыбор = Истина) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", МножественныйВыбор);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", НЕ МножественныйВыбор);
	
	СтруктураОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураОтбора.Вставить("ТекущаяОрганизация", Организация);
	КонецЕсли; 
		
	Если ЗначениеЗаполнено(Подразделение) Тогда
		СтруктураОтбора.Вставить("ТекущееПодразделение", Подразделение);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДатаПримененияОтбора) Тогда
		СтруктураОтбора.Вставить("ДатаПримененияОтбора", ДатаПримененияОтбора);
	КонецЕсли; 
	
	Если СтруктураОтбора.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	КонецЕсли; 
	
	ОткрытьФорму("Справочник.Сотрудники.ФормаВыбора", ПараметрыФормы, ВладелецФормыВыбора);	
	
КонецПроцедуры

// Открывает форму выбора справочника Сотрудники с отбором только работающих
// сотрудников в периоде.
//
// Параметры:
//		ВладелецФормыВыбора - УправляемаяФорма, ПолеВвода - Форма или элемент формы, для которого открывается форма выбора.
//		Организация - СправочникСсылка.Организации - если не указана, в отбор попадают все сотрудники компании.
//		Подразделение - СправочникСсылка.ПодразделенияОрганизации - если не указано - в отбор попадают все сотрудники.
//		НачалоПериодаПримененияОтбора - Дата - начало периода для отбора сотрудников.
//		ОкончаниеПериодаПримененияОтбора - Дата - окончание периода для отбора сотрудников.
//    МножественныйВыбор - Булево - если ложь - форма откроется для выбора одного сотрудника.
//
Процедура ВыбратьСотрудниковРаботающихВПериоде(ВладелецФормыВыбора, Организация = Неопределено, Подразделение = Неопределено, НачалоПериодаПримененияОтбора = '00010101', ОкончаниеПериодаПримененияОтбора = '00010101', МножественныйВыбор = Истина) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", МножественныйВыбор);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", НЕ МножественныйВыбор);
		
	СтруктураОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураОтбора.Вставить("ТекущаяОрганизация", Организация);
	КонецЕсли; 
		
	Если ЗначениеЗаполнено(Подразделение) Тогда
		СтруктураОтбора.Вставить("ТекущееПодразделение", Подразделение);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(НачалоПериодаПримененияОтбора) Тогда
		СтруктураОтбора.Вставить("НачалоПериодаПримененияОтбора", НачалоПериодаПримененияОтбора);
		СтруктураОтбора.Вставить("ОкончаниеПериодаПримененияОтбора", ОкончаниеПериодаПримененияОтбора);
	КонецЕсли; 
	
	Если СтруктураОтбора.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	КонецЕсли; 
	
	ОткрытьФорму("Справочник.Сотрудники.ФормаВыбора", ПараметрыФормы, ВладелецФормыВыбора);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьНаСоответствиеШтатномуРасписанию(Форма, Объект, ИсправленныйДокумент = НеОпределено) Экспорт 

КонецПроцедуры

Процедура КоличествоСтавокРегулирование(КоличествоСтавок, Направление, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	КоличествоСтавок = Окр(КоличествоСтавок + Направление, 0, ?(Направление > 0, РежимОкругления.Окр15как10, РежимОкругления.Окр15как20));
	
КонецПроцедуры	

#КонецОбласти
