////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обмен данными"
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ОбменТОиР20ЕРП20_ЗарегистрироватьИзменениеПередЗаписью(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.торо_ОбъектыРемонта") Тогда
		
		Если Источник.ЭтоГруппа или Источник.ПометкаУдаления или НЕ Источник.ЭтоАктив Тогда Возврат; КонецЕсли;
		
		УзелДляОтправки = ОбменДаннымиПовтИсп.ПолучитьУзелДляРегистрцииИзменений("ОбменТОиР20ЕРП20");
		Если УзелДляОтправки = Неопределено или УзелДляОтправки.ПереноситьАктивыВУПП = Ложь Тогда Возврат; КонецЕсли;
		
		Если Источник.ЭтоНовый() И НЕ ЗначениеЗаполнено(Источник.ПолучитьСсылкуНового()) Тогда
			НоваяСсылка = Справочники[Источник.Метаданные().Имя].ПолучитьСсылку();
			Источник.УстановитьСсылкуНового(НоваяСсылка);
		КонецЕсли;
		ПланыОбмена.ЗарегистрироватьИзменения(УзелДляОтправки, Источник);
		
	Иначе
		ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписью("ОбменТОиР20ЕРП20", Источник, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбменТОиР20ЕРП20_ЗарегистрироватьИзменениеДокументаПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Источник.ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	УзелДляОтправки = ОбменДаннымиПовтИсп.ПолучитьУзелДляРегистрцииИзменений("ОбменТОиР20ЕРП20");
	Если УзелДляОтправки = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда
		НоваяСсылка = Документы[Источник.Метаданные().Имя].ПолучитьСсылку();
		Источник.УстановитьСсылкуНового(НоваяСсылка);
	КонецЕсли;
	ПланыОбмена.ЗарегистрироватьИзменения(УзелДляОтправки, Источник);	
	
КонецПроцедуры

Процедура ОбменТОиР20ЕРП20_ЗарегистрироватьИзменениеНабораЗаписейПередЗаписью(Источник, Отказ, Замещение) Экспорт
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписьюРегистра("ОбменТОиР20ЕРП20", Источник, Отказ,Замещение);
КонецПроцедуры

Процедура ОбменТОиР20ЕРП20_ЗарегистрироватьУдалениеДокументаПередУдалением(Источник, Отказ) Экспорт
	УзелДляОтправки = ОбменДаннымиПовтИсп.ПолучитьУзелДляРегистрцииИзменений("ОбменТОиР20ЕРП20");
	Если УзелДляОтправки = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда
		НоваяСсылка = Документы[Источник.Метаданные().Имя].ПолучитьСсылку();
		Источник.УстановитьСсылкуНового(НоваяСсылка);
	КонецЕсли;
	ПланыОбмена.ЗарегистрироватьИзменения(УзелДляОтправки, Источник);
КонецПроцедуры

#КонецОбласти
