#Область ПрограммныйИнтерфейс

// Получает форматную строка для объекта конфигурации и пользователя.
//	Если для пользователя нет индивидуальных настроек, используется значение по умолчанию.
//
// Параметры:
//   ОбъектСтрока - Строка - Строковое представление объекта метаданных.
// 
// Возвращаемое значение:
//		Строка - форматная строка.
Функция ПолучитьФорматнуюСтроку(ОбъектСтрока) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("торо_ИспользоватьНастраиваемыеПредставления") Тогда
		Возврат Неопределено;
	ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("торо_РазрешитьПользовательскуюНастройкуПредставлений") ИЛИ ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПредставлений", "ПредставлениеДля"+ОбъектСтрока+"ПоУмолчанию", Истина) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ПараметрыПользовательскогоПредставленияОбъектов.ОбъектКонфигурации,
	               |	торо_ПараметрыПользовательскогоПредставленияОбъектов.ФорматнаяСтрокаПоУмолчанию
				   |ИЗ
	               |	РегистрСведений.торо_ПараметрыПользовательскогоПредставленияОбъектов КАК торо_ПараметрыПользовательскогоПредставленияОбъектов
				   |ГДЕ
				   |	торо_ПараметрыПользовательскогоПредставленияОбъектов.ОбъектКонфигурации = &ОбъектСтрока";
		Запрос.УстановитьПараметр("ОбъектСтрока",ОбъектСтрока);		   
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ФорматнаяСтрока = Выборка.ФорматнаяСтрокаПоУмолчанию;
		Иначе
			ФорматнаяСтрока = "%Наименование%";
		КонецЕсли;
	Иначе	
		ФорматнаяСтрока = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПредставлений", "ПредставлениеДля"+ОбъектСтрока);
	КонецЕсли;
	
	Возврат ФорматнаяСтрока;
	
КонецФункции

// Получает список реквизитов, которые можно использовать при формировании форматной строки в конструкторе.
//	Список реквизитов задается разработчиком без возможности расширения пользователем.
//
// Параметры:
//   ОбъектКонфигурации - Строка - Строковое представление объекта метаданных.
// 
// Возвращаемое значение:
//		СписокЗначений - список реквизитов.
//
Функция ПолучитьСписокРеквизитовДляФорматнойСтроки(ОбъектКонфигурации) Экспорт
	
	СписокВозврата = Новый СписокЗначений;
	
	Если Найти(ОбъектКонфигурации,"Справочники.") > 0 Тогда
		Справочники[Сред(ОбъектКонфигурации,13)].ЗаполнитьСписокРеквизитовДляФорматнойСтроки(СписокВозврата);
	ИначеЕсли Найти(ОбъектКонфигурации,"Документы.") > 0 Тогда
		Документы[Сред(ОбъектКонфигурации,11)].ЗаполнитьСписокРеквизитовДляФорматнойСтроки(СписокВозврата);
	КонецЕсли;
	
	Возврат СписокВозврата;
	
КонецФункции

#КонецОбласти