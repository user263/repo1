
////////////////////////////////////////////////////////////////////////////////
// торо_ГарантийноеОбслуживание: методы, реализующие соответствующий функционал
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс
// Функция определяет гарантийный ли ремонт, с указанными датами начала и окончания.
//
// Параметры:
//	 ОбъектРемонта - СправочникСсылка.торо_ОбъектыРемонта - объект ремонта,
//	 ДатаНачала    - Дата - дата начала ремонта, 
//	 ДатаОкончания - Дата - дата окончания ремонта.
//
// Возвращаемое значение:
//		Булево - ремонт гарантийный.
Функция ПолучитьЗначениеФлагаГарантийныйРемонтДляОбъектаРемонта(ОбъектРемонта,ДатаНачала,ДатаОкончания) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаНачала) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТаблицаГаранитийОбъекта = ПолучитьТаблицуГарантий(ОбъектРемонта, ДатаОкончания);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаГаранитийОбъекта.ОбъектРемонта,
	|	ТаблицаГаранитийОбъекта.ПроверятьПериодГарантии,
	|	ТаблицаГаранитийОбъекта.ДатаНачала,
	|	ТаблицаГаранитийОбъекта.ДатаОкончания,
	|	ТаблицаГаранитийОбъекта.ПроверятьНаработку,
	|	ТаблицаГаранитийОбъекта.ПоказательНаработки,
	|	ТаблицаГаранитийОбъекта.ЗначениеНаработки
	|ПОМЕСТИТЬ ТаблицаГаранитийОбъекта
	|ИЗ
	|	&ТаблицаГаранитийОбъекта КАК ТаблицаГаранитийОбъекта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаГаранитийОбъекта.ОбъектРемонта КАК ОбъектРемонта,
	|	ТаблицаГаранитийОбъекта.ПроверятьПериодГарантии,
	|	ТаблицаГаранитийОбъекта.ДатаНачала,
	|	ТаблицаГаранитийОбъекта.ДатаОкончания,
	|	ТаблицаГаранитийОбъекта.ПроверятьНаработку,
	|	ТаблицаГаранитийОбъекта.ПоказательНаработки КАК ПоказательНаработки,
	|	ТаблицаГаранитийОбъекта.ЗначениеНаработки
	|ПОМЕСТИТЬ ГарантииПоСроку
	|ИЗ
	|	ТаблицаГаранитийОбъекта КАК ТаблицаГаранитийОбъекта
	|ГДЕ
	|	ТаблицаГаранитийОбъекта.ПроверятьПериодГарантии = ИСТИНА
	|	И ТаблицаГаранитийОбъекта.ДатаОкончания >= &ДатаНачала
	|	И ТаблицаГаранитийОбъекта.ДатаНачала <= &ДатаНачала
	|	И (&ДатаОкончания >= ТаблицаГаранитийОбъекта.ДатаНачала
	|			ИЛИ &БезДатыОкончания)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаГаранитийОбъекта.ОбъектРемонта,
	|	ТаблицаГаранитийОбъекта.ПроверятьПериодГарантии,
	|	ТаблицаГаранитийОбъекта.ДатаНачала,
	|	ТаблицаГаранитийОбъекта.ДатаОкончания,
	|	ТаблицаГаранитийОбъекта.ПроверятьНаработку,
	|	ТаблицаГаранитийОбъекта.ПоказательНаработки,
	|	ТаблицаГаранитийОбъекта.ЗначениеНаработки
	|ИЗ
	|	ТаблицаГаранитийОбъекта КАК ТаблицаГаранитийОбъекта
	|ГДЕ
	|	ТаблицаГаранитийОбъекта.ПроверятьПериодГарантии = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_НаработкаОбъектовРемонтаОстатки.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонтаОстатки.Показатель КАК Показатель,
	|	торо_НаработкаОбъектовРемонтаОстатки.НаработкаОстаток КАК Наработка
	|ПОМЕСТИТЬ ТекущаяНаработка
	|ИЗ
	|	РегистрНакопления.торо_НаработкаОбъектовРемонта.Остатки(
	|			&ДатаНачала,
	|			(ОбъектРемонта, Показатель) В
	|				(ВЫБРАТЬ
	|					ГарантииПоСроку.ОбъектРемонта,
	|					ГарантииПоСроку.ПоказательНаработки
	|				ИЗ
	|					ГарантииПоСроку КАК ГарантииПоСроку
	|				ГДЕ
	|					ГарантииПоСроку.ПроверятьНаработку)) КАК торо_НаработкаОбъектовРемонтаОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГарантииПоСроку.ОбъектРемонта,
	|	ГарантииПоСроку.ПроверятьПериодГарантии,
	|	ГарантииПоСроку.ДатаНачала,
	|	ГарантииПоСроку.ДатаОкончания,
	|	ГарантииПоСроку.ПроверятьНаработку,
	|	ГарантииПоСроку.ПоказательНаработки,
	|	ГарантииПоСроку.ЗначениеНаработки,
	|	ЕСТЬNULL(ТекущаяНаработка.Наработка, 0) КАК Наработка
	|ИЗ
	|	ГарантииПоСроку КАК ГарантииПоСроку
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущаяНаработка КАК ТекущаяНаработка
	|		ПО ГарантииПоСроку.ПроверятьНаработку
 	|			И ГарантииПоСроку.ОбъектРемонта = ТекущаяНаработка.ОбъектРемонта
	|			И ГарантииПоСроку.ПоказательНаработки = ТекущаяНаработка.Показатель
	|ГДЕ
	|	(ГарантииПоСроку.ПроверятьНаработку = ЛОЖЬ
	|			ИЛИ ЕСТЬNULL(ТекущаяНаработка.Наработка, 0) <= ГарантииПоСроку.ЗначениеНаработки)";
	
	Запрос.УстановитьПараметр("ТаблицаГаранитийОбъекта", ТаблицаГаранитийОбъекта);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("БезДатыОкончания", НЕ ЗначениеЗаполнено(ДатаОкончания));
	
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции

// Функция возвращает дату начала и окончания гарантии, объект гарантии для объекта ремонта переданного
// параметром ОбъектРемонта.
//
// Параметры:
//	 ОбъектРемонта - СправочникСсылка.торо_ОбъектыРемонта - объект ремонта, указанный в строке,
//  ДатаОкончания - Дата - дата окончания ремонта, используется
//					для определения положения в структуре иерархии для иераррхии,
//					в которой объекты перемещаются документами.
//
// Возвращаемое значение:
//		ТаблицаЗначений - таблица гарантий.
//
Функция ПолучитьТаблицуГарантий(ОбъектРемонта, ДатаОкончания = Неопределено) Экспорт
	
	Иерархия = Константы.торо_ИерархияДляВводаНовыхОР.Получить();
	
	СписокОбъектовДляПолученияСроковГарантии = ПолучитьСписокОбъектовДляПолученияСроковГарантии(ОбъектРемонта,Иерархия, ДатаОкончания);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_СрокиГарантииОбъектовРемонта.ОбъектРемонта,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ДатаНачалаГарантии КАК ДатаНачала,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ДатаОкончанияГарантии КАК ДатаОкончания,
	               |	торо_СрокиГарантииОбъектовРемонта.ГарантирующаяОрганизация,
	               |	торо_СрокиГарантииОбъектовРемонта.Договор,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ПроверятьПериодГарантии КАК ПроверятьПериодГарантии,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ПроверятьНаработку КАК ПроверятьНаработку,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ПоказательНаработки КАК ПоказательНаработки,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ЗначениеНаработки КАК ЗначениеНаработки
	               |ИЗ
	               |	РегистрСведений.торо_СрокиГарантииОбъектовРемонта КАК торо_СрокиГарантииОбъектовРемонта
	               |ГДЕ
	               |	торо_СрокиГарантииОбъектовРемонта.ОбъектРемонта В(&ОбъектРемонта)";
	
	Запрос.УстановитьПараметр("ОбъектРемонта", СписокОбъектовДляПолученияСроковГарантии);
	
	ТаблицаГарантий = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаГарантий;
	
КонецФункции

// Функция для каждого Объекта ремонта из таблицы возвращает
// Даты начала и окончания гарантии и гарантирующую организацию
// для объектов ремонта, даты ремонтов которых находятся в гарантийном периоде.
//
// Параметры:
//	 ТаблицаСОбъектамиИДатамиРемонтов - ТаблицаЗначений - с колонками: ОбъектРемонта, ДатаНачала, ДатаОкончания.
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица гарантий.
Функция ПолучитьТаблицуГарантийДляВсехОбъектов(ТаблицаСОбъектамиИДатамиРемонтов) Экспорт
	
	ТаблицаСОбъектамиГаранитии = ЗаполнитьРодителей(ТаблицаСОбъектамиИДатамиРемонтов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаСОбъектамиГаранитии.ОбъектРемонта КАК ОбъектРемонта,
	               |	ТаблицаСОбъектамиГаранитии.ОбъектГарантии КАК ОбъектГарантии,
	               |	ТаблицаСОбъектамиГаранитии.ДатаНачала КАК ДатаНачала,
	               |	ТаблицаСОбъектамиГаранитии.ДатаОкончания КАК ДатаОкончания
	               |ПОМЕСТИТЬ втТаблицаДляПолученияГарантийнойИнформации
	               |ИЗ
	               |	&ТаблицаСОбъектамиГаранитии КАК ТаблицаСОбъектамиГаранитии
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТаблицаДляПолученияГарантийнойИнформации.ОбъектРемонта КАК ОбъектРемонта,
	               |	втТаблицаДляПолученияГарантийнойИнформации.ДатаНачала КАК ДатаНачала,
	               |	втТаблицаДляПолученияГарантийнойИнформации.ДатаОкончания КАК ДатаОкончания,
	               |	торо_СрокиГарантииОбъектовРемонта.ОбъектРемонта КАК ГарантийныйОбъект,
	               |	торо_СрокиГарантииОбъектовРемонта.ГарантирующаяОрганизация КАК ГарантирующаяОрганизация,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ДатаНачалаГарантии КАК ДатаНачалаГарантии,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ДатаОкончанияГарантии КАК ДатаОкончанияГарантии,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ПроверятьПериодГарантии КАК ПроверятьПериодГарантии,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ПроверятьНаработку КАК ПроверятьНаработку,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ПоказательНаработки КАК ПоказательНаработки,
	               |	торо_СрокиГарантииОбъектовРемонта.УсловияГарантии.ЗначениеНаработки КАК ЗначениеНаработки
	               |ПОМЕСТИТЬ УсловияГарантии
	               |ИЗ
	               |	втТаблицаДляПолученияГарантийнойИнформации КАК втТаблицаДляПолученияГарантийнойИнформации
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_СрокиГарантииОбъектовРемонта КАК торо_СрокиГарантииОбъектовРемонта
	               |		ПО втТаблицаДляПолученияГарантийнойИнформации.ОбъектГарантии = торо_СрокиГарантииОбъектовРемонта.ОбъектРемонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УсловияГарантии.ОбъектРемонта КАК ОбъектРемонта,
	               |	УсловияГарантии.ДатаНачала КАК ДатаНачала,
	               |	УсловияГарантии.ДатаОкончания КАК ДатаОкончания,
	               |	УсловияГарантии.ГарантийныйОбъект КАК ГарантийныйОбъект,
	               |	УсловияГарантии.ГарантирующаяОрганизация КАК ГарантирующаяОрганизация,
	               |	УсловияГарантии.ДатаНачалаГарантии КАК ДатаНачалаГарантии,
	               |	УсловияГарантии.ДатаОкончанияГарантии КАК ДатаОкончанияГарантии,
	               |	УсловияГарантии.ПроверятьПериодГарантии КАК ПроверятьПериодГарантии,
	               |	УсловияГарантии.ПроверятьНаработку КАК ПроверятьНаработку,
	               |	УсловияГарантии.ПоказательНаработки КАК ПоказательНаработки,
	               |	УсловияГарантии.ЗначениеНаработки КАК ЗначениеНаработки
	               |ПОМЕСТИТЬ ГарантииПоСрокам
	               |ИЗ
	               |	УсловияГарантии КАК УсловияГарантии
	               |ГДЕ
	               |	УсловияГарантии.ПроверятьПериодГарантии = ИСТИНА
	               |	И УсловияГарантии.ДатаОкончанияГарантии > УсловияГарантии.ДатаНачала
	               |	И УсловияГарантии.ДатаНачалаГарантии <= УсловияГарантии.ДатаНачала
	               |	И (УсловияГарантии.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	               |			ИЛИ УсловияГарантии.ДатаОкончания > УсловияГарантии.ДатаНачалаГарантии)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	УсловияГарантии.ОбъектРемонта,
	               |	УсловияГарантии.ДатаНачала,
	               |	УсловияГарантии.ДатаОкончания,
	               |	УсловияГарантии.ГарантийныйОбъект,
	               |	УсловияГарантии.ГарантирующаяОрганизация,
	               |	УсловияГарантии.ДатаНачалаГарантии,
	               |	УсловияГарантии.ДатаОкончанияГарантии,
	               |	УсловияГарантии.ПроверятьПериодГарантии,
	               |	УсловияГарантии.ПроверятьНаработку,
	               |	УсловияГарантии.ПоказательНаработки,
	               |	УсловияГарантии.ЗначениеНаработки
	               |ИЗ
	               |	УсловияГарантии КАК УсловияГарантии
	               |ГДЕ
	               |	УсловияГарантии.ПроверятьПериодГарантии = ЛОЖЬ
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ОбъектРемонта,
	               |	ПоказательНаработки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	торо_НаработкаОбъектовРемонтаОстатки.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_НаработкаОбъектовРемонтаОстатки.Показатель КАК Показатель,
	               |	торо_НаработкаОбъектовРемонтаОстатки.Период КАК ДатаКон,
	               |	торо_НаработкаОбъектовРемонтаОстатки.НаработкаОборот КАК Наработка
	               |ПОМЕСТИТЬ РазвернутаяНаработка
	               |ИЗ
	               |	РегистрНакопления.торо_НаработкаОбъектовРемонта.Обороты(
	               |			&МинДатаНачала,
	               |			&МаксДатаНачала,
	               |			Секунда,
	               |			(ОбъектРемонта, Показатель) В
	               |				(ВЫБРАТЬ
	               |					ГарантииПоСрокам.ОбъектРемонта,
	               |					ГарантииПоСрокам.ПоказательНаработки
	               |				ИЗ
	               |					ГарантииПоСрокам КАК ГарантииПоСрокам
	               |				ГДЕ
	               |					ГарантииПоСрокам.ПроверятьНаработку)) КАК торо_НаработкаОбъектовРемонтаОстатки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	торо_НаработкаОбъектовРемонтаОстатки.ОбъектРемонта,
	               |	торо_НаработкаОбъектовРемонтаОстатки.Показатель,
	               |	&МинДатаНачала,
	               |	торо_НаработкаОбъектовРемонтаОстатки.НаработкаОстаток
	               |ИЗ
	               |	РегистрНакопления.торо_НаработкаОбъектовРемонта.Остатки(
	               |			&МинДатаНачала,
	               |			(ОбъектРемонта, Показатель) В
	               |				(ВЫБРАТЬ
	               |					ГарантииПоСрокам.ОбъектРемонта,
	               |					ГарантииПоСрокам.ПоказательНаработки
	               |				ИЗ
	               |					ГарантииПоСрокам КАК ГарантииПоСрокам
	               |				ГДЕ
	               |					ГарантииПоСрокам.ПроверятьНаработку)) КАК торо_НаработкаОбъектовРемонтаОстатки
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ОбъектРемонта,
	               |	Показатель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ГарантииПоСрокам.ОбъектРемонта КАК ОбъектРемонта,
	               |	ГарантииПоСрокам.ДатаНачала КАК ДатаНачала,
	               |	ГарантииПоСрокам.ДатаОкончания КАК ДатаОкончания,
	               |	ГарантииПоСрокам.ГарантийныйОбъект КАК ГарантийныйОбъект,
	               |	ГарантииПоСрокам.ГарантирующаяОрганизация КАК ГарантирующаяОрганизация,
	               |	ГарантииПоСрокам.ДатаНачалаГарантии КАК ДатаНачалаГарантии,
	               |	ГарантииПоСрокам.ДатаОкончанияГарантии КАК ДатаОкончанияГарантии,
	               |	ГарантииПоСрокам.ПроверятьПериодГарантии КАК ПроверятьПериодГарантии,
	               |	ГарантииПоСрокам.ПроверятьНаработку КАК ПроверятьНаработку,
	               |	ГарантииПоСрокам.ПоказательНаработки КАК ПоказательНаработки,
	               |	ГарантииПоСрокам.ЗначениеНаработки КАК ЗначениеНаработки,
	               |	СУММА(ЕСТЬNULL(РазвернутаяНаработка.Наработка, 0)) КАК Наработка
	               |ПОМЕСТИТЬ ДанныеСНаработкой
	               |ИЗ
	               |	ГарантииПоСрокам КАК ГарантииПоСрокам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РазвернутаяНаработка КАК РазвернутаяНаработка
	               |		ПО (ГарантииПоСрокам.ПроверятьНаработку)
	               |			И ГарантииПоСрокам.ОбъектРемонта = РазвернутаяНаработка.ОбъектРемонта
	               |			И ГарантииПоСрокам.ПоказательНаработки = РазвернутаяНаработка.Показатель
	               |			И ГарантииПоСрокам.ДатаНачала >= РазвернутаяНаработка.ДатаКон
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ГарантииПоСрокам.ОбъектРемонта,
	               |	ГарантииПоСрокам.ДатаНачала,
	               |	ГарантииПоСрокам.ДатаОкончания,
	               |	ГарантииПоСрокам.ГарантийныйОбъект,
	               |	ГарантииПоСрокам.ГарантирующаяОрганизация,
	               |	ГарантииПоСрокам.ДатаНачалаГарантии,
	               |	ГарантииПоСрокам.ДатаОкончанияГарантии,
	               |	ГарантииПоСрокам.ПроверятьПериодГарантии,
	               |	ГарантииПоСрокам.ПроверятьНаработку,
	               |	ГарантииПоСрокам.ПоказательНаработки,
	               |	ГарантииПоСрокам.ЗначениеНаработки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеСНаработкой.ОбъектРемонта КАК ОбъектРемонта,
	               |	ДанныеСНаработкой.ДатаНачала КАК ДатаНачала,
	               |	ДанныеСНаработкой.ДатаОкончания КАК ДатаОкончания,
	               |	ДанныеСНаработкой.ГарантийныйОбъект КАК ГарантийныйОбъект,
	               |	ДанныеСНаработкой.ГарантирующаяОрганизация КАК ГарантирующаяОрганизация,
	               |	ДанныеСНаработкой.ДатаНачалаГарантии КАК ДатаНачалаГарантии,
	               |	ДанныеСНаработкой.ДатаОкончанияГарантии КАК ДатаОкончанияГарантии,
	               |	ДанныеСНаработкой.ПроверятьПериодГарантии КАК ПроверятьПериодГарантии,
	               |	ДанныеСНаработкой.ПроверятьНаработку КАК ПроверятьНаработку,
	               |	ДанныеСНаработкой.ПоказательНаработки КАК ПоказательНаработки,
	               |	ДанныеСНаработкой.ЗначениеНаработки КАК ЗначениеНаработки,
	               |	ДанныеСНаработкой.Наработка КАК Наработка
	               |ИЗ
	               |	ДанныеСНаработкой КАК ДанныеСНаработкой
	               |ГДЕ
	               |	(ДанныеСНаработкой.ПроверятьНаработку = ЛОЖЬ
	               |			ИЛИ ДанныеСНаработкой.Наработка <= ДанныеСНаработкой.ЗначениеНаработки)";
	
	Запрос.УстановитьПараметр("ТаблицаСОбъектамиГаранитии", ТаблицаСОбъектамиГаранитии);
	
	Если ТаблицаСОбъектамиГаранитии.Количество() > 0 Тогда
		МаксДатаНачала = ТаблицаСОбъектамиГаранитии[0].ДатаНачала;
		МинДатаНачала = ТаблицаСОбъектамиГаранитии[0].ДатаНачала;
		Для каждого СтрокаТаблицы из ТаблицаСОбъектамиГаранитии Цикл
			Если СтрокаТаблицы.ДатаНачала > МаксДатаНачала Тогда
				МаксДатаНачала = СтрокаТаблицы.ДатаНачала;
			КонецЕсли;
			Если СтрокаТаблицы.ДатаНачала < МинДатаНачала Тогда
				МинДатаНачала = СтрокаТаблицы.ДатаНачала;
			КонецЕсли;
		КонецЦикла;
		Запрос.УстановитьПараметр("МаксДатаНачала", МаксДатаНачала);
		Запрос.УстановитьПараметр("МинДатаНачала", МинДатаНачала);
	Иначе
		Запрос.УстановитьПараметр("МинДатаНачала", Дата(1,1,1));
		Запрос.УстановитьПараметр("МаксДатаНачала", Дата(1,1,1));
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Формирует предупреждения.
//
// Параметры:
//  ОбъектФормы - Объект - объект формы.
//  ИмяТЧ - Строка - имя табличной части.
Процедура СформироватьПредупрежденияОРекламационныхАктах(Знач ОбъектФормы, ИмяТЧ) Экспорт
	
	ТаблицаСОбъектамиИДатамиРемонтов = ОбъектФормы[ИмяТЧ].Выгрузить().Скопировать(Новый Структура("ГарантийныйРемонт",Истина)); 
	
	КолонкаОтказавшийЭлемент = ТаблицаСОбъектамиИДатамиРемонтов.Колонки.Найти("ОтказавшийЭлемент");
	КолонкаПлановаяДатаРемонта =  ТаблицаСОбъектамиИДатамиРемонтов.Колонки.Найти("ПлановаяДатаРемонта");
	
	Если Не КолонкаОтказавшийЭлемент = Неопределено Тогда
		ТаблицаСОбъектамиИДатамиРемонтов.Колонки.ОбъектРемонта.Имя     = "ОбъектРемонтаРодитель";
		ТаблицаСОбъектамиИДатамиРемонтов.Колонки.ОтказавшийЭлемент.Имя = "ОбъектРемонта";
		ТаблицаСОбъектамиИДатамиРемонтов.Колонки.Добавить("ДатаОкончания");
		ТаблицаСОбъектамиИДатамиРемонтов.Колонки.Добавить("ДатаНачала");
		ТаблицаСОбъектамиИДатамиРемонтов.ЗаполнитьЗначения(ОбъектФормы.ДатаОбнаружения, "ДатаНачала");
	ИначеЕсли Не КолонкаПлановаяДатаРемонта = Неопределено Тогда
		
		ТаблицаСОбъектамиИДатамиРемонтов.Колонки.ПлановаяДатаРемонта.Имя = "ДатаОкончания";
		ТаблицаСОбъектамиИДатамиРемонтов.Колонки.Добавить("ДатаНачала");
		Для каждого Строка Из ТаблицаСОбъектамиИДатамиРемонтов Цикл
			Строка.ДатаНачала = НачалоДня(Строка.ДатаОкончания);
		КонецЦикла; 
		

	КонецЕсли; 
	
	ТаблицаГарантий = ПолучитьТаблицуГарантийДляВсехОбъектов(ТаблицаСОбъектамиИДатамиРемонтов);
	
	Для каждого Строка Из ТаблицаГарантий Цикл
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Оборудование <%1> находится на гарантийном обслуживании у организации %2. Необходимо оформить рекламационный акт.'");
		Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Сообщение.Текст,Строка.ГарантийныйОбъект,Строка.ГарантирующаяОрганизация);
		Сообщение.Сообщить();
		
	КонецЦикла; 
	
КонецПроцедуры

// Проверяет наличие контрагентов в документах.
//
// Параметры:
//  ДокОбъект - ДокументОбъект - объект.
//  ИмяТЧ - Строка - имя табличной части.
//  ИмяРеквизита - Строка - Имя реквизита.
//  Отказ - Булево - отказ.
Процедура ПроверитьНаличиеКонтрагентов(ДокОбъект, ИмяТЧ, ИмяРеквизита, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументРемонтыОборудования.ID,
	|	ДокументРемонтыОборудования.ГарантийныйРемонт
	|ПОМЕСТИТЬ ДокументРемонтыОборудования
	|ИЗ
	|	&ДокументРемонтыОборудования КАК ДокументРемонтыОборудования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументИсполнителиРемонтныхРабот.РемонтыОборудования_ID,
	|	ДокументИсполнителиРемонтныхРабот." + ИмяРеквизита + " КАК Исполнитель
	|ПОМЕСТИТЬ ДокументИсполнителиРемонтныхРабот
	|ИЗ
	|	&ДокументИсполнителиРемонтныхРабот КАК ДокументИсполнителиРемонтныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументРемонтыОборудования.ID КАК ID,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВЫРАЗИТЬ(ДокументИсполнителиРемонтныхРабот.Исполнитель КАК Справочник.Контрагенты) ССЫЛКА Справочник.Контрагенты
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьКонтрагент
	|ИЗ
	|	ДокументРемонтыОборудования КАК ДокументРемонтыОборудования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументИсполнителиРемонтныхРабот КАК ДокументИсполнителиРемонтныхРабот
	|		ПО ДокументРемонтыОборудования.ID = ДокументИсполнителиРемонтныхРабот.РемонтыОборудования_ID
	|ГДЕ
	|	ДокументРемонтыОборудования.ГарантийныйРемонт
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументРемонтыОборудования.ID";
	
	Запрос.УстановитьПараметр("ДокументРемонтыОборудования", ДокОбъект.РемонтыОборудования.Выгрузить());
	Запрос.УстановитьПараметр("ДокументИсполнителиРемонтныхРабот", ДокОбъект[ИмяТЧ].Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если НЕ Выборка.ЕстьКонтрагент Тогда
				
				Отказ = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Отказ Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Ремонт гарантийного оборудования собственными силами запрещён.'");
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Функция возвращает список родителей объекта ремонта для определения
// для них сроков гарантии и гарантирующих организаций.
// Параметры:
//	ОР - Объект ремонта,
//	Иерархия - иерархия для ввода новых объектов ремонта,
//	ДатаОкончания - Дата окончания ремонта, используется в случае,
//	когда объекты ремонта в иерархии перемещаются документами,
//  ТЗРезультат - таблица иеирархии, если не передана параметром, то будет получена.
// Возвращаемое значение: 
//		СписокЗначений - список объектов.
Функция ПолучитьСписокОбъектовДляПолученияСроковГарантии(ОР, Иерархия, ДатаОкончания = Неопределено, ТЗРезультат = Неопределено) 
		
	СписокОбъектовДляПолученияСроковГарантии = Новый СписокЗначений;
	
	СписокОбъектовДляПолученияСроковГарантии.Добавить(ОР);
	
	Если Иерархия.СтроитсяАвтоматически Тогда
		
		Возврат СписокОбъектовДляПолученияСроковГарантии;
		
	Иначе
		
		Если ТЗРезультат = Неопределено  Тогда
			
			ТЗРезультат = ПолучитьТЗРезультат(Иерархия);

		КонецЕсли; 
				
		ЗаполнитьСписиокРодителейОбъектаРемонта(СписокОбъектовДляПолученияСроковГарантии, ОР, ТЗРезультат, ДатаОкончания);
		
		Возврат СписокОбъектовДляПолученияСроковГарантии;
		
	КонецЕсли;
		
	
КонецФункции

// Рекурсивная процедура - заполняет список родителей для объекта ремонта, переданного параметром
// изначально вызывается из функции ПолучитьСписокОбъектовДляПолученияСроковГарантии.
// Параметры:
//  Список - список для добавления родителей,
//	ОР - Объект ремонта,
//	Иерархия - иерархия для ввода новых объектов ремонта,
//	ДатаОкончания - Дата окончания ремонта, используется в случае,
//	когда объекты ремонта в иерархии перемещаются документами,
//  ТЗИерархии - таблица иеирархии.
Процедура ЗаполнитьСписиокРодителейОбъектаРемонта(Список,ОР,ТЗИерархии, ДатаОкончания = Неопределено)
	
	МассивСтрок = ТЗИерархии.НайтиСтроки(Новый Структура("объектИерархии", ОР));
	Если МассивСтрок.Количество() > 0 Тогда
		
		Если ДатаОкончания = Неопределено
			ИЛИ ТЗИерархии.Колонки.Найти("Период") = Неопределено Тогда
			
			Родитель = МассивСтрок[0].РодительИерархии;
			Если ЗначениеЗаполнено(Родитель) Тогда
				Список.Добавить(Родитель);
				ЗаполнитьСписиокРодителейОбъектаРемонта(Список, Родитель, ТЗИерархии);
			КонецЕсли; 
			
			
		Иначе
			Для каждого ЭлементМассива Из МассивСтрок Цикл
				
				Если ЭлементМассива.Период > ДатаОкончания Тогда
					Продолжить;
				Иначе
					
					Родитель = ЭлементМассива.РодительИерархии;
					Если ЗначениеЗаполнено(Родитель) Тогда
						Список.Добавить(Родитель);
						ЗаполнитьСписиокРодителейОбъектаРемонта(Список, Родитель, ТЗИерархии, ДатаОкончания);
					КонецЕсли; 
					
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Функция возвращает сруктуру иерархи ввиде таблицы значений
Функция ПолучитьТЗРезультат(Иерархия)
	
	Запрос = Новый Запрос;	
	Если Иерархия.ИзменяетсяДокументами Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		|	торо_РасположениеОРВСтруктуреИерархии.Период,
		|	торо_РасположениеОРВСтруктуреИерархии.ОбъектИерархии,
		|	торо_РасположениеОРВСтруктуреИерархии.РодительИерархии
		|ИЗ
		|	РегистрСведений.торо_РасположениеОРВСтруктуреИерархии КАК торо_РасположениеОРВСтруктуреИерархии
		|ГДЕ
		|	торо_РасположениеОРВСтруктуреИерархии.СтруктураИерархии = &СтруктураИерархии";
		

	Иначе                                                       
		Запрос.Текст = "ВЫБРАТЬ
		|	торо_ИерархическиеСтруктурыОР.ОбъектИерархии,
		|	торо_ИерархическиеСтруктурыОР.РодительИерархии
		|ИЗ
		|	РегистрСведений.торо_ИерархическиеСтруктурыОР КАК торо_ИерархическиеСтруктурыОР
		|ГДЕ
		|	торо_ИерархическиеСтруктурыОР.СтруктураИерархии = &СтруктураИерархии";
		
	КонецЕсли;
	
	
	Запрос.УстановитьПараметр("СтруктураИерархии", Иерархия);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции


// Функция выполняет фомирвание списка родителей для каждого Объекта ремонта из
// переданной в качетсвве параметра ТЗ.
// Параметры:
//	 ТаблицаСОбъектамиИДатамиРемонтов - ТаблицаЗначений с колонками: ОбъектРемонта, ДатаНачала, ДатаОкончания. 
Функция ЗаполнитьРодителей(ТаблицаСОбъектамиИДатамиРемонтов)
		
	Иерархия = Константы.торо_ИерархияДляВводаНовыхОР.Получить();
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("СправочникСсылка.торо_ОбъектыРемонта"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив);
	
	ТаблицаЗначений.Колонки.Добавить("ОбъектРемонта", ОписаниеТипов);
	ТаблицаЗначений.Колонки.Добавить("ОбъектГарантии",ОписаниеТипов);
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Дата"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив,,,Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаЗначений.Колонки.Добавить("ДатаНачала",    ОписаниеТипов);
	ТаблицаЗначений.Колонки.Добавить("ДатаОкончания", ОписаниеТипов);
	
	СтруктураВозврата = Новый Структура("ОбщийСписокОбъектов, ТаблицаЗначений");
	
	СписокОбъектовДляПолученияСроковГарантии = Новый СписокЗначений;
	
	Для каждого Строка Из ТаблицаСОбъектамиИДатамиРемонтов Цикл
		
		Если Иерархия.СтроитсяАвтоматически Тогда
			
			СписокОбъектовДляПолученияСроковГарантии.Добавить(Строка.ОбъектРемонта);
			
		Иначе
			
			ТЗРезультат = ПолучитьТЗРезультат(Иерархия);
			
			СписокОбъектовДляПолученияСроковГарантии = ПолучитьСписокОбъектовДляПолученияСроковГарантии(Строка.ОбъектРемонта,Иерархия, Строка.ДатаОкончания, ТЗРезультат);
			
		КонецЕсли;
		
		Для каждого Элемент Из СписокОбъектовДляПолученияСроковГарантии Цикл
			Если Элемент.Значение.ЭтоГруппа Тогда
				Продолжить;
			КонецЕсли; 
			Нс = ТаблицаЗначений.Добавить();
			Нс.ОбъектРемонта  = Строка.ОбъектРемонта;
			Нс.ДатаНачала     = Строка.ДатаНачала; 
			Нс.ДатаОкончания  = Строка.ДатаОкончания ;
			Нс.ОбъектГарантии = Элемент.Значение;
			
		КонецЦикла;
	КонецЦикла;	
	
	Возврат ТаблицаЗначений;
	
КонецФункции

#КонецОбласти




