#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Кадровый учет
	НеобходимыеДанные = "ФизическоеЛицо,ВидЗанятости";
	КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), НеобходимыеДанные, ДатаУвольнения);
	КадровыеДанныеСотрудника = КадровыеДанныеСотрудника[0];
	
	КадровыеСобытия = Документы.Увольнение.КадровыеСобытияУвольнение(Сотрудник, ДатаУвольнения, ФизическоеЛицо, , Организация);
	СформироватьКадровыеДвижения(КадровыеСобытия);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Действие") И ДанныеЗаполнения.Действие = "Исправить" Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Увольнение.Организация,
			|	Увольнение.Подразделение,
			|	Увольнение.ДатаУвольнения,
			|	Увольнение.Сотрудник,
			|	Увольнение.ОснованиеУвольнения,
			|	Увольнение.Руководитель,
			|	Увольнение.ДолжностьРуководителя,
			|	Увольнение.ФизическоеЛицо,
			|	Увольнение.Ответственный,
			|	Увольнение.Комментарий
			|ИЗ
			|	Документ.Увольнение КАК Увольнение
			|ГДЕ
			|	Увольнение.Ссылка = &Ссылка");
			
			Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения.Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
			ИсправленныйДокумент = ДанныеЗаполнения.Ссылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
#Область СлужебныеПроцедурыИФункции

// Выполняет запись движений по кадровому учету
//
Процедура СформироватьКадровыеДвижения(КадровыеСобытия) Экспорт
	
	КадровыйУчет.СформироватьКадровыеДвижения(ЭтотОбъект, Движения, КадровыеСобытия);
	Если Движения.КадроваяИсторияСотрудников.Записывать Тогда
		Движения.КадроваяИсторияСотрудников.Записать();
		Движения.КадроваяИсторияСотрудников.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
#КонецЕсли
