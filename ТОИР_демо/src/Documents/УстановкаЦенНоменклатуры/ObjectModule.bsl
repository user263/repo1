#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	УстановкаЦенНоменклатурыТовары.Номенклатура КАК Номенклатура,
	               |	УстановкаЦенНоменклатурыТовары.ВидЦены КАК ВидЦены,
	               |	УстановкаЦенНоменклатурыТовары.Характеристика КАК Характеристика,
	               |	УстановкаЦенНоменклатурыТовары.Цена КАК Цена,
	               |	УстановкаЦенНоменклатурыТовары.Валюта КАК Валюта,
	               |	УстановкаЦенНоменклатурыТовары.ЕдиницаИзмерения КАК ДокЕдиницаИзмерения,
	               |	УстановкаЦенНоменклатурыТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВЫБОР
	               |		КОГДА УстановкаЦенНоменклатурыТовары.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяССылка)
	               |			ТОГДА 1
	               |		ИНАЧЕ УстановкаЦенНоменклатурыТовары.ЕдиницаИзмерения.Коэффициент
	               |	КОНЕЦ КАК Коэффициент
	               |ИЗ
	               |	Документ.УстановкаЦенНоменклатуры.Товары КАК УстановкаЦенНоменклатурыТовары
	               |ГДЕ
	               |	УстановкаЦенНоменклатурыТовары.Ссылка = &Ссылка";
				   
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	резЗапроса = Запрос.Выполнить();
	
	Если резЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = резЗапроса.Выбрать();
	
	// регистр ЦеныНоменклатуры
	Движения.ЦеныНоменклатуры.Записывать = Истина;
	Движения.ЦеныНоменклатуры.Очистить();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ЦеныНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
		Движение.Период = Дата;
		Если Выборка.ЕдиницаИзмерения <> Выборка.ДокЕдиницаИзмерения Тогда
			Движение.Цена = ?(Выборка.Коэффициент = 0, 0, Движение.Цена / Выборка.Коэффициент);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Для каждого ТекСтрокаТовары Из Товары Цикл
	
		Если НЕ ЗначениеЗаполнено(ТекСтрокаТовары.Валюта) Тогда
			ТекСтрокаТовары.Валюта = Валюта;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекСтрокаТовары.ВидЦены) Тогда
			ТекСтрокаТовары.ВидЦены = ВидЦены;
		КонецЕсли;
	
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли