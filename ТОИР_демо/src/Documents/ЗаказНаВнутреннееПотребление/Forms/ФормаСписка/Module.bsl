
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	// Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	    
    ИспользоватьВнутреннееПотреблениеПоНесколькимЗаказам = Истина;
        
    // СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаГлобальныеКоманды);
	// Конец СтандартныеПодсистемы.Печать
	
	торо_СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка, Новый Структура("УстановитьСвойствоЭлементовФормыОтПрав", Истина));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьВнутреннееПотреблениеТоваров(Команда)
	
	СоздатьВнутреннееПотреблениеТоваровНаОснованииЗаказа();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
    УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#Область Прочее

&НаКлиенте
Процедура СоздатьВнутреннееПотреблениеТоваровНаОснованииЗаказа()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		ТекстПредупреждения = НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
		
	КонецЕсли;
	
	РеквизитыШапки = Новый Структура();
	МассивСсылок = Новый Массив();
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() = 1 Или Не ИспользоватьВнутреннееПотреблениеПоНесколькимЗаказам Тогда
		
		Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'");
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
			Возврат;
			
		КонецЕсли;
		
		ПараметрыОснования = Новый Структура;
		ПараметрыОснования.Вставить("ДокументОснование", ТекущиеДанные.Ссылка);
		ОткрытьФорму("Документ.ВнутреннееПотреблениеТоваров.Форма.ФормаДокумента",
			Новый Структура("Основание", ПараметрыОснования));
		
	Иначе
		
		Для каждого Заказ из Элементы.Список.ВыделенныеСтроки Цикл
			
			Если ТипЗнч(Заказ) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			МассивСсылок.Добавить(Заказ);
			
		КонецЦикла;
		
		Если МассивСсылок.Количество() = 0 Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'");
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
			Возврат;
			
		КонецЕсли;
		
		ТекстПредупреждения = Неопределено;
		РеквизитыШапки = ДанныеЗаполненияВнутреннегоПотребления(МассивСсылок, ТекстПредупреждения);
		Если РеквизитыШапки = Неопределено Тогда
			Если ТекстПредупреждения <> Неопределено Тогда
				ПоказатьПредупреждение(, ТекстПредупреждения);
			КонецЕсли; 
			Возврат
		КонецЕсли;
			
		ПараметрыОснования = Новый Структура;
		ПараметрыОснования.Вставить("РеквизитыШапки", РеквизитыШапки);
		ПараметрыОснования.Вставить("ДокументОснование", МассивСсылок);
		
		ОткрытьФорму("Документ.ВнутреннееПотреблениеТоваров.Форма.ФормаДокумента",
			Новый Структура("Основание", ПараметрыОснования));
			
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеЗаполненияВнутреннегоПотребления(Знач МассивСсылок, ТекстПредупреждения)
	
	Возврат Документы.ЗаказНаВнутреннееПотребление.ПараметрыОформленияВнутреннегоПотребления(МассивСсылок, ТекстПредупреждения);
	
КонецФункции

#КонецОбласти

#КонецОбласти
