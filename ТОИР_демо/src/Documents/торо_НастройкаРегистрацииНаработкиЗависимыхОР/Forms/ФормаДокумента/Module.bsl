#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства

	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
			
			Объект.Организация = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиТОиР",
			"ОсновнаяОрганизация",
			Истина);
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			
			Объект.Подразделение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиТОиР",
			"ОсновноеПодразделение",
			Истина);
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.Ответственный) Тогда
			
			Объект.Ответственный = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиТОиР",
			"ОсновнойОтветственный",
			Справочники.Пользователи.ПустаяСсылка());
			
		КонецЕсли;
	КонецЕсли;
	
	МассивОР = Новый Массив;
	Для каждого СтрокаТЧ Из Объект.ОбъектыРемонта Цикл
		МассивОР.Добавить(СтрокаТЧ.ОбъектРемонта);
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	торо_ПараметрыНаработкиОбъектовРемонта.Показатель,
	|	торо_ПараметрыНаработкиОбъектовРемонта.Показатель.Код КАК КодПоказателя,
	|	торо_ПараметрыНаработкиОбъектовРемонта.ОбъектРемонта,
	|	""Страница"" + торо_ПараметрыНаработкиОбъектовРемонта.Показатель.Код КАК ИмяСтраницы
	|ИЗ
	|	РегистрСведений.торо_ПараметрыНаработкиОбъектовРемонта КАК торо_ПараметрыНаработкиОбъектовРемонта
	|ГДЕ
	|	торо_ПараметрыНаработкиОбъектовРемонта.ОбъектРемонта В(&ОбъектРемонта)";
	
	Запрос.УстановитьПараметр("ОбъектРемонта", МассивОР);
	
	Результат = Запрос.Выполнить();
	
	ТаблицаСоответствияПоказателей.Загрузить(Результат.Выгрузить());
	
	СоздатьЗакладки(ТаблицаСоответствияПоказателей);
		
	// Заголовок формы++
	торо_РаботаСДиалогами.УстановитьЗаголовокФормыДокумента("", РеквизитФормыВЗначение("Объект"), ЭтаФорма);
	// Заголовок формы--
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Заголовок формы++
	торо_РаботаСДиалогами.УстановитьЗаголовокФормыДокумента("", РеквизитФормыВЗначение("Объект"), ЭтаФорма);
	// Заголовок формы--
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ДатаВводаПриИзменении(Элемент)
	Для каждого СтрокаТЧ Из Объект.ЗависимыеОбъектыРемонта Цикл
		СтрокаТЧ.ДатаВвода = Объект.ДатаВвода;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыРемонта
&НаКлиенте
Процедура ОбъектыРемонтаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ID_ОбъектаРемонта = Новый УникальныйИдентификатор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыРемонтаПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные <> Неопределено И ЗначениеЗаполнено(ТекДанные.ОбъектРемонта) Тогда
		
		Для каждого СтраницаПанелиНаработки Из Элементы.ПанельПараметровНаработки.ПодчиненныеЭлементы Цикл
			
			ПараметрыПоиска = Новый Структура("ОбъектРемонта, ИмяСтраницы", ТекДанные.ОбъектРемонта, СтраницаПанелиНаработки.Имя);
			СтрокаПоказатель = ТаблицаСоответствияПоказателей.НайтиСтроки(ПараметрыПоиска);
			Если СтрокаПоказатель.Количество() = 0 Тогда
				
				СтраницаПанелиНаработки.Видимость = Ложь;
				
			Иначе
				
				СтраницаПанелиНаработки.Видимость = Истина;
				
				Элементы["ТабличноеПоле" + СтрокаПоказатель[0].КодПоказателя].ОтборСтрок = Новый ФиксированнаяСтруктура("ID_ВышестоящегоОбъекта, ПараметрНаработки", Элемент.ТекущиеДанные.ID_ОбъектаРемонта, СтрокаПоказатель[0].Показатель);
				
			КонецЕсли;
			
		КонецЦикла;
	ИначеЕсли Не (ТекДанные = Неопределено ИЛИ ТекДанные.ID_ОбъектаРемонта = "") Тогда
		Для каждого СтраницаПанелиНаработки Из Элементы.ПанельПараметровНаработки.ПодчиненныеЭлементы Цикл
			СтраницаПанелиНаработки.Видимость = Ложь;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыРемонтаПередУдалением(Элемент, Отказ)
	Если Не Отказ Тогда
		УдалитьСтраницы(Элемент.ТекущиеДанные.ОбъектРемонта, Элемент.ТекущиеДанные.ID_ОбъектаРемонта);
		
		МассивДляУдаления = ТаблицаСоответствияПоказателей.НайтиСтроки(Новый структура("ОбъектРемонта", Элемент.ТекущиеДанные.ОбъектРемонта));
		Для каждого текСтрока из МассивДляУдаления Цикл
			ТаблицаСоответствияПоказателей.Удалить(текСтрока);
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыРемонтаОбъектРемонтаПриИзменении(Элемент)
	ТекДанные = Элементы.ОбъектыРемонта.ТекущиеДанные;
	Если НЕ ТекДанные = Неопределено И ЗначениеЗаполнено(ТекДанные.ОбъектРемонта) Тогда
		СтруктураВозврата = ПолучитьПоказателиОРНаСервере(ТекДанные.ОбъектРемонта);	
		
		Для каждого Элем Из СтруктураВозврата Цикл
			
			НС = ТаблицаСоответствияПоказателей.Добавить();
			
			ЗаполнитьЗначенияСвойств(НС, Элем);
			
		КонецЦикла; 
		
		СоздатьЗакладки(СтруктураВозврата);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыРемонтаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если Не (НоваяСтрока И ОтменаРедактирования) И ТекДанные <> Неопределено И ЗначениеЗаполнено(ТекДанные.ОбъектРемонта) Тогда
		СтруктураПоиска = Новый Структура("ОбъектРемонта", ТекДанные.ОбъектРемонта);
		Если Объект.ОбъектыРемонта.НайтиСтроки(СтруктураПоиска).Количество() > 1 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Выбранный объект ремонта уже есть в списке!'");
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		ОбъектыРемонтаПриАктивизацииСтроки(Элемент);
	Иначе
		Для каждого СтраницаПанелиНаработки Из Элементы.ПанельПараметровНаработки.ПодчиненныеЭлементы Цикл
			СтраницаПанелиНаработки.Видимость = Ложь;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗависимыеОбъектыРемонта
&НаКлиенте
Процедура ЗависимыеОбъектыРемонтаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Элементы.ОбъектыРемонта.ТекущиеДанные = Неопределено  ИЛИ НЕ ЗначениеЗаполнено(Элементы.ОбъектыРемонта.ТекущиеДанные.ОбъектРемонта) Тогда
		торо_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Для добавления зависимого объекта ремонта необходимо выбрать объект ремонта!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗависимыеОбъектыРемонтаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если НоваяСтрока И Не ОтменаРедактирования Тогда
		ТекДанные.ID_ВышестоящегоОбъекта = Элементы.ОбъектыРемонта.ТекущиеДанные.ID_ОбъектаРемонта;
		СтрокаПоказатель = ТаблицаСоответствияПоказателей.НайтиСтроки(Новый Структура("ИмяСтраницы", Элементы.ПанельПараметровНаработки.ТекущаяСтраница.Имя));
		Если СтрокаПоказатель.Количество() Тогда
			ТекДанные.ПараметрНаработки = СтрокаПоказатель[0].Показатель;
		КонецЕсли;
		ТекДанные.ДатаВвода = Объект.ДатаВвода;
	КонецЕсли;
	Если Не (НоваяСтрока И ОтменаРедактирования) Тогда
		СтруктураПоиска = Новый Структура("ID_ВышестоящегоОбъекта, Организация, Подразделение, Направление, ТипОбъектаРемонта, ОбъектРемонта, ПараметрНаработки",
										  ТекДанные.ID_ВышестоящегоОбъекта, ТекДанные.Организация, ТекДанные.Подразделение, ТекДанные.Направление, ТекДанные.ТипОбъектаРемонта, ТекДанные.ОбъектРемонта, ТекДанные.ПараметрНаработки);
		
		НайденныеСтроки = Объект.ЗависимыеОбъектыРемонта.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 1 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Строка с такими данными уже добавлена!'");
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗависимыеОбъектыРемонтаОбъектРемонтаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НайденныеПоказатели = ТаблицаСоответствияПоказателей.НайтиСтроки(Новый Структура("ИмяСтраницы", Элементы.ПанельПараметровНаработки.ТекущаяСтраница.Имя));
	
	Если НайденныеПоказатели.Количество() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		СписокОбъектов = ПолучитьСписокОбъектовРемонтаПоТекПоказателю(НайденныеПоказатели[0].Показатель);
		
		СтруктураПараметров = Новый Структура("КлючНазначенияИспользования, СписокОР, ПараметрНаработки", "торо_НастройкаРегистрацииНаработкиЗависимыхОР", СписокОбъектов, НайденныеПоказатели[0].Показатель); 
		ФормаПодбораОР = ПолучитьФорму("Справочник.торо_ОбъектыРемонта.ФормаВыбора", СтруктураПараметров, Элемент);
				
		ФормаПодбораОР.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
    УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УдалитьСтраницы(ОбъектРемонта, IDОбъектаРемонта)
	ПоказателиУдаляемогоОР = ТаблицаСоответствияПоказателей.НайтиСтроки(Новый Структура("ОбъектРемонта", ОбъектРемонта));
	МассивУдаляемыхСтрок = Объект.ЗависимыеОбъектыРемонта.НайтиСтроки(Новый Структура("ID_ВышестоящегоОбъекта", IDОбъектаРемонта));
	Для Каждого СтрокаМассива Из МассивУдаляемыхСтрок Цикл
		Объект.ЗависимыеОбъектыРемонта.Удалить(СтрокаМассива);
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПоказателиОРНаСервере(ОбъектРемонта)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_ПараметрыНаработкиОбъектовРемонта.Показатель,
	|	торо_ПараметрыНаработкиОбъектовРемонта.Показатель.Код КАК КодПоказателя
	|ИЗ
	|	РегистрСведений.торо_ПараметрыНаработкиОбъектовРемонта КАК торо_ПараметрыНаработкиОбъектовРемонта
	|ГДЕ
	|	торо_ПараметрыНаработкиОбъектовРемонта.ОбъектРемонта = &ОбъектРемонта";
	
	Запрос.УстановитьПараметр("ОбъектРемонта", ОбъектРемонта);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	СтруктураВозврата = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		
		СтруктураВозврата.Добавить(Новый Структура("Показатель, КодПоказателя, ОбъектРемонта, ИмяСтраницы", Выборка.Показатель, ВЫборка.КодПоказателя, ОбъектРемонта, "Страница" + Строка(Выборка.КодПоказателя)));
		
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервере
Процедура СоздатьЗакладки(МассивПоказателей)
	
	Для каждого Выборка Из МассивПоказателей Цикл
		
			Если Элементы.ПанельПараметровНаработки.ПодчиненныеЭлементы.Найти("Страница" + Выборка.КодПоказателя) = Неопределено Тогда
				
				ДобавляемоеТабПоле = "ТабличноеПоле" + Выборка.КодПоказателя;
				ДобавляемаяКомПанель = "КомПанель" + Выборка.КодПоказателя;
				
				ЭлемФормы = Элементы.Добавить("Страница" + Выборка.КодПоказателя,  Тип("ГруппаФормы") , Элементы.ПанельПараметровНаработки);
				ЭлемФормы.Заголовок = Строка(Выборка.Показатель);
				ЭлемФормы.Подсказка = Строка(Выборка.Показатель);
				ЭлемФормы.ТолькоПросмотр = Ложь;
				ЭлемФормы.Доступность = Истина;
				
				Элементы.ПанельПараметровНаработки.ТекущаяСтраница = Элементы.ПанельПараметровНаработки.ПодчиненныеЭлементы.Найти("Страница" + Выборка.КодПоказателя); 
				
				НовоеТабПоле = Элементы.Добавить(ДобавляемоеТабПоле, Тип("ТаблицаФормы"), ЭлемФормы);
				НовоеТабПоле.ПутьКДанным = "Объект.ЗависимыеОбъектыРемонта";
				НовоеТабПоле.ТолькоПросмотр = Ложь;
				НовоеТабПоле.Доступность = Истина;
				
				НовоеТабПоле.УстановитьДействие("ПередОкончаниемРедактирования", "ЗависимыеОбъектыРемонтаПередОкончаниемРедактирования"); 
				
				НовоеТабПоле.УстановитьДействие("ПередНачаломДобавления", "ЗависимыеОбъектыРемонтаПередНачаломДобавления"); 
				
				НовоеПолеФормы = Элементы.Добавить(Строка(НовоеТабПоле.Имя) + "Организация", Тип("ПолеФормы"), НовоеТабПоле);
				НовоеПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
				НовоеПолеФормы.ПутьКДанным = "Объект.ЗависимыеОбъектыРемонта.Организация";

				НовоеПолеФормы = Элементы.Добавить(Строка(НовоеТабПоле.Имя) + "Подразделение", Тип("ПолеФормы"), НовоеТабПоле);
				НовоеПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
				НовоеПолеФормы.ПутьКДанным = "Объект.ЗависимыеОбъектыРемонта.Подразделение";

				НовоеПолеФормы = Элементы.Добавить(Строка(НовоеТабПоле.Имя) + "Направление", Тип("ПолеФормы"), НовоеТабПоле);
				НовоеПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
				НовоеПолеФормы.ПутьКДанным = "Объект.ЗависимыеОбъектыРемонта.Направление";

				НовоеПолеФормы = Элементы.Добавить(Строка(НовоеТабПоле.Имя) + "ТипОбъектаРемонта", Тип("ПолеФормы"), НовоеТабПоле);
				НовоеПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
				НовоеПолеФормы.ПутьКДанным = "Объект.ЗависимыеОбъектыРемонта.ТипОбъектаРемонта";
				
				НовоеПолеФормы = Элементы.Добавить(Строка(НовоеТабПоле.Имя) + "ОбъектРемонта", Тип("ПолеФормы"), НовоеТабПоле);
				НовоеПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
				НовоеПолеФормы.ПутьКДанным = "Объект.ЗависимыеОбъектыРемонта.ОбъектРемонта";
				НовоеПолеФормы.УстановитьДействие("НачалоВыбора","ЗависимыеОбъектыРемонтаОбъектРемонтаНачалоВыбора");

				НовоеПолеФормы = Элементы.Добавить(Строка(НовоеТабПоле.Имя) + "КоэффициентПересчета", Тип("ПолеФормы"), НовоеТабПоле);
				НовоеПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
				НовоеПолеФормы.ПутьКДанным = "Объект.ЗависимыеОбъектыРемонта.КоэффициентПересчета";
				
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокОбъектовРемонтаПоТекПоказателю(Показатель)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	торо_ПараметрыНаработкиОбъектовРемонта.ОбъектРемонта
	|ИЗ
	|	РегистрСведений.торо_ПараметрыНаработкиОбъектовРемонта КАК торо_ПараметрыНаработкиОбъектовРемонта
	|ГДЕ
	|	торо_ПараметрыНаработкиОбъектовРемонта.Показатель = &Показатель";
	
	Запрос.УстановитьПараметр("Показатель", Показатель);
	
	Результат = Запрос.Выполнить();
	
	СписокОбъектов = Новый СписокЗначений;
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокОбъектов.Добавить(ВыборкаДетальныеЗаписи.ОбъектРемонта);
	КонецЦикла;
	
	Возврат СписокОбъектов;
	
КонецФункции // ()

#КонецОбласти

