
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Объект.ПереноситьАктивыВУпп Тогда
		Элементы.СпособПереносаАктивов.Доступность = Ложь;
	КонецЕсли; 
	
	Если Не Объект.ОтражатьВыявленныеДефекты Тогда
		Элементы.ТаблицаСоответствияКритичностиИВремениУстранения.Доступность = Ложь;
	КонецЕсли;	
	
	Если Объект.ОтражатьПринятиеКУчетуОСВУПП = Ложь
		И Объект.ОтражатьСписаниеОСВУПП = Ложь Тогда
		
		ПравилаОтправкиАктивов = "ТолькоОбъектыАктивы"
		
	ИначеЕсли Объект.ОтражатьПринятиеКУчетуОСВУПП = Истина
		И Объект.ОтражатьСписаниеОСВУПП = Ложь Тогда
		
		ПравилаОтправкиАктивов = "ОРИПринятиеКУчету"		
		
	ИначеЕсли Объект.ОтражатьПринятиеКУчетуОСВУПП = Истина
		И Объект.ОтражатьСписаниеОСВУПП = Истина Тогда
		
		ПравилаОтправкиАктивов = "ВыбытиеОР"
		
	КонецЕсли;
	
	Если Не Объект.ТаблицаСоответствияКритичностиИВремениУстранения.Количество() > 0 Тогда
		
		ОбновитьТаблицуКритичностей() 
				
	Иначе
		
		Для каждого Строка Из Объект.ТаблицаСоответствияКритичностиИВремениУстранения Цикл
			Строка.ВремяСтрокой = СформироватьПродолжительность(Строка.ВремяНаУстранение);
		КонецЦикла;
		
	КонецЕсли; 

	Объект.РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию;
	Объект.РежимВыгрузкиПоУсловию  = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию;
	объект.РежимВыгрузкиПриНеобходимости = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьЗаголовокКнопкиОтборов();
	ЗаполнитьЗаголовокКнопкиОтборов(Истина);
	
	Если Объект.ИспользоватьОтборПоОрганизациям Тогда
		ОтборПоОрганизациям = "ПоВыбраннымОрганизациям";
	Иначе
		ОтборПоОрганизациям = "ПоВсемОрганизациям"; 
	КонецЕсли;
	
	Если Объект.ИспользоватьОтборПоСкладам Тогда
		ОтборПоСкладам = "ПоВыбраннымСкладам";
	Иначе
		ОтборПоСкладам = "ПоВсемСкладам"; 
	КонецЕсли;
	
	ОтборПоОрганизациямПриИзменении(Истина);
	ОтборПоСкладамПриИзменении(Истина);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Для каждого Строка Из Объект.ТаблицаСоответствияКритичностиИВремениУстранения Цикл
		Строка.ВремяСтрокой = СформироватьПродолжительность(Строка.ВремяНаУстранение);
	КонецЦикла;
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПравилаОтправкиАктивовПриИзменении(Элемент)
	РасставитьФлаги();
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоОрганизациямПриИзменении(Элемент)
	
	Элементы.ВыбратьОрганизаии.Доступность = (ОтборПоОрганизациям = "ПоВыбраннымОрганизациям");
	Объект.ИспользоватьОтборПоОрганизациям = (ОтборПоОрганизациям = "ПоВыбраннымОрганизациям");

КонецПроцедуры

&НаКлиенте
Процедура ОтражатьВыявленныеДефектыПриИзменении(Элемент)
	Элементы.ТаблицаСоответствияКритичностиИВремениУстранения.Доступность = Объект.ОтражатьВыявленныеДефекты;
КонецПроцедуры

&НаКлиенте
Процедура ПереноситьАктивыВУппПриИзменении(Элемент)
	Элементы.СпособПереносаАктивов.Доступность = Объект.ПереноситьАктивыВУпп;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствияКритичностиИВремениУстраненияВремяСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ТаблицаСоответствияКритичностиИВремениУстранения.ТекущиеДанные;
	
	Если Не ТекущиеДанные = Неопределено Тогда
		
		торо_ЗаполнениеДокументовКлиент.ОткрытьФормуПодбораПродолжительности(ТекущиеДанные.ВремяНаУстранение, Элемент, Неопределено);		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствияКритичностиИВремениУстраненияВремяСтрокойОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ТаблицаСоответствияКритичностиИВремениУстранения.ТекущиеДанные;
	
	Если Не ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные.ВремяСтрокой      = СформироватьПродолжительность(ВыбранноеЗначение);
		ТекущиеДанные.ВремяНаУстранение = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтборПоСкладамПриИзменении(Элемент)
	Элементы.УкатьСклады.Доступность = (ОтборПоСкладам  = "ПоВыбраннымСкладам");
	Объект.ИспользоватьОтборПоСкладам = (ОтборПоСкладам = "ПоВыбраннымСкладам");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьОрганизации(Команда)
	
	СписокОрганизаций = Новый СписокЗначений;
	
	Для каждого Строка Из Объект.Организации Цикл
		СписокОрганизаций.Добавить(Строка.Организация);
	КонецЦикла;
	
	СписокВыбораОрганизаций = ПолучитьСписокОрганизаций(СписокОрганизаций);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьОрганизацииЗавершение", ЭтаФорма);
	СписокВыбораОрганизаций.ПоказатьОтметкуЭлементов(ОписаниеОповещения,НСтр("ru = 'Выберите организации для отбора'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьСклады(Команда)
	
	СписокСкладов = Новый СписокЗначений;
	
	Для каждого Строка Из Объект.Склады Цикл
		СписокСкладов.Добавить(Строка.Склад);
	КонецЦикла;
	
	СписокВыбораСкладов = ПолучитьСписокСкладов(СписокСкладов);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьСкладЗавершение", ЭтаФорма);
	СписокВыбораСкладов.ПоказатьОтметкуЭлементов(ОписаниеОповещения,НСтр("ru = 'Выберите склады для отбора'"));

КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	ТекущиеДанные = Элементы.ТаблицаСоответствияКритичностиИВремениУстранения.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		СтрокаТаблицы = Объект.ТаблицаСоответствияКритичностиИВремениУстранения.НайтиСтроки(Новый Структура("КритичностьДефекта", ТекущиеДанные.КритичностьДефекта));
		Если Не СтрокаТаблицы.Количество() = 0 Тогда
			Объект.ТаблицаСоответствияКритичностиИВремениУстранения.Удалить(СтрокаТаблицы[0]);
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
	ОбновитьТаблицуКритичностей()
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьТаблицуКритичностей()
	
	Объект.ТаблицаСоответствияКритичностиИВремениУстранения.Очистить();
	Выборка = Справочники.торо_КритичностьДефекта.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НС = Объект.ТаблицаСоответствияКритичностиИВремениУстранения.Добавить();
		НС.КритичностьДефекта = Выборка.Ссылка;
	КонецЦикла; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура РасставитьФлаги()
	
	Если ПравилаОтправкиАктивов = "ТолькоОбъектыАктивы" Тогда
		Объект.ОтражатьПринятиеКУчетуОСВУПП = Ложь;
		Объект.ОтражатьСписаниеОСВУПП       = Ложь;
	ИначеЕсли ПравилаОтправкиАктивов = "ОРИПринятиеКУчету" Тогда
		Объект.ОтражатьПринятиеКУчетуОСВУПП = Истина;
		Объект.ОтражатьСписаниеОСВУПП       = Ложь;
	ИначеЕсли ПравилаОтправкиАктивов = "ВыбытиеОР" Тогда
		Объект.ОтражатьПринятиеКУчетуОСВУПП = Истина;
		Объект.ОтражатьСписаниеОСВУПП       = Истина;
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗаголовокКнопкиОтборов(ЭтоСклад = Ложь)
	Если ЭтоСклад Тогда
		
		ТекстЗаголовка = "";
		Для каждого Строка Из Объект.Склады Цикл
			Если ЗначениеЗаполнено(ТекстЗаголовка) Тогда
				ТекстЗаголовка = ТекстЗаголовка + "; ";
			КонецЕсли; 
			ТекстЗаголовка = ТекстЗаголовка + Строка.Склад;
		КонецЦикла; 
		
		Элементы.УкатьСклады.Заголовок = ТекстЗаголовка;
		
	Иначе	
		ТекстЗаголовка = "";
		Для каждого Строка Из Объект.Организации Цикл
			Если ЗначениеЗаполнено(ТекстЗаголовка) Тогда
				ТекстЗаголовка = ТекстЗаголовка + "; ";
			КонецЕсли; 
			ТекстЗаголовка = ТекстЗаголовка + Строка.Организация;
		КонецЦикла; 
		Элементы.ВыбратьОрганизаии.Заголовок = ТекстЗаголовка;
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокОрганизаций(СписокВыбранныхОрганизаций)
	
	СписокОрганизаций = Новый СписокЗначений;
	Выборка = Справочники.Организации.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Пометка = Не (СписокВыбранныхОрганизаций.НайтиПоЗначению(Выборка.Ссылка) = Неопределено);
		СписокОрганизаций.Добавить(Выборка.Ссылка,,Пометка);

	КонецЦикла; 
	
	Возврат СписокОрганизаций;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокСкладов(СписокВыбранныхСкладов)
	
	СписокСкладов = Новый СписокЗначений;
	Выборка = Справочники.Склады.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли; 
		Пометка = Не (СписокВыбранныхСкладов.НайтиПоЗначению(Выборка.Ссылка) = Неопределено);
		СписокСкладов.Добавить(Выборка.Ссылка,,Пометка);

	КонецЦикла; 
	
	Возврат СписокСкладов;
	
КонецФункции

&НаКлиенте
Процедура ВыбратьОрганизацииЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Объект.Организации.Очистить();
	
	Для каждого ЭлементСписка Из Результат Цикл
		Если ЭлементСписка.Пометка Тогда
			НС = Объект.Организации.Добавить();
			НС.Организация = ЭлементСписка.Значение;
		КонецЕсли; 
	КонецЦикла; 
	ЗаполнитьЗаголовокКнопкиОтборов();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСкладЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Объект.Склады.Очистить();
	
	Для каждого ЭлементСписка Из Результат Цикл
		Если ЭлементСписка.Пометка Тогда
			НС = Объект.Склады.Добавить();
			НС.Склад = ЭлементСписка.Значение;
		КонецЕсли; 
	КонецЦикла;
	
	ЗаполнитьЗаголовокКнопкиОтборов(Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьПродолжительность(ПродолжительностьВСекундах) 
	
	Если ПродолжительностьВСекундах = 0 Тогда
		
		Возврат "Ввести продолжительность";
		
	Иначе
		
		Часы    = Цел(ПродолжительностьВСекундах / 3600);
		Минуты  = Цел((ПродолжительностьВСекундах - 3600 * Часы) / 60);
		Секунды = ПродолжительностьВСекундах - 3600 * Часы - Минуты * 60;
		
		Возврат "" + Часы + " ч. " + Минуты + " м. " + Секунды + " с.";
		
	КонецЕсли;
	
КонецФункции
#КонецОбласти
	  