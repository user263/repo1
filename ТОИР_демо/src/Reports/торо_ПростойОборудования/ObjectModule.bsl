#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ДанныеПоПростоям = ДанныеПоПростоям(НастройкиОсновнойСхемы);
										
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ДанныеПоПростоям", ДанныеПоПростоям);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);	
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ДанныеПоПростоям(НастройкиОсновнойСхемы)
	
	// сюда закинем список ор а потом и список графиков
	МВТ = Новый МенеджерВременныхТаблиц;
	ПериодОтчета = НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПериодОтчета");
	Если ПериодОтчета.Использование Тогда
		ДатаНач = ПериодОтчета.Значение.ДатаНачала;
		ДатаКон = ?(ПериодОтчета.Значение.ДатаОкончания = Дата("00010101"),Дата("39991231235959"),ПериодОтчета.Значение.ДатаОкончания);
	Иначе
		ДатаНач = Дата("00010101");
		ДатаКон = Дата("39991231235959");
	КонецЕсли;
	
	// получаем список графиков
	МассивГрафиков = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ВремяПростояОборудования.ОбъектРемонта КАК ОбъектРемонта,
	               |	ВЫБОР
	               |		КОГДА торо_ВремяПростояОборудования.Период < &ДатаНач
	               |			ТОГДА &датаНач
	               |		ИНАЧЕ торо_ВремяПростояОборудования.Период
	               |	КОНЕЦ КАК ДатаНачала,
	               |	ВЫБОР
	               |		КОГДА торо_ВремяПростояОборудования.ДатаЗапуска > &ДатаКон
	               |			ТОГДА &ДатаКон
	               |		ИНАЧЕ торо_ВремяПростояОборудования.ДатаЗапуска
	               |	КОНЕЦ КАК ДатаОкончания,
	               |	ВЫБОР
	               |		КОГДА торо_ВремяПростояОборудования.Период >= &ДатаНач
	               |				И торо_ВремяПростояОборудования.ДатаЗапуска <= &ДатаКон
	               |			ТОГДА торо_ВремяПростояОборудования.ВремяПростоя
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ВремяПростоя,
	               |	торо_ВремяПростояОборудования.Регистратор,
	               |	торо_ВремяПростояОборудования.Период КАК ДатаНачалаПоРегистру,
	               |	торо_ВремяПростояОборудования.ДатаЗапуска КАК ДатаОкончанияПоРегистру,
	               |	торо_ВремяПростояОборудования.Регистратор.Дата,
	               |	торо_ВремяПростояОборудования.ПричинаПростоя
	               |ПОМЕСТИТЬ ТаблицаОРСДублями
	               |ИЗ
	               |	РегистрНакопления.торо_ВремяПростояОборудования КАК торо_ВремяПростояОборудования
	               |ГДЕ
	               |	торо_ВремяПростояОборудования.ДатаЗапуска > &ДатаНач
	               |	И торо_ВремяПростояОборудования.Период < &ДатаКон
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	торо_ТекущееСостояниеОРСрезПоследних.ОбъектРемонта,
	               |	ВЫБОР
	               |		КОГДА торо_ТекущееСостояниеОРСрезПоследних.Период < &ДатаНач
	               |			ТОГДА &ДатаНач
	               |		ИНАЧЕ торо_ТекущееСостояниеОРСрезПоследних.Период
	               |	КОНЕЦ,
	               |	&ДатаКон,
	               |	0,
	               |	торо_ТекущееСостояниеОРСрезПоследних.Регистратор,
	               |	торо_ТекущееСостояниеОРСрезПоследних.Период,
	               |	ДАТАВРЕМЯ(1,1,1,0,0,0),
	               |	торо_ТекущееСостояниеОРСрезПоследних.Регистратор.Дата,
	               |	торо_ТекущееСостояниеОРСрезПоследних.ПричинаПростоя
	               |ИЗ
	               |	РегистрСведений.торо_ТекущееСостояниеОР.СрезПоследних(&ДатаКон, ) КАК торо_ТекущееСостояниеОРСрезПоследних
	               |ГДЕ
	               |	торо_ТекущееСостояниеОРСрезПоследних.ВидЭксплуатации.ТипЭксплуатации = ЗНАЧЕНИЕ(Перечисление.торо_ТипЭксплуатации.Простой)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ТаблицаОРСДублями.ОбъектРемонта,
	               |	ТаблицаОРСДублями.ДатаНачала,
	               |	ТаблицаОРСДублями.ДатаОкончания,
	               |	ТаблицаОРСДублями.ВремяПростоя,
	               |	МИНИМУМ(ТаблицаОРСДублями.РегистраторДата) КАК РегистраторДата
	               |ПОМЕСТИТЬ ТаблицаОРСДатами
	               |ИЗ
	               |	ТаблицаОРСДублями КАК ТаблицаОРСДублями
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаОРСДублями.ОбъектРемонта,
	               |	ТаблицаОРСДублями.ДатаНачала,
	               |	ТаблицаОРСДублями.ДатаОкончания,
	               |	ТаблицаОРСДублями.ВремяПростоя
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаОРСДублями.ОбъектРемонта,
	               |	ТаблицаОРСДублями.ДатаНачала,
	               |	ТаблицаОРСДублями.ДатаОкончания,
	               |	ТаблицаОРСДублями.ВремяПростоя,
	               |	ТаблицаОРСДублями.Регистратор,
	               |	ТаблицаОРСДублями.ОбъектРемонта.ПлановыйГрафикРаботы КАК ПлановыйГрафикРаботы,
	               |	ТаблицаОРСДублями.ПричинаПростоя,
	               |	ТаблицаОРСДублями.ДатаНачалаПоРегистру,
				   |	ТаблицаОРСДублями.ДатаОкончанияПоРегистру
	               |ПОМЕСТИТЬ ТаблицаОР
	               |ИЗ
	               |	ТаблицаОРСДублями КАК ТаблицаОРСДублями
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОРСДатами КАК ТаблицаОРСДатами
	               |		ПО ТаблицаОРСДублями.ДатаНачала = ТаблицаОРСДатами.ДатаНачала
	               |			И ТаблицаОРСДублями.ДатаОкончания = ТаблицаОРСДатами.ДатаОкончания
	               |			И ТаблицаОРСДублями.РегистраторДата = ТаблицаОРСДатами.РегистраторДата
	               |			И ТаблицаОРСДублями.ОбъектРемонта = ТаблицаОРСДатами.ОбъектРемонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ТаблицаОР.ПлановыйГрафикРаботы КАК ПлановыйГрафикРаботы
	               |ИЗ
	               |	ТаблицаОР КАК ТаблицаОР";

	
	Запрос.УстановитьПараметр("ДатаКон",ДатаКон);
	Запрос.УстановитьПараметр("ДатаНач",ДатаНач);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивГрафиков.Добавить(Выборка.ПлановыйГрафикРаботы);
	КонецЦикла;
	
	КалендарныеГрафики.СоздатьВТРасписанияРаботыНаПериод(МВТ,МассивГрафиков,ДатаНач,ДатаКон);
	
	// Получаем итоговую таблицу
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст ="ВЫБРАТЬ РАЗРЕШЕННЫЕ
	              |	ВТРасписанияРаботы.ГрафикРаботы,
	              |	ВТРасписанияРаботы.ДатаГрафика,
	              |	ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0), ВТРасписанияРаботы.ВремяНачала, СЕКУНДА)) КАК ВремяНачала,
	              |	ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0), ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА)) КАК ВремяОкончания
	              |ПОМЕСТИТЬ ВТРасписаниеРаботыПолныеДаты
	              |ИЗ
	              |	ВТРасписанияРаботы КАК ВТРасписанияРаботы
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ТаблицаОР.ОбъектРемонта КАК ОбъектРемонта,
	              |	СУММА(ВЫБОР
	              |			КОГДА ВТРасписаниеРаботыПолныеДаты.ДатаГрафика <> НАЧАЛОПЕРИОДА(ТаблицаОР.ДатаНачала, ДЕНЬ)
	              |					И ВТРасписаниеРаботыПолныеДаты.ДатаГрафика <> НАЧАЛОПЕРИОДА(ТаблицаОР.ДатаОкончания, ДЕНЬ)
	              |				ТОГДА РАЗНОСТЬДАТ(ВТРасписаниеРаботыПолныеДаты.ВремяНачала, ВТРасписаниеРаботыПолныеДаты.ВремяОкончания, СЕКУНДА)
	              |			ИНАЧЕ ВЫБОР
	              |					КОГДА ТаблицаОР.ДатаНачала < ВТРасписаниеРаботыПолныеДаты.ВремяНачала
	              |						ТОГДА ВЫБОР
	              |								КОГДА ТаблицаОР.ДатаОкончания < ВТРасписаниеРаботыПолныеДаты.ВремяОкончания
	              |									ТОГДА РАЗНОСТЬДАТ(ВТРасписаниеРаботыПолныеДаты.ВремяНачала, ТаблицаОР.ДатаОкончания, СЕКУНДА)
	              |								ИНАЧЕ РАЗНОСТЬДАТ(ВТРасписаниеРаботыПолныеДаты.ВремяНачала, ВТРасписаниеРаботыПолныеДаты.ВремяОкончания, СЕКУНДА)
	              |							КОНЕЦ
	              |					ИНАЧЕ ВЫБОР
	              |							КОГДА ТаблицаОР.ДатаОкончания < ВТРасписаниеРаботыПолныеДаты.ВремяОкончания
	              |								ТОГДА РАЗНОСТЬДАТ(ТаблицаОР.ДатаНачала, ТаблицаОР.ДатаОкончания, СЕКУНДА)
	              |							ИНАЧЕ РАЗНОСТЬДАТ(ТаблицаОР.ДатаНачала, ВТРасписаниеРаботыПолныеДаты.ВремяОкончания, СЕКУНДА)
	              |						КОНЕЦ
	              |				КОНЕЦ
	              |		КОНЕЦ) КАК ВремяПростоя,
	              |	ТаблицаОР.ДатаНачала КАК ДатаНачала,
	              |	ТаблицаОР.ДатаОкончания КАК ДатаОкончания,
	              |	ТаблицаОР.Регистратор,
	              |	ТаблицаОР.ПричинаПростоя,
	              |	ТаблицаОР.ДатаНачалаПоРегистру,
	              |	ТаблицаОР.ДатаОкончанияПоРегистру
	              |ПОМЕСТИТЬ ТабСВременемПростоя
	              |ИЗ
	              |	ТаблицаОР КАК ТаблицаОР
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписаниеРаботыПолныеДаты КАК ВТРасписаниеРаботыПолныеДаты
	              |		ПО ТаблицаОР.ПлановыйГрафикРаботы = ВТРасписаниеРаботыПолныеДаты.ГрафикРаботы
	              |			И (ВТРасписаниеРаботыПолныеДаты.ДатаГрафика >= НАЧАЛОПЕРИОДА(ТаблицаОР.ДатаНачала, ДЕНЬ))
	              |			И (ВТРасписаниеРаботыПолныеДаты.ДатаГрафика <= ВЫБОР
	              |				КОГДА &ДатаКон = ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59)
	              |					ТОГДА &ТекущаяДата
	              |				ИНАЧЕ НАЧАЛОПЕРИОДА(ТаблицаОР.ДатаОкончания, ДЕНЬ)
	              |			КОНЕЦ)
	              |			И (ВТРасписаниеРаботыПолныеДаты.ВремяНачала < ТаблицаОР.ДатаОкончания)
	              |			И (ТаблицаОР.ВремяПростоя = 0)
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ТаблицаОР.ОбъектРемонта,
	              |	ТаблицаОР.ДатаНачала,
	              |	ТаблицаОР.ДатаОкончания,
	              |	ТаблицаОР.Регистратор,
	              |	ТаблицаОР.ПричинаПростоя,
	              |	ТаблицаОР.ДатаНачалаПоРегистру,
	              |	ТаблицаОР.ДатаОкончанияПоРегистру
	              |
	              |ОБЪЕДИНИТЬ ВСЕ
	              |
	              |ВЫБРАТЬ
	              |	ТаблицаОР.ОбъектРемонта,
	              |	ТаблицаОР.ВремяПростоя,
	              |	ТаблицаОР.ДатаНачала,
	              |	ТаблицаОР.ДатаОкончания,
	              |	ТаблицаОР.Регистратор,
	              |	ТаблицаОР.ПричинаПростоя,
	              |	ТаблицаОР.ДатаНачалаПоРегистру,
	              |	ТаблицаОР.ДатаОкончанияПоРегистру
	              |ИЗ
	              |	ТаблицаОР КАК ТаблицаОР
	              |ГДЕ
	              |	ТаблицаОР.ВремяПростоя > 0
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ТабСВременемПростоя.ОбъектРемонта,
	              |	ТабСВременемПростоя.ОбъектРемонта КАК ОР,
	              |	ТабСВременемПростоя.ВремяПростоя КАК Секунды,
	              |	ТабСВременемПростоя.ДатаНачала КАК ДатаНачала,
	              |	ТабСВременемПростоя.ДатаОкончания КАК ДатаОкончания,
	              |	ТабСВременемПростоя.Регистратор,
	              |	ТабСВременемПростоя.ПричинаПростоя,
	              |	ТабСВременемПростоя.ДатаНачалаПоРегистру КАК ДатаОстанова,
	              |	ТабСВременемПростоя.ДатаОкончанияПоРегистру КАК ДатаЗапуска,
	              |	ВЫБОР
	              |		КОГДА торо_ТекущееСостояниеОРСрезПоследних.ВидЭксплуатации.ТипЭксплуатации = ЗНАЧЕНИЕ(Перечисление.торо_ТипЭксплуатации.Эксплуатация)
	              |			ТОГДА ""Оборудование работает""
	              |		ИНАЧЕ ВЫБОР
	              |				КОГДА торо_ТекущееСостояниеОРСрезПоследних.ВидЭксплуатации.ТипЭксплуатации = ЗНАЧЕНИЕ(Перечисление.торо_ТипЭксплуатации.Простой)
	              |					ТОГДА ""Оборудование остановлено""
	              |				ИНАЧЕ ""Оборудование на испытаниях""
	              |			КОНЕЦ
	              |	КОНЕЦ КАК СтатусОборудования,
	              |	торо_ТекущееСостояниеОР.Примечание
	              |ИЗ
	              |	ТабСВременемПростоя КАК ТабСВременемПростоя
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ТекущееСостояниеОР.СрезПоследних(&ТекущаяДата, ) КАК торо_ТекущееСостояниеОРСрезПоследних
	              |		ПО ТабСВременемПростоя.ОбъектРемонта = торо_ТекущееСостояниеОРСрезПоследних.ОбъектРемонта
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ТекущееСостояниеОР КАК торо_ТекущееСостояниеОР
	              |		ПО ТабСВременемПростоя.ОбъектРемонта = торо_ТекущееСостояниеОР.ОбъектРемонта
	              |			И ТабСВременемПростоя.Регистратор = торо_ТекущееСостояниеОР.Регистратор
	              |			И ТабСВременемПростоя.ДатаНачалаПоРегистру = торо_ТекущееСостояниеОР.Период
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	ДатаОстанова";

	Запрос.УстановитьПараметр("ДатаНач",ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон",ДатаКон);
	Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции
#КонецОбласти

#КонецЕсли