#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ТабДанныхДляСКД = ПолучитьДанныеДляСКД(НастройкиОсновнойСхемы);
	НастроитьДополнениеПоПериоду(НастройкиОсновнойСхемы);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);
	
	ВнешниеНаборы = Новый Структура("ВнешнийНабор", ТабДанныхДляСКД);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляСКД(НастройкиОсновнойСхемы)
	
	ПараметрыОтчетаСтруктура = ПолучитьПараметрыОтчета(НастройкиОсновнойСхемы);
	
	МенеджерВТ = ПолучитьОсновныеДанныеДляОтчета(НастройкиОсновнойСхемы, ПараметрыОтчетаСтруктура);
	ПериодОтчета = ПолучитьПериодОтчетаРеальный(МенеджерВТ);
	Графики = ПолучитьГрафикиДляРасчета(МенеджерВТ);
	ГрафикиРаботы.СоздатьВТРасписанияРаботыНаПериод(МенеджерВТ, Графики, ПериодОтчета.ДатаНачала, ПериодОтчета.ДатаОкончания);
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ГОД)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, НЕДЕЛЯ)
	|	КОНЕЦ КАК ДатаГрафика,
	|	СУММА(РАЗНОСТЬДАТ(ВТРасписанияРаботы.ВремяНачала, ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА)) КАК ВремяВСекундах,
	|	ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.Сотрудник
	|ПОМЕСТИТЬ ПлановоеВремяРаботыСотрудников
	|ИЗ
	|	ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета КАК ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК ВТРасписанияРаботы
	|		ПО ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.ГрафикРаботы = ВТРасписанияРаботы.ГрафикРаботы
	|			И (ВТРасписанияРаботы.ДатаГрафика МЕЖДУ ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.ДатаНачала И ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.ДатаОкончания)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ГОД)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, НЕДЕЛЯ)
	|	КОНЕЦ,
	|	ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФактЗатратыВРазрезеОбъектовИВидов.ОбъектРемонта,
	|	ФактЗатратыВРазрезеОбъектовИВидов.ВидРемонта,
	|	ЕСТЬNULL(ФактЗатратыВРазрезеОбъектовИВидов.Сотрудник, ПлановоеВремяРаботыСотрудников.Сотрудник) КАК Сотрудник,
	|	СУММА(ФактЗатратыВРазрезеОбъектовИВидов.ВремяРаботы) КАК ВремяРаботыФакт,
	|	ЕСТЬNULL(ФактЗатратыВРазрезеОбъектовИВидов.Период, ПлановоеВремяРаботыСотрудников.ДатаГрафика) КАК Период,
	|	МАКСИМУМ(ПлановоеВремяРаботыСотрудников.ВремяВСекундах) КАК ВремяРаботыПлан
	|ИЗ
	|	ПлановоеВремяРаботыСотрудников КАК ПлановоеВремяРаботыСотрудников
	|		ПОЛНОЕ СОЕДИНЕНИЕ ФактЗатратыВРазрезеОбъектовИВидов КАК ФактЗатратыВРазрезеОбъектовИВидов
	|		ПО ПлановоеВремяРаботыСотрудников.Сотрудник = ФактЗатратыВРазрезеОбъектовИВидов.Сотрудник
	|			И ПлановоеВремяРаботыСотрудников.ДатаГрафика = ФактЗатратыВРазрезеОбъектовИВидов.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактЗатратыВРазрезеОбъектовИВидов.ОбъектРемонта,
	|	ФактЗатратыВРазрезеОбъектовИВидов.ВидРемонта,
	|	ЕСТЬNULL(ФактЗатратыВРазрезеОбъектовИВидов.Сотрудник, ПлановоеВремяРаботыСотрудников.Сотрудник),
	|	ЕСТЬNULL(ФактЗатратыВРазрезеОбъектовИВидов.Период, ПлановоеВремяРаботыСотрудников.ДатаГрафика)");
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Периодичность", ПараметрыОтчетаСтруктура.Периодичность);
	
	ГрафикиРаботыРасч = Запрос.Выполнить().Выгрузить();
	
	МенеджерВТ.Закрыть();
	Возврат ГрафикиРаботыРасч;
	
КонецФункции

Функция ПолучитьОсновныеДанныеДляОтчета(НастройкиОсновнойСхемы, ПараметрыОтчетаСтруктура)
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_Ремонты.ID
	|ПОМЕСТИТЬ ОтборПоВидамДокументов
	|ИЗ
	|	РегистрСведений.торо_Ремонты КАК торо_Ремонты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ЗавершенныеРемонтныеРаботы КАК торо_ЗавершенныеРемонтныеРаботы
	|		ПО торо_Ремонты.ID = торо_ЗавершенныеРемонтныеРаботы.ID
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ЗавершенныеМероприятия КАК торо_ЗавершенныеМероприятия
	|		ПО торо_Ремонты.ID = торо_ЗавершенныеМероприятия.ID
	|ГДЕ
	|	((&ПоППР
	|				И торо_Ремонты.Регистратор ССЫЛКА Документ.торо_ПланГрафикРемонта
	|			ИЛИ &ПоПредписаниям
	|				И торо_Ремонты.Регистратор ССЫЛКА Документ.торо_ВнешнееОснованиеДляРабот
	|			ИЛИ &ПоВыявленнымДефектам
	|				И торо_Ремонты.Регистратор ССЫЛКА Документ.торо_ВыявленныеДефекты
	|			ИЛИ &ПоРегламентнымМероприятиям
	|				И торо_Ремонты.Регистратор ССЫЛКА Документ.торо_ГрафикРегламентныхМероприятийТОиР)
	|				И НЕ(торо_ЗавершенныеРемонтныеРаботы.Регистратор ЕСТЬ NULL 
	|						И торо_ЗавершенныеМероприятия.Регистратор ЕСТЬ NULL ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.РемонтыОборудования_ID,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Квалификация,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Сотрудник,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.ВремяРаботы,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Период
	|ПОМЕСТИТЬ ФактЗатраты
	|ИЗ
	|	РегистрНакопления.торо_ФактическиеТрудовыеЗатратыРемонтныхРабот КАК торо_ФактическиеТрудовыеЗатратыРемонтныхРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПоВидамДокументов КАК ОтборПоВидамДокументов
	|		ПО торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.РемонтыОборудования_ID = ОтборПоВидамДокументов.ID
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ПараметрПериодИспользуется
	|				ТОГДА торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МИНИМУМ(ВЫБОР
	|				КОГДА &ПараметрПериодИспользуется
	|					ТОГДА &ДатаНачала
	|				ИНАЧЕ ФактЗатраты.Период
	|			КОНЕЦ), &ДатаНачала) КАК ДатаНачала,
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|				КОГДА &ПараметрПериодИспользуется
	|					ТОГДА &ДатаОкончания
	|				ИНАЧЕ ФактЗатраты.Период
	|			КОНЕЦ), &ДатаОкончания) КАК ДатаОкончания
	|ПОМЕСТИТЬ ПериодОтчета
	|ИЗ
	|	ФактЗатраты КАК ФактЗатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КадроваяИсторияСотрудников.Сотрудник,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА КадроваяИсторияСотрудников.Регистратор ССЫЛКА Документ.Увольнение
	|					И КадроваяИсторияСотрудников.Период <= ПериодОтчета.ДатаОкончания
	|				ТОГДА КадроваяИсторияСотрудников.Период
	|			ИНАЧЕ ПериодОтчета.ДатаОкончания
	|		КОНЕЦ) КАК ДатаОкончанияПериодаОтчетаИлиУвольнения,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА КадроваяИсторияСотрудников.Период <= ПериодОтчета.ДатаНачала
	|				ТОГДА КадроваяИсторияСотрудников.Период
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ) КАК ДатаПервогоДокументаГрафикаРаботы
	|ПОМЕСТИТЬ ДатыДляПолученияГрафиков
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников,
	|	ПериодОтчета КАК ПериодОтчета
	|
	|СГРУППИРОВАТЬ ПО
	|	КадроваяИсторияСотрудников.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГрафикРаботыСотрудников.Сотрудник,
	|	ГрафикРаботыСотрудников.ГрафикРаботы,
	|	ГрафикРаботыСотрудников.Период
	|ПОМЕСТИТЬ ГрафикиПоСотрудникам
	|ИЗ
	|	РегистрСведений.ГрафикРаботыСотрудников КАК ГрафикРаботыСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыДляПолученияГрафиков КАК ДатыДляПолученияГрафиков
	|			ПОЛНОЕ СОЕДИНЕНИЕ ПериодОтчета КАК ПериодОтчета
	|			ПО (ИСТИНА)
	|		ПО ГрафикРаботыСотрудников.Сотрудник = ДатыДляПолученияГрафиков.Сотрудник
	|			И (ГрафикРаботыСотрудников.Период МЕЖДУ НАЧАЛОПЕРИОДА(ВЫБОР
	|					КОГДА ДатыДляПолученияГрафиков.ДатаПервогоДокументаГрафикаРаботы = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ПериодОтчета.ДатаНачала
	|					ИНАЧЕ ДатыДляПолученияГрафиков.ДатаПервогоДокументаГрафикаРаботы
	|				КОНЕЦ, ДЕНЬ) И ДатыДляПолученияГрафиков.ДатаОкончанияПериодаОтчетаИлиУвольнения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикиПоСотрудникам.Сотрудник,
	|	ГрафикиПоСотрудникам.ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА ГрафикиПоСотрудникам.Период < ПериодОтчета.ДатаНачала
	|			ТОГДА ПериодОтчета.ДатаНачала
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ГрафикиПоСотрудникам.Период, ДЕНЬ)
	|	КОНЕЦ КАК ДатаНачала,
	|	МАКСИМУМ(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ЕСТЬNULL(ГрафикиПоСотрудникам1.Период, ДатыДляПолученияГрафиков.ДатаОкончанияПериодаОтчетаИлиУвольнения), ДЕНЬ), СЕКУНДА, -1)) КАК ДатаОкончания
	|ПОМЕСТИТЬ ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета
	|ИЗ
	|	ГрафикиПоСотрудникам КАК ГрафикиПоСотрудникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикиПоСотрудникам КАК ГрафикиПоСотрудникам1
	|		ПО ГрафикиПоСотрудникам.Сотрудник = ГрафикиПоСотрудникам1.Сотрудник
	|			И ГрафикиПоСотрудникам.Период < ГрафикиПоСотрудникам1.Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыДляПолученияГрафиков КАК ДатыДляПолученияГрафиков
	|		ПО ГрафикиПоСотрудникам.Сотрудник = ДатыДляПолученияГрафиков.Сотрудник,
	|	ПериодОтчета КАК ПериодОтчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикиПоСотрудникам.Сотрудник,
	|	ГрафикиПоСотрудникам.ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА ГрафикиПоСотрудникам.Период < ПериодОтчета.ДатаНачала
	|			ТОГДА ПериодОтчета.ДатаНачала
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ГрафикиПоСотрудникам.Период, ДЕНЬ)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_РемонтыСрезПоследних.ID,
	|	МАКСИМУМ(торо_РемонтыСрезПоследних.ОбъектРемонта) КАК ОбъектРемонта,
	|	МАКСИМУМ(торо_РемонтыСрезПоследних.ВидРемонта) КАК ВидРемонта
	|ПОМЕСТИТЬ ВидыИОбъектыРемонтов
	|ИЗ
	|	РегистрСведений.торо_Ремонты.СрезПоследних(
	|			,
	|			ID В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ФактЗатраты.РемонтыОборудования_ID
	|				ИЗ
	|					ФактЗатраты КАК ФактЗатраты)) КАК торо_РемонтыСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_РемонтыСрезПоследних.ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, ГОД)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, НЕДЕЛЯ)
	|	КОНЕЦ КАК Период,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта,
	|	ФактЗатраты.Сотрудник,
	|	СУММА(ФактЗатраты.ВремяРаботы) КАК ВремяРаботы
	|ПОМЕСТИТЬ ФактЗатратыВРазрезеОбъектовИВидов
	|ИЗ
	|	ФактЗатраты КАК ФактЗатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыИОбъектыРемонтов КАК ВидыИОбъектыРемонтов
	|		ПО ФактЗатраты.РемонтыОборудования_ID = ВидыИОбъектыРемонтов.ID
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактЗатраты.Сотрудник,
	|	ВидыИОбъектыРемонтов.ВидРемонта,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта,
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, ГОД)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, НЕДЕЛЯ)
	|	КОНЕЦ");
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	КоллекцияПараметров = НастройкиОсновнойСхемы.ПараметрыДанных.Элементы;
	ПараметрПериод = КоллекцияПараметров.Найти("ПериодОтчета");
	ИспользованиеПериода = ПараметрПериод.Использование;
	Запрос.УстановитьПараметр("ПараметрПериодИспользуется", ИспользованиеПериода);
	Запрос.УстановитьПараметр("ДатаНачала", ?(ИспользованиеПериода, ПараметрПериод.Значение.ДатаНачала, Дата(1,1,1)));
	Запрос.УстановитьПараметр("ДатаОкончания", ?(ИспользованиеПериода, ПараметрПериод.Значение.ДатаОкончания, Дата(1,1,1)));
	Запрос.УстановитьПараметр("Периодичность",ПараметрыОтчетаСтруктура.Периодичность);
	Запрос.УстановитьПараметр("ПоРегламентнымМероприятиям",ПараметрыОтчетаСтруктура.ПоРегламентнымМероприятиям);
	Запрос.УстановитьПараметр("ПоППР",ПараметрыОтчетаСтруктура.ПоППР);
	Запрос.УстановитьПараметр("ПоПредписаниям",ПараметрыОтчетаСтруктура.ПоПредписаниям);
	Запрос.УстановитьПараметр("ПоВыявленнымДефектам",ПараметрыОтчетаСтруктура.ПоВыявленнымДефектам);
	
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, ПараметрыОтчетаСтруктура);
	Запрос.Выполнить();
	Возврат МенеджерВТ;
	
КонецФункции

Функция ПолучитьПериодОтчетаРеальный(МенеджерВТ)
	
	СтруктураВозврата = Новый Структура("ДатаНачала, ДатаОкончания");
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПериодОтчета.ДатаНачала,
	|	ПериодОтчета.ДатаОкончания
	|ИЗ
	|	ПериодОтчета КАК ПериодОтчета");
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
	Иначе
		СтруктураВозврата.ДатаНачала = Дата(1, 1, 1);
		СтруктураВозврата.ДатаОкончания = Дата(1, 1, 1);
	КонецЕсли;
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ПолучитьГрафикиДляРасчета(МенеджерВТ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГрафикиПоСотрудникам.ГрафикРаботы
	|ИЗ
	|	ГрафикиПоСотрудникам КАК ГрафикиПоСотрудникам");
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГрафикРаботы");
	
КонецФункции

Функция ПолучитьПараметрыОтчета(НастройкиОсновнойСхемы)
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Периодичность", НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("Периодичность").Значение);
	ПараметрыОтчета.Вставить("ПоРегламентнымМероприятиям",НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПоРегламентнымМероприятиям").Значение);
	ПараметрыОтчета.Вставить("ПоППР",НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПоППР").Значение);
	ПараметрыОтчета.Вставить("ПоПредписаниям",НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПоПредписаниям").Значение);
	ПараметрыОтчета.Вставить("ПоВыявленнымДефектам",НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПоВыявленнымДефектам").Значение);
	Возврат ПараметрыОтчета;
	
КонецФункции


Процедура НастроитьДополнениеПоПериоду(НастройкиСхемы)
	
	ПолеГруппировки = ПолучитьПолеГруппировкиКомпоновкиДанныхДляНастройкиДополнения(НастройкиСхемы, "Период");
	ПараметрПериодичностьОтчета = НастройкиСхемы.ПараметрыДанных.Элементы.Найти("Периодичность");
	ТипДополненияПериодаПеречисление = ПараметрПериодичностьОтчета.Значение;
	Если ПолеГруппировки <> Неопределено И ПараметрПериодичностьОтчета <> Неопределено Тогда
		ТипДополненияПериодаПеречисление = ПараметрПериодичностьОтчета.Значение;
		УстановитьТипДополненияПериодаДляПоляГруппировки(ПолеГруппировки, НастройкиСхемы,ТипДополненияПериодаПеречисление);
		УстановитьДатыНачалаИОкончанияДополнения(ПолеГруппировки, НастройкиСхемы,ТипДополненияПериодаПеречисление);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПолеГруппировкиКомпоновкиДанныхДляНастройкиДополнения(НастройкиСхемы, ИмяГруппировки)
	
	Для Каждого ЭлементНастройки Из НастройкиСхемы.Структура Цикл
		Если ТипЗнч(ЭлементНастройки) = Тип("ТаблицаКомпоновкиДанных") Тогда
			Для Каждого Колонка Из ЭлементНастройки.Колонки Цикл
				Если Колонка.Имя = ИмяГруппировки Тогда
					Возврат Колонка.ПоляГруппировки.Элементы[0];
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Процедура УстановитьТипДополненияПериодаДляПоляГруппировки(ПолеГруппировкиКомпоновкиДанных, НастройкиСхемы, ТипДополненияПериодаПеречисление)

	Если ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Месяц Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Месяц;
	ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.День Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.День;
	ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Год Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Год;
	ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Квартал Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Квартал;
	ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Неделя Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Неделя;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДатыНачалаИОкончанияДополнения(ПолеГруппировкиКомпоновкиДанных, НастройкиСхемы,ТипДополненияПериодаПеречисление)

	ПараметрПериодОтчета = НастройкиСхемы.ПараметрыДанных.Элементы.Найти("ПериодОтчета");
	
	Если ПараметрПериодОтчета <> Неопределено И ПараметрПериодОтчета.Использование Тогда
		
		Если ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Месяц Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = НачалоМесяца(ПараметрПериодОтчета.Значение.ДатаНачала);
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = КонецМесяца(ПараметрПериодОтчета.Значение.ДатаОкончания);
		ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.День Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = ПараметрПериодОтчета.Значение.ДатаНачала;
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = ПараметрПериодОтчета.Значение.ДатаОкончания;
		ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Год Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = НачалоГода(ПараметрПериодОтчета.Значение.ДатаНачала);
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = КонецГода(ПараметрПериодОтчета.Значение.ДатаОкончания);
		ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Квартал Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = НачалоКвартала(ПараметрПериодОтчета.Значение.ДатаНачала);
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = КонецКвартала(ПараметрПериодОтчета.Значение.ДатаОкончания);
		ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Неделя Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = НачалоНедели(ПараметрПериодОтчета.Значение.ДатаНачала);
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = КонецНедели(ПараметрПериодОтчета.Значение.ДатаОкончания);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли