
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяГруппыСеткиДиаграмм = "ГруппаТаблица";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РежимМонитора = "Обычный";
	РежимМонитораПриИзменении(Элементы.РежимМонитора);
		
	ПодключитьОбработчикВыбораПоказателей();
	ПодключитьОбработчикОжидания("ОбработчикОжиданияКонтрольПользовательскихНастроек", 1);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	БылаЗагрузкаНастроек = Истина;	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	БылаЗагрузкаНастроек = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	БылаЗагрузкаНастроек = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте 
Процедура Сформировать(Команда) 
	
	КоличествоКолонок = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "КоличествоКолонокПоказателя");
	Если КоличествоКолонок = 0 Тогда
		КоличествоКолонок = 2;
	КонецЕсли;
	
	СписокВариантовАнализа = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ПоказателиKPI", Истина);
	КоличествоСтрок = торо_ПоказателиKPIКлиентСервер.ОкруглитьВверх(СписокВариантовАнализа.Количество()/КоличествоКолонок);
	
	СоздатьСеткуЭлементовФормы(КоличествоСтрок, КоличествоКолонок, СписокВариантовАнализа);
	ПолучитьПользовательскиеНастройкиНаФорму();
	
	СформироватьНаСервере(СписокВариантовАнализа);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере(СписокВариантовАнализа)
		
	торо_ПоказателиKPI.ПолучитьДанныеПоказателей(СписокВариантовАнализа, ЭтаФорма);
	ЗаполнитьЦелевыеЗначенияПоказателей(СписокВариантовАнализа);
	ВывестиДанныеПоказателей(СписокВариантовАнализа);
	ЗаполнитьИнтегральнуюОценку(СписокВариантовАнализа);
	
КонецПроцедуры

&НаКлиенте
Процедура Перерисовать(Команда)
	
	КоличествоКолонок = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "КоличествоКолонокПоказателя");
	Если КоличествоКолонок = 0 Тогда
		КоличествоКолонок = 2;
	КонецЕсли;

	КоличествоСтрок = торо_ПоказателиKPIКлиентСервер.ОкруглитьВверх(СписокВариантовАнализа.Количество()/КоличествоКолонок);
	СоздатьСеткуЭлементовФормы(КоличествоСтрок, КоличествоКолонок, СписокВариантовАнализа);
	
	ТипСерийИнтегральнойОценки = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ТипСерийИнтегральнойОценки");
	
	ПерерисоватьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПерерисоватьНаСервере()

	ЗаполнитьЦелевыеЗначенияПоказателей(СписокВариантовАнализа);
	ВывестиДанныеПоказателей(СписокВариантовАнализа);
	ЗаполнитьИнтегральнуюОценку(СписокВариантовАнализа);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РасшифроватьПоказатель(Команда)
	
	НомерЯчейки = СтрЗаменить(Команда.Имя, "_РасшифроватьПоказатель", "");
	Попытка
		НомерЯчейки = Число(НомерЯчейки);
	Исключение
		Возврат;
	КонецПопытки;
		
	ВариантАнализа = СписокВариантовАнализа[НомерЯчейки-1].Значение;
	Показатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантАнализа, "Владелец");
	ИмяОтчетаДляРасшифровки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Показатель, "Отчет");
	
	ПараметрыОтчета = Новый Структура;
	
	Если НЕ ЗначениеЗаполнено(ИмяОтчетаДляРасшифровки) Тогда
		//ИмяОтчетаДляРасшифровки = "УниверсальныйОтчет";
		ОткрытьФормуУниверсальнойРасшифровки(ВариантАнализа, Показатель);
		Возврат;
	КонецЕсли;
	
	Если ИмяОтчетаДляРасшифровки = "УниверсальныйОтчет" Тогда
		ПараметрыОтчета.Вставить("ТипОбъектаМетаданных", "РегистрыНакопления");
		ПараметрыОтчета.Вставить("ПолноеИмяОбъектаМетаданных", "торо_ЗначенияВнешнихПоказателейKPI");
		ПараметрыОтчета.Вставить("ИмяТаблицы", "ОстаткиИОбороты");
	КонецЕсли;
	
	ПараметрыМетаданных = ПолучитьПараметрыМетаданныхОтчетаРасшифровки(ИмяОтчетаДляРасшифровки, ПараметрыОтчета);
	
	Если НЕ ПараметрыМетаданных.ОтчетСуществует Тогда
		ШаблонСообщения = НСтр("ru='Отчет с именем ""%1"" не найден в конфигурации.'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ИмяОтчетаДляРасшифровки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;	
	
	СтруктураОтбора = Новый Структура;
	Если ИспользоватьОтборПоОрганизации И ПараметрыМетаданных.ЕстьОрганизация Тогда
		СтруктураОтбора.Вставить("Организация", ОтборПоОрганизации);
	КонецЕсли;
	Если ИспользоватьОтборПоПодразделению И ПараметрыМетаданных.ЕстьПодразделение Тогда
		СтруктураОтбора.Вставить("Подразделение", ОтборПоПодразделению);
	КонецЕсли;
	Если ИспользоватьОтборПоОР И ПараметрыМетаданных.ЕстьОбъектРемонта Тогда
		СтруктураОтбора.Вставить("ОбъектРемонтныхРабот", ОтборПоОР);
		СтруктураОтбора.Вставить("ОбъектРемонта", ОтборПоОР);
		Если ИмяОтчетаДляРасшифровки = "торо_ПрямыеЗатратыНаВыполнениеРемонтов" Тогда
			СтруктураОтбора.Вставить("Объект", ОтборПоОР);
		ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_СписокОборудования" Тогда
			СтруктураОтбора.Вставить("ОбъектИерархии", ОтборПоОР);
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыМетаданных.ЕстьПоказатель Тогда
		СтруктураОтбора.Вставить("Показатель", Показатель);
	КонецЕсли;
	
	Если ИмяОтчетаДляРасшифровки = "торо_ПланМТОТрудозатратИнструментов" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_ПлановыеВнеплановыеРемонты" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
		СтруктураОтбора.Вставить("ПериодВыполнения", ОтборПоПериоду);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_ПлановыеОстановыОборудования" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_ПланФактВыполненияРемонтовПодрядчиками" Тогда
		СтруктураОтбора.Вставить("СтандПериод", ОтборПоПериоду);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_ПланФактЗатрат" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
      СтруктураОтбора.Вставить("ПериодВыполнения", ОтборПоПериоду);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_ПланФактныйАнализППР" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_ПричиныОтменыПлановыхРемонтов" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_ПрямыеЗатратыНаВыполнениеРемонтов" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_СостояниеОборудования" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
      СтруктураОтбора.Вставить("ПериодВыполнени", ОтборПоПериоду);
	ИначеЕсли ИмяОтчетаДляРасшифровки = "торо_СписокОборудования" Тогда
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду.ДатаОкончания);
	Иначе
		СтруктураОтбора.Вставить("Период", ОтборПоПериоду);
	КонецЕсли;
	
	СтруктураОтбора.Вставить("ПериодОтчета", ОтборПоПериоду);
	
	ПараметрыФормыОтчетаРасшифровки = Новый Структура;
	ПараметрыФормыОтчетаРасшифровки.Вставить("Период", ОтборПоПериоду);
	ПараметрыФормыОтчетаРасшифровки.Вставить("СформироватьПриОткрытии", Истина);
		
	Если ИмяОтчетаДляРасшифровки = "УниверсальныйОтчет" Тогда
		ПараметрыОтчета.Вставить("Период", ОтборПоПериоду);
		ПараметрыОтчета.Вставить("ПараметрыОтбора", СтруктураОтбора);
		ПараметрыФормыОтчетаРасшифровки.Вставить("ПараметрыОтчета", ПараметрыОтчета);
	Иначе
		ПараметрыОтчета.Вставить("ПараметрыОтбора", СтруктураОтбора);
		ПараметрыФормыОтчетаРасшифровки.Вставить("ПараметрыОтчета", ПараметрыОтчета);
	КонецЕсли;
	
	Попытка
		ОткрытьФорму("Отчет."+ИмяОтчетаДляРасшифровки+".Форма", ПараметрыФормыОтчетаРасшифровки, ЭтаФорма, Показатель);
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ШаблонСообщения = НСтр("ru='Не удалось открыть форму отчета для расшифровки: %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуУниверсальнойРасшифровки(ВариантАнализа, Показатель)
	
	ПараметрыФормы = Новый Структура;
	торо_ПоказателиKPIКлиентСервер.ПодготовитьСтруктуруПараметровДляОтчетаРасшифровкиПоказателя(ЭтаФорма, ПараметрыФормы, ВариантАнализа, Показатель);
	
	ОткрытьФорму("Отчет.торо_МониторKPI.Форма.ФормаРасшифровкиПоказателя", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПоказатель(Команда) 
	
	НомерЯчейки = СтрЗаменить(Команда.Имя, "_ОткрытьПоказатель", "");
	Попытка
		НомерЯчейки = Число(НомерЯчейки);
	Исключение
		Возврат;
	КонецПопытки;
	
	ВариантДляОткрытия = СписокВариантовАнализа[НомерЯчейки-1].Значение;
	ПоказатьЗначение(,ВариантДляОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
   ТабДок = НапечататьОтчет();
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	ИдентификаторПечатнойФормы = "МониторKPI";
	КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
	ПечатнаяФорма = УправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
	ПечатнаяФорма.СинонимМакета = "Монитор KPI";
	ПечатнаяФорма.ТабличныйДокумент = ТабДок;
	ПечатнаяФорма.ИмяФайлаПечатнойФормы = "МониторKPI";
	
	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм,,ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура МетодическаяИнформацияПоМонитору(Команда)
	
	ОткрытьСправкуФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	
	УстановитьСтандартныеНастройкиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлеметовФормы

&НаКлиенте
Процедура РежимМонитораПриИзменении(Элемент)
	
	Если РежимМонитора = "Обычный" Тогда
		Элементы.СтраницыРежимов.ТекущаяСтраница = Элементы.ОбычныйРежим;
		Элементы.ОбычныйРежим.Видимость = Истина;
		Элементы.ИнтегральнаяОценка.Видимость = Ложь;
	Иначе
		Элементы.СтраницыРежимов.ТекущаяСтраница = Элементы.ИнтегральнаяОценка;
		Элементы.ОбычныйРежим.Видимость = Ложь;
		Элементы.ИнтегральнаяОценка.Видимость = Истина;
	КонецЕсли;
	
	УстановитьВидимостьНастроекИнтегральнойОценки();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказателиKPIНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрТипПоказателей = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ТипПоказателей");
	ПараметрПоказателиKPI = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ПоказателиKPI", Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипПоказателя", ПараметрТипПоказателей);
	ПараметрыФормы.Вставить("СписокЗначений", ПараметрПоказателиKPI);
	ОткрытьФорму("Отчет.торо_МониторKPI.Форма.ФормаВыбораВариантовАнализа", ПараметрыФормы, Элемент, ЭтаФорма.УникальныйИдентификатор, ВариантОткрытияОкна.ОтдельноеОкно);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТипСерийИнтегральнойОценкиПриИзменении(Элемент, СтандартнаяОбработка)
		
	Перерисовать(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСтандартныеНастройкиНаСервере()
	
	СтандартныеНастройки = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы;
	ПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	Для каждого СтандартныйПараметр из СтандартныеНастройки Цикл
		Если ЗначениеЗаполнено(СтандартныйПараметр.ИдентификаторПользовательскойНастройки) Тогда
			ПользПараметр = ПользовательскиеНастройки.Элементы.Найти(СтандартныйПараметр.ИдентификаторПользовательскойНастройки);
			Если ПользПараметр = Неопределено Тогда
				ПользПараметр = ПользовательскиеНастройки.Элементы.Добавить();
				ПользПараметр.ИдентификаторПользовательскойНастройки = СтандартныйПараметр.ИдентификаторПользовательскойНастройки;
				ПользПараметр.Параметр = СтандартныйПараметр.Параметр;
			КонецЕсли;
			ПользПараметр.Использование = СтандартныйПараметр.Использование;
			ПользПараметр.Значение = СтандартныйПараметр.Значение;
			ПользПараметр.РежимОтображения = СтандартныйПараметр.РежимОтображения;
		КонецЕсли;
	КонецЦикла;
	
	ПодключитьОбработчикВыбораПоказателей();
	УстановитьВидимостьНастроекИнтегральнойОценки();
	
КонецПроцедуры

&НаСервере
Процедура ПодключитьОбработчикВыбораПоказателей()
	
	ЭлементСписокВариантовАнализа = НайтиЭлементФормыПользовательскойНастройкиПоЗаголовку(ЭтаФорма, "Показатели (KPI)");
	Если ЭлементСписокВариантовАнализа <> Неопределено Тогда
		ЭлементСписокВариантовАнализа.УстановитьДействие("НачалоВыбора", "Подключаемый_ПоказателиKPIНачалоВыбора");
	КонецЕсли;
	
	ЭлементТипСерийИнтегральнойОценки = НайтиЭлементФормыПользовательскойНастройкиПоЗаголовку(ЭтаФорма, "Тип серий интегральной оценки");
	Если ЭлементТипСерийИнтегральнойОценки <> Неопределено Тогда
		ЭлементТипСерийИнтегральнойОценки.УстановитьДействие("ПриИзменении", "Подключаемый_ТипСерийИнтегральнойОценкиПриИзменении");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьНастроекИнтегральнойОценки()
	
	ЭлементТипСерийИнтегральнойОценки = НайтиЭлементФормыПользовательскойНастройкиПоЗаголовку(ЭтаФорма, "Тип серий интегральной оценки");
	Если РежимМонитора = "Обычный" Тогда
		ЭлементТипСерийИнтегральнойОценки.Видимость = Ложь;
	Иначе
		ЭлементТипСерийИнтегральнойОценки.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияКонтрольПользовательскихНастроек()
	
	Если БылаЗагрузкаНастроек Тогда
		ПодключитьОбработчикВыбораПоказателей();
		УстановитьВидимостьНастроекИнтегральнойОценки();
		БылаЗагрузкаНастроек = Ложь;
	КонецЕсли;
	
КонецПроцедуры
/////////////////////////////////////////////////////////////////////////////
// Создание элементов формы

&НаСервере
Процедура СоздатьСеткуЭлементовФормы(КоличествоСтрок, КоличествоКолонок, СписокВариантовАнализа)
	
	УдалитьСтарыеЭлементы();
	
	РодительскаяГруппа = Элементы[ИмяГруппыСеткиДиаграмм];
	
	НомерЯчейки = 1;
	Для НомерСтроки = 1 по КоличествоСтрок Цикл
		Если НомерЯчейки > СписокВариантовАнализа.Количество() Тогда
			Прервать;
		КонецЕсли;
						
		ГруппаСтрока = Элементы.Добавить("Строка"+НомерСтроки, Тип("ГруппаФормы"), РодительскаяГруппа);
		ГруппаСтрока.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаСтрока.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ГруппаСтрока.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаСтрока.Объединенная = Ложь;
		
		Для НомерКолонки = 1 По КоличествоКолонок Цикл
			Если НомерЯчейки > СписокВариантовАнализа.Количество() Тогда
				Прервать;
			КонецЕсли;
			
			НомерЯчейкиСтрокой = Формат(НомерЯчейки, "ЧГ=0");
			
			ВариантАнализа = СписокВариантовАнализа[НомерЯчейки-1].Значение;
			ЕдиницаИзмерения = ВариантАнализа.Владелец.ЕдиницаИзмерения;
			
			ГруппаЯчейка = Элементы.Добавить("Ячейка"+НомерЯчейкиСтрокой, Тип("ГруппаФормы"), ГруппаСтрока);
			ГруппаЯчейка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаЯчейка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
			ГруппаЯчейка.Отображение = ОтображениеОбычнойГруппы.Нет;

			ГруппаШапкаПоказателя = Элементы.Добавить("ШапкаПоказателя"+НомерЯчейкиСтрокой, Тип("ГруппаФормы"), ГруппаЯчейка);
			ГруппаШапкаПоказателя.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаШапкаПоказателя.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
			ГруппаШапкаПоказателя.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаШапкаПоказателя.РастягиватьПоГоризонтали = Истина;
			
			ДекорацияПоказателя = Элементы.Добавить("НаименованиеПоказателя"+НомерЯчейкиСтрокой, Тип("ДекорацияФормы"), ГруппаШапкаПоказателя);
			ДекорацияПоказателя.Вид = ВидДекорацииФормы.Надпись;
			ДекорацияПоказателя.АвтоМаксимальнаяШирина = Ложь;
			ДекорацияПоказателя.РастягиватьПоГоризонтали = Истина;
			ДекорацияПоказателя.Заголовок = Строка(ВариантАнализа) + ", "+Строка(ЕдиницаИзмерения);
			ДекорацияПоказателя.Шрифт = Новый Шрифт(ДекорацияПоказателя.Шрифт,,,Истина);
			
			ГруппаКоманднаяПанельПоказателя = Элементы.Добавить("КоманднаяПанельПоказателя"+НомерЯчейкиСтрокой, Тип("ГруппаФормы"), ГруппаШапкаПоказателя);
			ГруппаКоманднаяПанельПоказателя.Вид = ВидГруппыФормы.КоманднаяПанель;
			ГруппаКоманднаяПанельПоказателя.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
			
			ПодменюПоказателя = Элементы.Добавить("ПодменюПоказателя"+НомерЯчейкиСтрокой, Тип("ГруппаФормы"), ГруппаКоманднаяПанельПоказателя);
			ПодменюПоказателя.Вид = ВидГруппыФормы.Подменю;
			ПодменюПоказателя.Заголовок = "\/";
			
			КомандаРасшифровки = ЭтаФорма.Команды.Добавить("_РасшифроватьПоказатель"+НомерЯчейкиСтрокой);
			КомандаРасшифровки.Заголовок = "Расшифровать показатель";
			КомандаРасшифровки.Действие = "Подключаемый_РасшифроватьПоказатель";
			КомандаРасшифровки.Картинка = БиблиотекаКартинок.Отчет;
			
			ЭлементКомандаРасшифровки = Элементы.Добавить("_РасшифроватьПоказатель"+НомерЯчейкиСтрокой, Тип("КнопкаФормы"), ПодменюПоказателя);
			ЭлементКомандаРасшифровки.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
			ЭлементКомандаРасшифровки.ИмяКоманды = "_РасшифроватьПоказатель"+НомерЯчейкиСтрокой;
			
			КомандаОткрытия = ЭтаФорма.Команды.Добавить("_ОткрытьПоказатель"+НомерЯчейкиСтрокой);
			КомандаОткрытия.Заголовок = "Открыть показатель";
			КомандаОткрытия.Действие = "Подключаемый_ОткрытьПоказатель";
			КомандаОткрытия.Картинка = БиблиотекаКартинок.Изменить;
			
			ЭлементКомандаОткрытия = Элементы.Добавить("_ОткрытьПоказатель"+НомерЯчейкиСтрокой, Тип("КнопкаФормы"), ПодменюПоказателя);
			ЭлементКомандаОткрытия.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
			ЭлементКомандаОткрытия.ИмяКоманды = "_ОткрытьПоказатель"+НомерЯчейкиСтрокой;
			
			ДобавитьДиаграммуДляВариантаАнализа(НомерЯчейки, ГруппаЯчейка, ВариантАнализа);
			
			НомерЯчейки = НомерЯчейки + 1;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтарыеЭлементы()

	РодительскаяГруппа = Элементы[ИмяГруппыСеткиДиаграмм];
	УдаляемыеЭлементы = Новый массив;
	Для каждого Элемент из РодительскаяГруппа.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(Элемент);
	КонецЦикла; 
	
	Для каждого Элемент из УдаляемыеЭлементы Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
	УдаляемыеКоманды = Новый Массив;
	Для каждого Команда из ЭтаФорма.Команды Цикл
		Если СтрНачинаетсяС(Команда.Имя, "_РасшифроватьПоказатель") 
			ИЛИ СтрНачинаетсяС(Команда.Имя, "_ОткрытьПоказатель") Тогда
			УдаляемыеКоманды.Добавить(Команда);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Команда из УдаляемыеКоманды Цикл
		ЭтаФорма.Команды.Удалить(Команда);
	КонецЦикла;
	
	УдаляемыеРеквизиты = Новый Массив;
	МассивРеквизитов = ПолучитьРеквизиты();
	Для каждого Реквизит из МассивРеквизитов Цикл
		Если СтрНачинаетсяС(Реквизит.Имя, "_Результат") Тогда
			УдаляемыеРеквизиты.Добавить(Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ИзменитьРеквизиты(,УдаляемыеРеквизиты);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДиаграммуДляВариантаАнализа(НомерЯчейки, РодительскаяГруппа, ВариантАнализа)
	
	ВариантОтображения = ВариантАнализа.ВариантОтображенияПоУмолчанию;
	НомерЯчейкиСтрокой = Формат(НомерЯчейки, "ЧГ=0");
	ИмяРеквизита = "_Результат"+НомерЯчейкиСтрокой;
	
	Если ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Диаграмма Тогда
		РеквизитРезультат = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("Диаграмма")); 
		
		МассивРеквизитов = Новый Массив;
		МассивРеквизитов.Добавить(РеквизитРезультат);
		ИзменитьРеквизиты(МассивРеквизитов);
		
		ЭлементРезультат = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), РодительскаяГруппа);
		ЭлементРезультат.Вид = ВидПоляФормы.ПолеДиаграммы;
		ЭлементРезультат.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ЭлементРезультат.ПутьКДанным = ИмяРеквизита;
		
		ЭтаФорма[ИмяРеквизита].ТипДиаграммы = торо_ПоказателиKPIКлиентСервер.ПолучитьТипДиаграммыПоЗначениюПеречисления(ВариантАнализа.ТипДиаграммы);
		
	ИначеЕсли ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Кратко Тогда
		
		ЭлементРезультат = Элементы.Добавить(ИмяРеквизита, Тип("ДекорацияФормы"), РодительскаяГруппа);
		ЭлементРезультат.Вид = ВидДекорацииФормы.Надпись;
		
		ЭлементРезультат.РастягиватьПоГоризонтали = Истина;
		ЭлементРезультат.РастягиватьПоВертикали = Истина;
		ЭлементРезультат.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная);
		ЭлементРезультат.Высота = 2;
		ЭлементРезультат.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		ЭлементРезультат.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
		ЭлементРезультат.АвтоМаксимальнаяШирина = Ложь;
		ЭлементРезультат.Шрифт = Новый Шрифт("Arial", 20, Истина);
		
	ИначеЕсли ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Таблица Тогда
		МассивРеквизитов = Новый Массив;
		МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("ТаблицаЗначений")));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Период", Новый ОписаниеТипов("Дата"), ИмяРеквизита));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Разрез", Новый ОписаниеТипов("Строка"), ИмяРеквизита));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Значение", Новый ОписаниеТипов("Строка"), ИмяРеквизита));
		
		ИзменитьРеквизиты(МассивРеквизитов);

		ЭлементРезультат = Элементы.Добавить(ИмяРеквизита, Тип("ТаблицаФормы"), РодительскаяГруппа);
		ЭлементРезультат.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ЭлементРезультат.ПутьКДанным = ИмяРеквизита;
		ЭлементРезультат.ТолькоПросмотр = Истина;
		ЭлементРезультат.КоманднаяПанель.Видимость = Ложь;
		
		ЭлементПериод = Элементы.Добавить(ИмяРеквизита+"Период", Тип("ПолеФормы"), ЭлементРезультат);
		ЭлементПериод.ПутьКДанным = ИмяРеквизита+".Период";
		ЭлементПериод.Видимость = Ложь;
				
		ЭлементРазрез = Элементы.Добавить(ИмяРеквизита+"Разрез", Тип("ПолеФормы"), ЭлементРезультат);
		ЭлементРазрез.ПутьКДанным = ИмяРеквизита+".Разрез";
		
		ЭлементЗначение = Элементы.Добавить(ИмяРеквизита+"Значение", Тип("ПолеФормы"), ЭлементРезультат);
		ЭлементЗначение.ПутьКДанным = ИмяРеквизита+".Значение";
		
	КонецЕсли;
			
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////
// Расчет показателей

&НаКлиенте
Процедура ПолучитьПользовательскиеНастройкиНаФорму()
	
	// Получим настройки и отборы
	ОтборПоПериоду = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "Период");
	ОтборПоПериодуСравнения = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ПериодСравнения");
	
	ТипПоказателей = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ТипПоказателей");
	
	ИспользоватьДетализациюПоПериоду = ПолучитьИспользованиеПараметраИзПользовательскихНастроек(ЭтаФорма, "ДетализацияПоПериоду");
	Если ИспользоватьДетализациюПоПериоду Тогда
		ДетализацияПоПериоду = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ДетализацияПоПериоду");
	Иначе
		ДетализацияПоПериоду = Неопределено;
	КонецЕсли;
	
	ИспользоватьОтборПоОрганизации = ПолучитьИспользованиеПараметраИзПользовательскихНастроек(ЭтаФорма, "Организация");
	Если ИспользоватьОтборПоОрганизации Тогда
		ОтборПоОрганизации = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "Организация", Истина);
	Иначе
		ОтборПоОрганизации = Новый СписокЗначений;
	КонецЕсли;
	
	ИспользоватьОтборПоПодразделению = ПолучитьИспользованиеПараметраИзПользовательскихНастроек(ЭтаФорма, "Подразделение");
	Если ИспользоватьОтборПоПодразделению Тогда
		ОтборПоПодразделению = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "Подразделение", Истина);
	Иначе
		ОтборПоПодразделению = Новый СписокЗначений;
	КонецЕсли;

	ИспользоватьОтборПоОР = ПолучитьИспользованиеПараметраИзПользовательскихНастроек(ЭтаФорма, "ОбъектРемонта");
	Если ИспользоватьОтборПоОР Тогда
		ОтборПоОР = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ОбъектРемонта", Истина);
	Иначе
		ОтборПоОР = Новый СписокЗначений;
	КонецЕсли;
	
	ТипСерийИнтегральнойОценки = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "ТипСерийИнтегральнойОценки");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦелевыеЗначенияПоказателей(СписокВариантовАнализа)
	
	ЦелевыеЗначенияПоказателей.Очистить();
	табЦелевыхЗначений = торо_ПоказателиKPI.ПолучитьЦелевыеЗначенияПоказателей(СписокВариантовАнализа, ЭтаФорма);
	ЦелевыеЗначенияПоказателей.Загрузить(табЦелевыхЗначений);
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////
// Вывод данных

&НаСервере
Процедура ВывестиДанныеПоказателей(СписокВариантовАнализа)
	
	ТаблицаПараметровДиаграмм = торо_ПоказателиKPI.ПолучитьТаблицуСоответствиТиповАнализаИТиповДиаграмм();
	
	НомерЯчейки = 0;
	Для каждого ЭлементСписка из СписокВариантовАнализа Цикл
		НомерЯчейки = НомерЯчейки + 1;
		
		ВариантАнализа = ЭлементСписка.Значение;
		Показатель = ВариантАнализа.Владелец;
		ВариантОтображения = ВариантАнализа.ВариантОтображенияПоУмолчанию;
		
		СтрокаПоказателя = ДанныеПоказателей.НайтиСтроки(Новый Структура("Показатель", Показатель));
		Если СтрокаПоказателя.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокаПоказателя = СтрокаПоказателя[0];
		
		Если ЭтоАдресВременногоХранилища(СтрокаПоказателя.АдресХранилищаДанных) Тогда
			ТабДанныхИсх = ПолучитьИзВременногоХранилища(СтрокаПоказателя.АдресХранилищаДанных);
			ТабДанных = ТабДанныхИсх.Скопировать();
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ЭтоАдресВременногоХранилища(СтрокаПоказателя.АдресХранилищаДанныхСПериодами) Тогда
			ТабДанныхСПериодамиИсх = ПолучитьИзВременногоХранилища(СтрокаПоказателя.АдресХранилищаДанныхСПериодами);
			ТабДанныхСПериодами = ТабДанныхСПериодамиИсх.Скопировать();
		Иначе
			Продолжить;
		КонецЕсли;
		
		Попытка
			СтруктураДанныхДляВывода = торо_ПоказателиKPI.ПолучитьСтруктураДанныхДляВыводаВМонитор(ТабДанных, ТабДанныхСПериодами, ВариантАнализа, ЭтаФорма);
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			ШаблонОшибки = НСтр("ru='При обработке данных показателя ""%1"" произошла ошибка: %2'");
			ТекстСообщения = СтрШаблон(ШаблонОшибки, ВариантАнализа, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Продолжить;
		КонецПопытки;
		
		Если СтруктураДанныхДляВывода = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			Если ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Диаграмма Тогда
				ВывестиДанныеПоказателяВДиаграмму(СтруктураДанныхДляВывода, ВариантАнализа, НомерЯчейки);
			ИначеЕсли ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Кратко Тогда
				ВывестиДанныеПоказателяВКраткомВиде(СтруктураДанныхДляВывода, ВариантАнализа, НомерЯчейки);
			ИначеЕсли ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Таблица Тогда
				ВывестиДанныеПоказателяВТаблицу(СтруктураДанныхДляВывода, ВариантАнализа, НомерЯчейки);
			КонецЕсли;
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			ШаблонОшибки = НСтр("ru='При выводе данных показателя ""%1"" произошла ошибка: %2'");
			ТекстСообщения = СтрШаблон(ШаблонОшибки, ВариантАнализа, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
	КонецЦикла
	
КонецПроцедуры

&НаСервере
Процедура ВывестиДанныеПоказателяВДиаграмму(СтруктураДанныхДляВывода, ВариантАнализа,  НомерЯчейки)
	
	НомерЯчейкиСтрокой = Формат(НомерЯчейки, "ЧГ=0");
	Диаграмма = ЭтаФорма["_Результат"+НомерЯчейкиСтрокой];
	
	торо_ПоказателиKPI.ВывестиДанныеПоказателяВДиаграмму(СтруктураДанныхДляВывода, ВариантАнализа, Диаграмма, ЭтаФорма);	
	
КонецПроцедуры

&НаСервере
Процедура ВывестиДанныеПоказателяВКраткомВиде(СтруктураДанныхДляВывода, ВариантАнализа, НомерЯчейки) 
	
	НомерЯчейкиСтрокой = Формат(НомерЯчейки, "ЧГ=0");
	ЭлементРезультат = Элементы["_Результат"+НомерЯчейкиСтрокой];
	
	торо_ПоказателиKPI.ВывестиДанныеПоказателяВКраткомВиде(СтруктураДанныхДляВывода, ВариантАнализа, ЭлементРезультат.Заголовок);
		
КонецПроцедуры

&НаСервере
Процедура ВывестиДанныеПоказателяВТаблицу(СтруктураДанныхДляВывода, ВариантАнализа, НомерЯчейки) 
	
	НомерЯчейкиСтрокой = Формат(НомерЯчейки, "ЧГ=0");
	РеквизитРезультат = ЭтаФорма["_Результат"+НомерЯчейкиСтрокой];
	ЭлементПериод = Элементы["_Результат"+НомерЯчейкиСтрокой+"Период"];
	ЭлементРазрез = Элементы["_Результат"+НомерЯчейкиСтрокой+"Разрез"];
	
	торо_ПоказателиKPI.ВывестиДанныеПоказателяВТаблицу(СтруктураДанныхДляВывода, ВариантАнализа, РеквизитРезультат);
	
	ТипАнализа = СтруктураДанныхДляВывода.ТипАнализа;
			
	// Динамика изменения
	Если ТипАнализа = Перечисления.торо_ТипыАнализа.ДинамикаИзменения Тогда
		ЭлементПериод.Видимость = Истина;
		ЭлементПериод.Формат = торо_ПоказателиKPIКлиентСервер.ПолучитьФорматнуюСтрокуДляДаты(ЭтаФорма);
		ЭлементРазрез.Видимость = Ложь;
		
	// Покомпонентное сравнение (динамика)
	ИначеЕсли ТипАнализа = Перечисления.торо_ТипыАнализа.ПокомпонентноеСравнениеДинамика Тогда
		ЭлементПериод.Видимость = Истина;
		ЭлементПериод.Формат = торо_ПоказателиKPIКлиентСервер.ПолучитьФорматнуюСтрокуДляДаты(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////
// Печать

&НаСервере
Функция НапечататьОтчет()
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	Макет = ОтчетОбъект.ПолучитьМакет("МакетСводнойСтраницы");
	
	НетДоступныхПоказателей = (СписокВариантовАнализа.Количество()=0);
	торо_ПоказателиKPI.ВывестиСообщениеНетДоступныхПоказателей(ДокументРезультат, Макет, НетДоступныхПоказателей);

	Если РежимМонитора = "Обычный" Тогда
		
		// Вывод заголовка и параметров
		торо_ПоказателиKPI.ВывестиЗаголовок(ДокументРезультат, Макет);
		торо_ПоказателиKPI.ВывестиПараметры(ДокументРезультат, Макет, ЭтаФорма);
		торо_ПоказателиKPI.ВывестиДатуВремяФормирования(ДокументРезультат, Макет);
	
		// Вывод показателей
		
		КоличествоКолонок = ПолучитьЗначениеПараметраИзПользовательскихНастроек(ЭтаФорма, "КоличествоКолонокПоказателя");
		Если КоличествоКолонок = 0 Тогда
			КоличествоКолонок = 2;
		КонецЕсли;
		
		СчетчикЭлементовСтроки = 1;
		СчетчикСтрок = 1;
		КоличествоСтрок = Цел(СписокВариантовАнализа.Количество()/КоличествоКолонок) + Мин(СписокВариантовАнализа.Количество()% КоличествоКолонок, 1);
		
		НомерЯчейки = 0;
		
		Для Каждого ЭлементСписка Из СписокВариантовАнализа Цикл 
			НомерЯчейки = НомерЯчейки + 1;
			ВариантАнализа = ЭлементСписка.Значение;
			
			ВывестиПоказательНаПечать(ДокументРезультат, Макет, ВариантАнализа, НомерЯчейки);
			
			СчетчикЭлементовСтроки = СчетчикЭлементовСтроки + 1;
			Если СчетчикЭлементовСтроки > КоличествоКолонок Тогда 
				СчетчикЭлементовСтроки = 1;
				СчетчикСтрок = СчетчикСтрок + 1;
				
				// Для последней строки отступ выводить не требуется
				Если СчетчикСтрок <= КоличествоСтрок Тогда
					торо_ПоказателиKPI.ВывестиОтступ(ДокументРезультат, Макет);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		МакетИнтегральнойОценки = ОтчетОбъект.ПолучитьМакет("МакетИнтегральнойОценки");

		// Вывод заголовка и параметров
		торо_ПоказателиKPI.ВывестиЗаголовок(ДокументРезультат, МакетИнтегральнойОценки);
		торо_ПоказателиKPI.ВывестиПараметры(ДокументРезультат, МакетИнтегральнойОценки, ЭтаФорма);
		торо_ПоказателиKPI.ВывестиДатуВремяФормирования(ДокументРезультат, МакетИнтегральнойОценки);
	
		// Вывод показателей
		торо_ПоказателиKPI.ВывестиИнтегральнуюОценкуНаПечать(ДокументРезультат, МакетИнтегральнойОценки, ИнтегральнаяДиаграмма);
		
	КонецЕсли;

	Возврат ДокументРезультат;
	
КонецФункции

Процедура ВывестиПоказательНаПечать(ДокументРезультат, Макет, ВариантАнализа, НомерЯчейки)
	
	ВариантОтображения = ВариантАнализа.ВариантОтображенияПоУмолчанию;
	
	ВременныйДокумент = Новый ТабличныйДокумент;
	торо_ПоказателиKPI.ВывестиЗаголовокПоказателяНаПечать(ВременныйДокумент, Макет, ВариантАнализа);
	
	Если ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Диаграмма Тогда
		ВывестиПоказательНаПечатьДиаграмма(ВременныйДокумент, Макет, ВариантАнализа, НомерЯчейки);
	ИначеЕсли ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Кратко Тогда
		ВывестиПоказательНаПечатьКратко(ВременныйДокумент, Макет, ВариантАнализа, НомерЯчейки);
	ИначеЕсли ВариантОтображения = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Таблица Тогда
		ВывестиПоказательНаПечатьТаблица(ВременныйДокумент, Макет, ВариантАнализа, НомерЯчейки);
	КонецЕсли;
	
	ДокументРезультат.Присоединить(ВременныйДокумент);
	
КонецПроцедуры

Процедура ВывестиПоказательНаПечатьДиаграмма(ДокументРезультат, Макет, ВариантАнализа, НомерЯчейки)
	
	НомерЯчейкиСтрокой = Формат(НомерЯчейки, "ЧГ=0");
	ДиаграммаИсточник = ЭтаФорма["_Результат"+НомерЯчейкиСтрокой];
	
	торо_ПоказателиKPI.ВывестиПоказательНаПечатьДиаграмма(ДокументРезультат, Макет, ДиаграммаИсточник);
	
КонецПроцедуры

Процедура ВывестиПоказательНаПечатьКратко(ДокументРезультат, Макет, ВариантАнализа, НомерЯчейки)
	
	НомерЯчейкиСтрокой = Формат(НомерЯчейки, "ЧГ=0");
	ЭлементРезультат = Элементы["_Результат"+НомерЯчейкиСтрокой];
	
	торо_ПоказателиKPI.ВывестиПоказательНаПечатьКратко(ДокументРезультат, Макет, ЭлементРезультат.Заголовок);
		
КонецПроцедуры

Процедура ВывестиПоказательНаПечатьТаблица(ДокументРезультат, Макет, ВариантАнализа, НомерЯчейки)
	
	НомерЯчейкиСтрокой = Формат(НомерЯчейки, "ЧГ=0");
	РеквизитРезультат = ЭтаФорма["_Результат"+НомерЯчейкиСтрокой];
	
	ТаблицаЗначений = РеквизитРезультат.Выгрузить();
	
	торо_ПоказателиKPI.ВывестиПоказательНаПечатьТаблица(ДокументРезультат, Макет, ВариантАнализа, ЭтаФорма, ТаблицаЗначений);
		
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////
// Интегральная оценка

&НаСервере
Процедура ЗаполнитьИнтегральнуюОценку(СписокВариантовАнализа) 
	
	СвернутаяТабВариантовАнализа = торо_ПоказателиKPI.ПолучитьСписокПоказателейДляИнтегральнойОценки(СписокВариантовАнализа);
	
	Если СвернутаяТабВариантовАнализа.Количество() < 3 Тогда
		Элементы.ИнтегральнаяДиаграмма.Видимость = Ложь;
		Элементы.ПояснениеКИнтегральнойОценке.Видимость = Истина;
		Возврат;
	Иначе
		Элементы.ИнтегральнаяДиаграмма.Видимость = Истина;
		Элементы.ПояснениеКИнтегральнойОценке.Видимость = Ложь;
	КонецЕсли;
	
	торо_ПоказателиKPI.ЗаполнитьИнтегральнуюОценку(ИнтегральнаяДиаграмма, СвернутаяТабВариантовАнализа, ЭтаФорма);	
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////
// Мелкие

&НаСервереБезКонтекста
Функция ПолучитьПараметрыМетаданныхОтчетаРасшифровки(ИмяОтчетаДляРасшифровки, ПараметрыОтчета)
	
	ПараметрыМетаданных = Новый Структура("ЕстьОрганизация, ЕстьПодразделение, ЕстьОбъектРемонта, ЕстьПоказатель, ОтчетСуществует", 
														Ложь, Ложь, Ложь, Ложь, Истина);
	
	МетаданныеОтчета = Метаданные.Отчеты.Найти(ИмяОтчетаДляРасшифровки);
	Если МетаданныеОтчета = Неопределено Тогда
		ПараметрыМетаданных.Вставить("ОтчетСуществует", Ложь);	
		Возврат ПараметрыМетаданных;	
	КонецЕсли;
	
	Если ИмяОтчетаДляРасшифровки = "УниверсальныйОтчет" Тогда
		ПараметрыМетаданных.Вставить("ЕстьОрганизация", Истина);
		ПараметрыМетаданных.Вставить("ЕстьПодразделение", Истина);
		ПараметрыМетаданных.Вставить("ЕстьОбъектРемонта", Истина);
		ПараметрыМетаданных.Вставить("ЕстьПоказатель", Истина);
		
	Иначе

	   ПараметрыМетаданных.Вставить("ЕстьОрганизация", Истина);
		ПараметрыМетаданных.Вставить("ЕстьПодразделение", Истина);
		ПараметрыМетаданных.Вставить("ЕстьОбъектРемонта", Истина);
	
	КонецЕсли;
	
	Возврат ПараметрыМетаданных;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НайтиЭлементФормыПользовательскойНастройкиПоЗаголовку(Форма, Заголовок)
	
	Элементы = Форма.Элементы;
	ЭлементФормы = Неопределено;
	Для каждого Элемент из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") И Элемент.Вид = ВидПоляФормы.ПолеВвода
			И СтрНачинаетсяС(Элемент.Имя, "КомпоновщикНастроекПользовательскиеНастройкиЭлемент")
			И Элемент.Заголовок = Заголовок Тогда
			ЭлементФормы = Элемент;
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат ЭлементФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЗначениеПараметраИзПользовательскихНастроек(Форма, ИмяПараметра, ПреобразоватьКСпискуЗначений = Ложь)
	
	ЗначениеПараметра = Неопределено;
	Для каждого ЭлементНастройки из Форма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если Строка(ЭлементНастройки.Параметр) = ИмяПараметра Тогда
			ЗначениеПараметра = ЭлементНастройки.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеДляВозврата = ЗначениеПараметра;
	
	Если ПреобразоватьКСпискуЗначений И ТипЗнч(ЗначениеПараметра) <> Тип("СписокЗначений") Тогда
		ЗначениеДляВозврата = Новый СписокЗначений;
		Если ЗначениеПараметра <> Неопределено Тогда
			ЗначениеДляВозврата.Добавить(ЗначениеПараметра);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеДляВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИспользованиеПараметраИзПользовательскихНастроек(Форма, ИмяПараметра)
	
	ИспользованиеПараметра = Ложь;
	Для каждого ЭлементНастройки из Форма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если Строка(ЭлементНастройки.Параметр) = ИмяПараметра Тогда
			ИспользованиеПараметра = ЭлементНастройки.Использование;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИспользованиеПараметра;
	
КонецФункции

#КонецОбласти
