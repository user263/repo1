#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипПоказателя = Параметры.ТипПоказателя;
	СписокВариантов.ЗагрузитьЗначения(Параметры.СписокЗначений.ВыгрузитьЗначения());
	
	Если ЗначениеЗаполнено(ТипПоказателя) Тогда
		СписокТиповПоказателяДляОтбора.Добавить(ТипПоказателя);
		Если ТипПоказателя = Перечисления.торо_ТипыПоказателейKPI.ПоказательОрганизации Тогда
			СписокТиповПоказателяДляОтбора.Добавить(Перечисления.торо_ТипыПоказателейKPI.ПоказательПодразделения);
			СписокТиповПоказателяДляОтбора.Добавить(Перечисления.торо_ТипыПоказателейKPI.ПоказательОбъектаРемонта);
		ИначеЕсли ТипПоказателя = Перечисления.торо_ТипыПоказателейKPI.ПоказательПодразделения Тогда
			СписокТиповПоказателяДляОтбора.Добавить(Перечисления.торо_ТипыПоказателейKPI.ПоказательОбъектаРемонта);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОповеститьОВыборе(СписокВариантов);
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ФормаВыбора = ПолучитьФорму("Справочник.торо_ВариантыАнализаПоказателейKPI.ФормаВыбора", ПараметрыФормы, Элементы.СписокВариантов);
	Если СписокТиповПоказателяДляОтбора.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ФормаВыбора.Список, "Владелец.ТипПоказателя", СписокТиповПоказателяДляОтбора);
	КонецЕсли;
	ФормаВыбора.Открыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВариантовЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	
	ФормаВыбора = ПолучитьФорму("Справочник.торо_ВариантыАнализаПоказателейKPI.ФормаВыбора", ПараметрыФормы, Элемент);
	Если СписокТиповПоказателяДляОтбора.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ФормаВыбора.Список, "Владелец.ТипПоказателя", СписокТиповПоказателяДляОтбора);
	КонецЕсли;
	ФормаВыбора.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВариантовЗначениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если СписокТиповПоказателяДляОтбора.Количество() > 0 Тогда
		ПараметрыПолученияДанных.Отбор.Вставить("Владелец", ПолучитьПоказателиПоТипу(СписокТиповПоказателяДляОтбора));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВариантовЗначениеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если СписокТиповПоказателяДляОтбора.Количество() > 0 Тогда
		ПараметрыПолученияДанных.Отбор.Вставить("Владелец", ПолучитьПоказателиПоТипу(СписокТиповПоказателяДляОтбора));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВариантовЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если СписокТиповПоказателяДляОтбора.Количество() > 0 Тогда
		ТипПоказателяВыбранный = ПолучитьТипПоказателя(ВыбранноеЗначение);
		Если СписокТиповПоказателяДляОтбора.НайтиПоЗначению(ТипПоказателяВыбранный) = Неопределено Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) И СписокВариантов.НайтиПоЗначению(ВыбранноеЗначение) <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		торо_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Данное значение уже добавлено.");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВариантовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) И СписокВариантов.НайтиПоЗначению(ВыбранноеЗначение) <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		торо_ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Данное значение уже добавлено.");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьТипПоказателя(ВариантАнализа)
	
	Возврат ВариантАнализа.Владелец.ТипПоказателя;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПоказателиПоТипу(ТипПоказателя)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_ПоказателиKPI.Ссылка
	|ИЗ
	|	Справочник.торо_ПоказателиKPI КАК торо_ПоказателиKPI
	|ГДЕ
	|	торо_ПоказателиKPI.ТипПоказателя В(&ТипПоказателя)";
	
	Запрос.УстановитьПараметр("ТипПоказателя", ТипПоказателя);
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

