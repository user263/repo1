
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ПродолжительностьВСекундах") Тогда
		ПродолжительностьВСекундах = Параметры.ПродолжительностьВСекундах;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьЧасыМинутыСекунды();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОК(Команда)
	
	ОповеститьОВыборе(ПродолжительностьВСекундах);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ЧасыПриИзменении(Элемент)
	
	ПересчитатьПродолжительностьВСекундах();		
	
КонецПроцедуры

&НаКлиенте
Процедура МинутыПриИзменении(Элемент)
	
	ПересчитатьПродолжительностьВСекундах();
	
КонецПроцедуры

&НаКлиенте
Процедура СекундыПриИзменении(Элемент)
	
	ПересчитатьПродолжительностьВСекундах();

КонецПроцедуры

&НаКлиенте
Процедура ПродолжительностьВСекундахПриИзменении(Элемент)
	ЗаполнитьЧасыМинутыСекунды();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПересчитатьПродолжительностьВСекундах()
	
	ПродолжительностьВСекундах = 3600 * Часы + 60 * Минуты + Секунды;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЧасыМинутыСекунды()
	
	Часы    = Цел(ПродолжительностьВСекундах / 3600);
	Минуты  = Цел((ПродолжительностьВСекундах - 3600 * Часы) / 60);
	Секунды = ПродолжительностьВСекундах - 3600 * Часы - Минуты * 60;
	
КонецПроцедуры

#КонецОбласти

