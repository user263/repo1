
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ОбъектКонфигурации") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбъектКонфигурации = Параметры.ОбъектКонфигурации;
	ФорматнаяСтрока = Параметры.ФорматнаяСтрока;
	ЭтаФорма.Заголовок = НСтр("ru = 'Конструктор форматной строки ['")+Параметры.ОбъектКонфигурацииСиноним+"]";
	
	СписокРеквизитов = торо_РаботаСПользовательскимПредставлениемПовтИсп.ПолучитьСписокРеквизитовДляФорматнойСтроки(ОбъектКонфигурации);
	
	Если СписокРеквизитов.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьСписокТэгов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОбъектКонфигурации = "" Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Форма не предназначена для интерактивного открытия!'"));
		Отказ = Истина;
	ИначеЕсли СписокРеквизитов.Количество() = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Тип объекта конфигурации не определен.'"));
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура кнДобавитьТегНажатие(Команда)
	
	ИмяТега = "%" + Команда.Имя + "%";
	Элементы.ФорматнаяСтрока.ВыделенныйТекст = ИмяТега;
		
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	ОповеститьОВыборе(ФорматнаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокТэгов()
	
	Для Каждого ЭлементСписка Из СписокРеквизитов Цикл
		ДобавитьКоманду(ЭлементСписка.Значение,ЭлементСписка.Представление,Элементы.КомандыТэгов);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ДобавитьКоманду (ИмяКоманды, ЗаголовокКоманды, Группа)
	НовКнопкаМеню = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Группа);
	НовКнопкаМеню.Вид        = ВидКнопкиФормы.КнопкаКоманднойПанели;
	НовКнопкаМеню.Заголовок  = ЗаголовокКоманды;

	НовКоманда = ЭтаФорма.Команды.Добавить(ИмяКоманды);
	НовКоманда.Действие      = "кнДобавитьТегНажатие";
	НовКоманда.Заголовок     = ЗаголовокКоманды;
	
	НовКнопкаМеню.ИмяКоманды = ИмяКоманды;	
КонецПроцедуры

#КонецОбласти