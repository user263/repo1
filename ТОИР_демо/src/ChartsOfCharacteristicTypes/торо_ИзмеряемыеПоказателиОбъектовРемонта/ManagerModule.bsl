#Область ПрограммныйИнтерфейс

// Функция предназначена для получения всех измеряемых показателей,
//	назначенных объекту ремонта (включая наследуемые из типового ОР).
//
//	Параметры:
//		СписокОР - СправочникСсылка.торо_ОбъектыРемонта, СписокЗначений, Массив - список ОР, для которых нужно получить показатели.
//		СписокПоказателей - ПланВидовХарактеристикСсылка.торо_ИзмеряемыеПоказателиОбъектовРемонта, 
//							СписокЗначений, Массив - список необходимых показателей (по умолчанию - Неопределено, все показатели).
//		ПолучатьГраничныеЗначения - Булево - признак для получения граничных значений показателя (по умолчанию - Ложь).
//		НеУчитыватьОР - Булево - не учитывать граничные значения, заданные для ОР индивидуально.
//		ТаблицаТиповыхОР - ТаблицаЗначений - таблица с типовыми ОР для выбранных ОР.
//
// Возвращаемое значение:
//		Массив - массив структур с данными об измеряемых показателях.
//
Функция ПолучитьСтруктуруИзмеряемыхПоказателейОбъектовРемонта(СписокОР,СписокПоказателей = Неопределено,ПолучатьГраничныеЗначения = Ложь, НеУчитыватьОР = Ложь, ТаблицаТиповыхОР = Неопределено) Экспорт
	
	Если ТаблицаТиповыхОР = Неопределено Тогда
		ЗапросТиповых = Новый Запрос;
		ЗапросТиповых.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	торо_ОбъектыРемонта.ТиповойОР
		|ИЗ
		|	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|ГДЕ
		|	торо_ОбъектыРемонта.Ссылка В(&Ссылка)
		|	И торо_ОбъектыРемонта.ТиповойОР <> ЗНАЧЕНИЕ(Справочник.торо_ТиповыеОР.ПустаяСсылка)";
		
		ЗапросТиповых.УстановитьПараметр("Ссылка", СписокОР);
		СписокТиповых = ЗапросТиповых.Выполнить().Выгрузить().ВыгрузитьКолонку("ТиповойОР");
		
		ТаблицаРодителейТиповых = торо_ОбщегоНазначения.ПолучитьТаблицуРодителейСпискаОбъектов(СписокТиповых, Истина, Тип("СправочникСсылка.торо_ТиповыеОР"));
		
		ЗапросТабТиповыхОР = Новый Запрос;
		ЗапросТабТиповыхОР.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаРодителей.Родитель,
		|	ТаблицаРодителей.Уровень,
		|	ТаблицаРодителей.Объект
		|ПОМЕСТИТЬ ТаблицаРодителейТиповыхОР
		|ИЗ
		|	&ТаблицаРодителей КАК ТаблицаРодителей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта,
		|	ТаблицаРодителейТиповыхОР.Родитель,
		|	ТаблицаРодителейТиповыхОР.Объект,
		|	ТаблицаРодителейТиповыхОР.Уровень
		|ИЗ
		|	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаРодителейТиповыхОР КАК ТаблицаРодителейТиповыхОР
		|		ПО торо_ОбъектыРемонта.ТиповойОР = ТаблицаРодителейТиповыхОР.Объект
		|ГДЕ
		|	торо_ОбъектыРемонта.Ссылка В(&СписокОР)";

		ЗапросТабТиповыхОР.УстановитьПараметр("СписокОР",СписокОР);
		ЗапросТабТиповыхОР.УстановитьПараметр("ТаблицаРодителей", ТаблицаРодителейТиповых);
		
		ТаблицаТиповыхОР = ЗапросТабТиповыхОР.Выполнить().Выгрузить();
		
	КонецЕсли;
			
	Если НЕ ПолучатьГраничныеЗначения Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаТиповыхОР.ОбъектРемонта КАК ОбъектРемонта,
		|	ТаблицаТиповыхОР.Родитель КАК ТиповойОР,
		|	ТаблицаТиповыхОР.Уровень
		|ПОМЕСТИТЬ ТаблицаТиповыхОР
		|ИЗ
		|	&ТаблицаТиповыхОР КАК ТаблицаТиповыхОР
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель,
		|	ИСТИНА КАК Приоритет,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Удален,
		|	-1 КАК Уровень
		|ПОМЕСТИТЬ ПоказателиОРИТиповогоОР
		|ИЗ
		|	РегистрСведений.торо_ИзмеряемыеПоказателиОбъектовРемонта КАК торо_ИзмеряемыеПоказателиОбъектовРемонта
		|
		|ГДЕ
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта В(&СписокОР)
		|	И торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель В(&СписокПоказателей)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТиповыхОР.ОбъектРемонта,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель,
		|	ЛОЖЬ,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Удален,
		|	ТаблицаТиповыхОР.Уровень
		|ИЗ
		|	РегистрСведений.торо_ИзмеряемыеПоказателиОбъектовРемонта КАК торо_ИзмеряемыеПоказателиОбъектовРемонта
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТиповыхОР КАК ТаблицаТиповыхОР
		|		ПО ((ВЫРАЗИТЬ(торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта КАК Справочник.торо_ТиповыеОР)) = ТаблицаТиповыхОР.ТиповойОР)
		|ГДЕ
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель В(&СписокПоказателей)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПриоритетовПолная.ОбъектРемонта,
		|	ТаблицаПриоритетовПолная.Показатель,
		|	НЕ МАКСИМУМ(ТаблицаПриоритетовПолная.Приоритет) КАК ИзТиповогоОР,
		|	МАКСИМУМ(ТаблицаПриоритетовПолная.Удален) КАК Удален
		|ИЗ
		|	ПоказателиОРИТиповогоОР КАК ТаблицаПриоритетовПолная
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПриоритетовПолная.ОбъектРемонта,
		|	ТаблицаПриоритетовПолная.Показатель
		|
		|ИМЕЮЩИЕ
		|	МАКСИМУМ(ТаблицаПриоритетовПолная.Удален) = ЛОЖЬ";

		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаТиповыхОР.ОбъектРемонта КАК ОбъектРемонта,
		|	ТаблицаТиповыхОР.Родитель КАК ТиповойОР,
		|	ТаблицаТиповыхОР.Уровень
		|ПОМЕСТИТЬ ТаблицаТиповыхОР
		|ИЗ
		|	&ТаблицаТиповыхОР КАК ТаблицаТиповыхОР
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаТиповыхОР.ОбъектРемонта КАК ОбъектРемонта,
		|	ТаблицаТиповыхОР.Объект КАК ТиповойОР
		|ПОМЕСТИТЬ ТаблицаТиповыхОР_Краткая
		|ИЗ
		|	&ТаблицаТиповыхОР КАК ТаблицаТиповыхОР
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектРемонта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА &НеУчитыватьОР = ЛОЖЬ
		|			ТОГДА торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМинимум
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДопустимыйМинимум,
		|	ВЫБОР
		|		КОГДА &НеУчитыватьОР = ЛОЖЬ
		|			ТОГДА торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМаксимум
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДопустимыйМаксимум,
		|	ВЫБОР
		|		КОГДА &НеУчитыватьОР = ЛОЖЬ
		|			ТОГДА торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМинимум
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КритическийМинимум,
		|	ВЫБОР
		|		КОГДА &НеУчитыватьОР = ЛОЖЬ
		|			ТОГДА торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМаксимум
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КритическийМаксимум,
		|	ВЫБОР
		|		КОГДА &НеУчитыватьОР = ЛОЖЬ
		|			ТОГДА торо_ИзмеряемыеПоказателиОбъектовРемонта.ЗначенияКонтролируемыхПоказателейИзОР
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЗначенияКонтролируемыхПоказателейИзОР,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ТочкаЗамера,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта КАК ОбъектРемонта,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель КАК Показатель,
		|	ТаблицаТиповыхОР_Краткая.ТиповойОР КАК ТиповойОР,
		|	1 КАК Приоритет,
		|	ЛОЖЬ КАК ИзТиповогоОР,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Удален,
		|	-1 КАК Уровень
		|ПОМЕСТИТЬ ПоказателиОРИТиповогоОР
		|ИЗ
		|	РегистрСведений.торо_ИзмеряемыеПоказателиОбъектовРемонта КАК торо_ИзмеряемыеПоказателиОбъектовРемонта
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТиповыхОР_Краткая КАК ТаблицаТиповыхОР_Краткая
		|		ПО торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта = ТаблицаТиповыхОР_Краткая.ОбъектРемонта
		|ГДЕ
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта В(&СписокОР)
		|	И торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель В(&СписокПоказателей)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМинимум,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМаксимум,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМинимум,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМаксимум,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ЗначенияКонтролируемыхПоказателейИзОР,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ТочкаЗамера,
		|	ТаблицаТиповыхОР.ОбъектРемонта,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель,
		|	ТаблицаТиповыхОР.ТиповойОР,
		|	0,
		|	ИСТИНА,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Удален,
		|	ТаблицаТиповыхОР.Уровень
		|ИЗ
		|	РегистрСведений.торо_ИзмеряемыеПоказателиОбъектовРемонта КАК торо_ИзмеряемыеПоказателиОбъектовРемонта
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТиповыхОР КАК ТаблицаТиповыхОР
		|		ПО ((ВЫРАЗИТЬ(торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта КАК Справочник.торо_ТиповыеОР)) = ТаблицаТиповыхОР.ТиповойОР)
		|ГДЕ
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель В(&СписокПоказателей)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектРемонта,
		|	Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиОРИТиповогоОР.ОбъектРемонта КАК ОбъектРемонта,
		|	ПоказателиОРИТиповогоОР.Показатель КАК Показатель,
		|	МИНИМУМ(ПоказателиОРИТиповогоОР.Уровень) КАК Уровень
		|ПОМЕСТИТЬ ВТ_ПриоритетыПоУровню
		|ИЗ
		|	ПоказателиОРИТиповогоОР КАК ПоказателиОРИТиповогоОР
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоказателиОРИТиповогоОР.ОбъектРемонта,
		|	ПоказателиОРИТиповогоОР.Показатель
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектРемонта,
		|	Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиОРИТиповогоОР.ДопустимыйМинимум,
		|	ПоказателиОРИТиповогоОР.ДопустимыйМаксимум,
		|	ПоказателиОРИТиповогоОР.КритическийМинимум,
		|	ПоказателиОРИТиповогоОР.КритическийМаксимум,
		|	ПоказателиОРИТиповогоОР.ЗначенияКонтролируемыхПоказателейИзОР,
		|	ПоказателиОРИТиповогоОР.ТочкаЗамера,
		|	ПоказателиОРИТиповогоОР.ОбъектРемонта,
		|	ПоказателиОРИТиповогоОР.Показатель,
		|	ПоказателиОРИТиповогоОР.ТиповойОР,
		|	ПоказателиОРИТиповогоОР.Приоритет,
		|	ПоказателиОРИТиповогоОР.ИзТиповогоОР,
		|	ПоказателиОРИТиповогоОР.Удален,
		|	ПоказателиОРИТиповогоОР.Уровень
		|ПОМЕСТИТЬ ВТ_ПриоритетныеПоказатели
		|ИЗ
		|	ПоказателиОРИТиповогоОР КАК ПоказателиОРИТиповогоОР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПриоритетыПоУровню КАК ВТ_ПриоритетыПоУровню
		|		ПО ПоказателиОРИТиповогоОР.ОбъектРемонта = ВТ_ПриоритетыПоУровню.ОбъектРемонта
		|			И ПоказателиОРИТиповогоОР.Показатель = ВТ_ПриоритетыПоУровню.Показатель
		|			И ПоказателиОРИТиповогоОР.Уровень = ВТ_ПриоритетыПоУровню.Уровень
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПриоритетовПолная.ОбъектРемонта,
		|	ТаблицаПриоритетовПолная.Показатель,
		|	МАКСИМУМ(ТаблицаПриоритетовПолная.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ТаблицаПриоритетов
		|ИЗ
		|	ВТ_ПриоритетныеПоказатели КАК ТаблицаПриоритетовПолная
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПриоритетовПолная.ОбъектРемонта,
		|	ТаблицаПриоритетовПолная.Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиОРИТиповогоОР.Показатель,
		|	ВЫБОР
		|		КОГДА ПоказателиОРИТиповогоОР.ИзТиповогоОР
		|			ТОГДА ПоказателиОРИТиповогоОР.ТиповойОР
		|		ИНАЧЕ ПоказателиОРИТиповогоОР.ОбъектРемонта
		|	КОНЕЦ КАК ОбъектРемонта,
		|	ПоказателиОРИТиповогоОР.ТиповойОР,
		|	ВЫБОР
		|		КОГДА ПоказателиОРИТиповогоОР.ЗначенияКонтролируемыхПоказателейИзОР
		|			ТОГДА ПоказателиОРИТиповогоОР.ДопустимыйМинимум
		|		ИНАЧЕ торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМинимум
		|	КОНЕЦ КАК ДопустимыйМинимум,
		|	ВЫБОР
		|		КОГДА ПоказателиОРИТиповогоОР.ЗначенияКонтролируемыхПоказателейИзОР
		|			ТОГДА ПоказателиОРИТиповогоОР.ДопустимыйМаксимум
		|		ИНАЧЕ торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМаксимум
		|	КОНЕЦ КАК ДопустимыйМаксимум,
		|	ВЫБОР
		|		КОГДА ПоказателиОРИТиповогоОР.ЗначенияКонтролируемыхПоказателейИзОР
		|			ТОГДА ПоказателиОРИТиповогоОР.КритическийМинимум
		|		ИНАЧЕ торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМинимум
		|	КОНЕЦ КАК КритическийМинимум,
		|	ВЫБОР
		|		КОГДА ПоказателиОРИТиповогоОР.ЗначенияКонтролируемыхПоказателейИзОР
		|			ТОГДА ПоказателиОРИТиповогоОР.КритическийМаксимум
		|		ИНАЧЕ торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМаксимум
		|	КОНЕЦ КАК КритическийМаксимум,
		|	ПоказателиОРИТиповогоОР.ЗначенияКонтролируемыхПоказателейИзОР,
		|	ПоказателиОРИТиповогоОР.ТочкаЗамера,
		|	ПоказателиОРИТиповогоОР.Удален,
		|	торо_ИзмеряемыеПоказателиОбъектовРемонта.ЕдиницаИзмерения,
		|	ПоказателиОРИТиповогоОР.ИзТиповогоОР
		|ИЗ
		|	ВТ_ПриоритетныеПоказатели КАК ПоказателиОРИТиповогоОР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПриоритетов КАК ТаблицаПриоритетов
		|		ПО ПоказателиОРИТиповогоОР.ОбъектРемонта = ТаблицаПриоритетов.ОбъектРемонта
		|			И ПоказателиОРИТиповогоОР.Показатель = ТаблицаПриоритетов.Показатель
		|			И ПоказателиОРИТиповогоОР.Приоритет = ТаблицаПриоритетов.Приоритет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.торо_ИзмеряемыеПоказателиОбъектовРемонта КАК торо_ИзмеряемыеПоказателиОбъектовРемонта
		|		ПО ПоказателиОРИТиповогоОР.Показатель = торо_ИзмеряемыеПоказателиОбъектовРемонта.Ссылка
		|ГДЕ
		|	НЕ ПоказателиОРИТиповогоОР.Удален";

	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Если СписокПоказателей <> Неопределено Тогда
		Запрос.УстановитьПараметр("СписокПоказателей",СписокПоказателей);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель В(&СписокПоказателей)","Истина");
	КонецЕсли;
	Запрос.УстановитьПараметр("СписокОР",СписокОР);
	Запрос.УстановитьПараметр("НеУчитыватьОР",НеУчитыватьОР);
	Запрос.УстановитьПараметр("ТаблицаТиповыхОР", ТаблицаТиповыхОР);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Возврат торо_ОбщегоНазначения.РезультатЗапросаВМассивСтруктур(Результат);
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
КонецФункции

#КонецОбласти